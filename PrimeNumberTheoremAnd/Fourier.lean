import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.Topology.ContinuousFunction.Bounded
import Mathlib.Order.Filter.ZeroAndBoundedAtFilter
import Mathlib.Analysis.Fourier.Inversion

import PrimeNumberTheoremAnd.Mathlib.Analysis.Fourier.FourierTransformDeriv

open FourierTransform Real Complex MeasureTheory Filter Topology BoundedContinuousFunction SchwartzMap VectorFourier BigOperators

local instance {E : Type*} : Coe (E â†’ â„) (E â†’ â„‚) := âŸ¨fun f n => f nâŸ©

@[simp]
theorem nnnorm_eq_of_mem_circle (z : circle) : â€–z.valâ€–â‚Š = 1 := NNReal.coe_eq_one.mp (by simp)

@[simp]
theorem nnnorm_circle_smul (z : circle) (s : â„‚) : â€–z â€¢ sâ€–â‚Š = â€–sâ€–â‚Š := by
  simp [show z â€¢ s = z.val * s from rfl]

noncomputable def e (u : â„) : â„ â†’áµ‡ â„‚ where
  toFun v := ğ (-v * u)
  map_bounded' := âŸ¨2, fun x y => (dist_le_norm_add_norm _ _).trans (by simp [one_add_one_eq_two])âŸ©

@[simp] lemma e_apply (u : â„) (v : â„) : e u v = ğ (-v * u) := rfl

theorem hasDerivAt_e {u x : â„} : HasDerivAt (e u) (-2 * Ï€ * u * I * e u x) x := by
  have l2 : HasDerivAt (fun v => -v * u) (-u) x := by simpa only [neg_mul_comm] using hasDerivAt_mul_const (-u)
  convert (hasDerivAt_fourierChar (-x * u)).scomp x l2 using 1
  simp ; ring

lemma fourierIntegral_deriv_aux2 (e : â„ â†’áµ‡ â„‚) {f : â„ â†’ â„‚} (hf : Integrable f) : Integrable (â‡‘e * f) :=
  hf.bdd_mul e.continuous.aestronglyMeasurable âŸ¨_, e.norm_coe_le_normâŸ©

@[simp] lemma F_neg {f : â„ â†’ â„‚} {u : â„} : ğ“• (fun x => -f x) u = - ğ“• f u := by
  simp [fourierIntegral_eq, integral_neg]

@[simp] lemma F_add {f g : â„ â†’ â„‚} (hf : Integrable f) (hg : Integrable g) (x : â„) :
    ğ“• (fun x => f x + g x) x = ğ“• f x + ğ“• g x :=
  congr_fun (fourierIntegral_add continuous_fourierChar (by exact continuous_mul) hf hg).symm x

@[simp] lemma F_sub {f g : â„ â†’ â„‚} (hf : Integrable f) (hg : Integrable g) (x : â„) :
    ğ“• (fun x => f x - g x) x = ğ“• f x - ğ“• g x := by
  simp_rw [sub_eq_add_neg] ; rw [F_add] ; simp ; exact hf ; exact hg.neg

@[simp] lemma F_mul {f : â„ â†’ â„‚} {c : â„‚} {u : â„} : ğ“• (fun x => c * f x) u = c * ğ“• f u := by
  simp [fourierIntegral_real_eq, â† integral_mul_left] ; congr ; ext
  simp [Real.fourierChar, expMapCircle] ; ring

lemma contDiff_ofReal : ContDiff â„ âŠ¤ ofReal' := by
  have key x : HasDerivAt ofReal' 1 x := hasDerivAt_id x |>.ofReal_comp
  have key' : deriv ofReal' = fun _ => 1 := by ext x ; exact (key x).deriv
  refine contDiff_top_iff_deriv.mpr âŸ¨fun x => (key x).differentiableAt, ?_âŸ©
  simpa [key'] using contDiff_const

structure W21 where
  toFun : â„ â†’ â„‚
  hh : ContDiff â„ 2 toFun
  hf : Integrable toFun
  hf' : Integrable (deriv toFun)
  hf'' : Integrable (deriv (deriv toFun))

instance : CoeFun W21 (fun _ => â„ â†’ â„‚) where coe := W21.toFun

structure CS2 (E : Type*) [NormedAddCommGroup E] [NormedSpace â„ E] where
  toFun : â„ â†’ E
  h1 : ContDiff â„ 2 toFun
  h2 : HasCompactSupport toFun

instance {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] : CoeFun (CS2 E) (fun _ => â„ â†’ E) where coe := CS2.toFun

lemma CS2.continuous {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (f : CS2 E) : Continuous f :=
  f.h1.continuous

lemma CS2.hasDerivAt {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (f : CS2 E) (x : â„) :
    HasDerivAt f (deriv f x) x :=
  (f.h1.differentiable one_le_two).differentiableAt.hasDerivAt

structure trunc extends (CS2 â„) where
  h3 : (Set.Icc (-1) (1)).indicator 1 â‰¤ toFun
  h4 : toFun â‰¤ Set.indicator (Set.Ioo (-2) (2)) 1

instance : CoeFun trunc (fun _ => â„ â†’ â„) where coe f := f.toFun

instance : Coe trunc (CS2 â„) where coe f := âŸ¨fun x => f x, f.h1, f.h2âŸ©

instance : Coe (CS2 â„) (CS2 â„‚) where coe f := âŸ¨f,
  contDiff_ofReal.of_le le_top |>.comp f.h1, f.h2.comp_left (g := ofReal') rflâŸ©

noncomputable def funscale {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (g : â„ â†’ E) (R x : â„) : E :=
    g (Râ»Â¹ â€¢ x)

noncomputable def CS2.scale {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (g : CS2 E) (R : â„) : CS2 E := by
  by_cases h : R = 0
  Â· exact âŸ¨0, contDiff_const, by simp [HasCompactSupport, tsupport]âŸ©
  Â· refine âŸ¨fun x => funscale g R x, ?_, ?_âŸ©
    Â· exact g.h1.comp (contDiff_const.smul contDiff_id)
    Â· exact g.h2.comp_smul (inv_ne_zero h)

lemma hasDerivAt_scale {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (f : CS2 E) (R x : â„) :
    HasDerivAt (f.scale R) (Râ»Â¹ â€¢ deriv f (Râ»Â¹ â€¢ x)) x := by
  by_cases h : R = 0 <;> simp [CS2.scale, h]
  Â· exact hasDerivAt_const _ _
  Â· exact (f.hasDerivAt (Râ»Â¹ â€¢ x)).scomp x (by simpa using (hasDerivAt_id x).const_smul Râ»Â¹)

namespace W21

variable {f : W21}

def sub (f g : W21) : W21 := by
  have l1 : deriv (â‡‘f - â‡‘g) = deriv f - deriv g := by
    ext x ; apply deriv_sub
    Â· exact (f.hh.differentiable one_le_two).differentiableAt
    Â· exact (g.hh.differentiable one_le_two).differentiableAt
  have l2 : deriv (deriv (â‡‘f - â‡‘g)) = deriv (deriv f) - deriv (deriv g) := by
    rw [l1] ; ext x ; apply deriv_sub
    Â· exact (f.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt
    Â· exact (g.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt
  refine âŸ¨f - g, f.hh.sub g.hh, f.hf.sub g.hf, ?_, ?_âŸ©
  Â· simpa [l1] using f.hf'.sub g.hf'
  Â· simpa [l2] using f.hf''.sub g.hf''

instance : HSub W21 W21 W21 where hSub := sub

noncomputable def norm (f : â„ â†’ â„‚) : â„ :=
    (âˆ« v, â€–f vâ€–) + (4 * Ï€ ^ 2)â»Â¹ * (âˆ« v, â€–deriv (deriv f) vâ€–)

lemma norm_nonneg {f : â„ â†’ â„‚} : 0 â‰¤ norm f :=
  add_nonneg (integral_nonneg (fun t => by simp))
    (mul_nonneg (by positivity) (integral_nonneg (fun t => by simp)))

noncomputable instance : Norm W21 where norm := norm âˆ˜ toFun

noncomputable def of_Schwartz (f : ğ“¢(â„, â„‚)) : W21 where
  toFun := f
  hh := f.smooth 2
  hf := f.integrable
  hf' := (SchwartzMap.derivCLM â„ f).integrable
  hf'' := (SchwartzMap.derivCLM â„ (SchwartzMap.derivCLM â„ f)).integrable

noncomputable instance : Coe ğ“¢(â„, â„‚) W21 where coe := of_Schwartz

def ofCS2 (f : CS2 â„‚) : W21 where
  toFun := f
  hh := f.h1
  hf := f.h1.continuous.integrable_of_hasCompactSupport f.h2
  hf' := (f.h1.continuous_deriv one_le_two).integrable_of_hasCompactSupport f.h2.deriv
  hf'' := (f.h1.iterate_deriv' 0 2).continuous.integrable_of_hasCompactSupport f.h2.deriv.deriv

instance : Coe (CS2 â„‚) W21 where coe := ofCS2

end W21

theorem fourierIntegral_self_add_deriv_deriv (f : W21) (u : â„) :
    (1 + u ^ 2) * ğ“• f u = ğ“• (fun u => f u - (1 / (4 * Ï€ ^ 2)) * deriv^[2] f u) u := by
  have l1 : Integrable (fun x => (((Ï€ : â„‚) ^ 2)â»Â¹ * 4â»Â¹) * deriv (deriv f) x) := (f.hf''.const_mul _)
  have l4 : Differentiable â„ f := f.hh.differentiable one_le_two
  have l5 : Differentiable â„ (deriv f) := (f.hh.iterate_deriv' 1 1).differentiable le_rfl
  simp [f.hf, l1, add_mul, Real.fourierIntegral_deriv f.hf' l5 f.hf'',
    Real.fourierIntegral_deriv f.hf l4 f.hf']
  field_simp [pi_ne_zero] ; ring_nf ; simp

instance : HMul (CS2 â„‚) W21 (CS2 â„‚) where hMul g f := âŸ¨g * f, g.h1.mul f.hh, g.h2.mul_rightâŸ©

instance : HMul (CS2 â„) W21 (CS2 â„‚) where hMul g f := (g : CS2 â„‚) * f

theorem W21_approximation (f : W21) (g : trunc) :
    Tendsto (fun R => â€–f - (g.scale R * f : W21)â€–) atTop (ğ“ 0) := by

  -- Preliminaries
  have cR {R : â„} : Continuous (fun v => v * Râ»Â¹) := continuous_id.mul continuous_const
  have vR v : Tendsto (fun R : â„ => v * Râ»Â¹) atTop (ğ“ 0) := by simpa using tendsto_inv_atTop_zero.const_mul v

  -- About f
  let f' v := deriv f v
  let f'' v := deriv (deriv f) v
  have cf : Continuous f := f.hh.continuous
  have cf' : Continuous f' := (f.hh.iterate_deriv' 1 1).continuous
  have cf'' : Continuous f'' := (f.hh.iterate_deriv' 0 2).continuous
  have df v : HasDerivAt f (f' v) v := f.hh.differentiable one_le_two |>.differentiableAt.hasDerivAt
  have df' v : HasDerivAt f' (f'' v) v := (f.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt.hasDerivAt

  -- About g
  let g' := deriv g
  let g'' v := deriv (deriv g) v
  have cg' : Continuous g' := (g.h1.iterate_deriv' 1 1).continuous
  have cg'' : Continuous g'' := (g.h1.iterate_deriv' 0 2).continuous
  have dg' v : HasDerivAt g' (g'' v) v := (g.h1.iterate_deriv' 1 1).hasStrictDerivAt le_rfl |>.hasDerivAt
  have mg' : âˆƒ c1, âˆ€ v, |g' v| â‰¤ c1 := by
    obtain âŸ¨x, hxâŸ© := cg'.abs.exists_forall_ge_of_hasCompactSupport g.h2.deriv.norm ; exact âŸ¨_, hxâŸ©
  have mg'' : âˆƒ c2, âˆ€ v, |g'' v| â‰¤ c2 := by
    obtain âŸ¨x, hxâŸ© := cg''.abs.exists_forall_ge_of_hasCompactSupport g.h2.deriv.deriv.norm ; exact âŸ¨_, hxâŸ©
  obtain âŸ¨c1, mg'âŸ© := mg' ; obtain âŸ¨c2, mg''âŸ© := mg''

  have g0 v : 0 â‰¤ g v := by have := g.h3 v ; by_cases h : v âˆˆ Set.Icc (-1) 1 <;> simp [h] at this <;> linarith
  have g1 v : g v â‰¤ 1 := by have := g.h4 v ; by_cases h : v âˆˆ Set.Ioo (-2) 2 <;> simp [h] at this <;> linarith
  have evg : g =á¶ [ğ“ 0] 1 := by
    have : Set.Icc (-1) 1 âˆˆ ğ“ (0 : â„) := by apply Icc_mem_nhds <;> linarith
    exact eventually_of_mem this (fun x hx => le_antisymm (g1 x) (by simpa [hx] using g.h3 x))
  have evg' : g' =á¶ [ğ“ 0] 0 := by convert â† evg.deriv ; exact deriv_const' _
  have evg'' : g'' =á¶ [ğ“ 0] 0 := by convert â† evg'.deriv ; exact deriv_const' _

  -- About h
  let h R v := 1 - g.scale R v
  let h' R v := - g' (v * Râ»Â¹) * Râ»Â¹
  let h'' R v := - g'' (v * Râ»Â¹) * Râ»Â¹ * Râ»Â¹
  have ch {R} : Continuous (fun v => (h R v : â„‚)) := by
    apply continuous_ofReal.comp
    apply continuous_const.sub
    apply CS2.continuous
  have ch' {R} : Continuous (fun v => (h' R v : â„‚)) := continuous_ofReal.comp <| (cg'.comp cR).neg.mul continuous_const
  have ch'' {R} : Continuous (fun v => (h'' R v : â„‚)) :=
    continuous_ofReal.comp <| ((cg''.comp cR).neg.mul continuous_const).mul continuous_const
  have dh R v : HasDerivAt (h R) (h' R v) v := by
    convert hasDerivAt_scale (E := â„) g R v |>.const_sub 1 using 1 ; simp [h', g'] ; ring_nf
  have dh' R v : HasDerivAt (h' R) (h'' R v) v := by
    simpa [h', h''] using HasDerivAt.mul_const ((dg' _).comp _ <| hasDerivAt_mul_const _).neg (Râ»Â¹)
  have hc1 : âˆ€á¶  R in atTop, âˆ€ v, |h' R v| â‰¤ c1 := by
    filter_upwards [eventually_ge_atTop 1] with R hR v
    have : 0 â‰¤ R := by linarith
    simp [h', abs_mul, abs_inv, abs_eq_self.mpr this]
    convert_to _ â‰¤ c1 * 1 ; simp
    apply mul_le_mul (mg' _) (inv_le_of_inv_le (by linarith) (by simpa using hR)) (by positivity)
    exact (abs_nonneg _).trans (mg' 0)
  have hc2 : âˆ€á¶  R in atTop, âˆ€ v, |h'' R v| â‰¤ c2 := by
    filter_upwards [eventually_ge_atTop 1] with R hR v
    have e1 : 0 â‰¤ R := by linarith
    have e2 : Râ»Â¹ â‰¤ 1 := inv_le_of_inv_le (by linarith) (by simpa using hR)
    simp [h'', abs_mul, abs_inv, abs_eq_self.mpr e1, mul_assoc]
    convert_to _ â‰¤ c2 * (1 * 1) ; simp
    apply mul_le_mul (mg'' _) ?_ (by positivity) ((abs_nonneg _).trans (mg'' 0))
    exact mul_le_mul e2 e2 (by positivity) zero_le_one

  have h0 R v : 0 â‰¤ h R v := by by_cases hR : R = 0 <;> simp [hR, h, CS2.scale, funscale, g1]
  have h1 R v : h R v â‰¤ 1 := by by_cases hR : R = 0 <;> simp [hR, h, CS2.scale, funscale, g0]
  have hh1 R v : |h R v| â‰¤ 1 := by rw [abs_le] ; constructor <;> linarith [h0 R v, h1 R v]
  have eh v : âˆ€á¶  R in atTop, h R v = 0 := by
    filter_upwards [(vR v).eventually evg, eventually_ne_atTop 0] with R hR hR'
    simp [h, hR, CS2.scale, hR', funscale, mul_comm Râ»Â¹]
  have eh' v : âˆ€á¶  R in atTop, h' R v = 0 := by filter_upwards [(vR v).eventually evg'] with R hR ; simp [h', hR]
  have eh'' v : âˆ€á¶  R in atTop, h'' R v = 0 := by filter_upwards [(vR v).eventually evg''] with R hR ; simp [h'', hR]

  -- Computations
  have l16 R v : deriv (deriv (fun v => h R v * f v)) v = h'' R v * f v + 2 * h' R v * f' v + h R v * f'' v := by
    have l3 v : HasDerivAt (fun v => h R v * f v) (h' R v * f v + h R v * f' v) v := (dh R v).ofReal_comp.mul (df v)
    have l5 : HasDerivAt (fun v => h' R v * f v) (h'' R v * f v + h' R v * f' v) v := (dh' R v).ofReal_comp.mul (df v)
    have l7 : HasDerivAt (fun v => h R v * f' v) (h' R v * f' v + h R v * f'' v) v := (dh R v).ofReal_comp.mul (df' v)
    have d1 : deriv (fun v => h R v * f v) = fun v => h' R v * f v + h R v * f' v := funext (fun v => (l3 v).deriv)
    rw [d1] ; convert (l5.add l7).deriv using 1 ; ring

  -- Proof
  convert_to Tendsto (fun R => W21.norm (fun v => h R v * f v)) atTop (ğ“ 0)
  Â· ext R ; change W21.norm _ = _ ; congr ; ext v ; simp [h, sub_mul] ; rfl
  rw [show (0 : â„) = 0 + ((4 * Ï€ ^ 2)â»Â¹ : â„) * 0 by simp]
  refine Tendsto.add ?_ (Tendsto.const_mul _ ?_)
  Â· let F R v := â€–h R v * f vâ€–
    have e1 : âˆ€á¶  (n : â„) in atTop, AEStronglyMeasurable (F n) volume := by
      apply eventually_of_forall ; intro R
      exact (ch.mul f.hh.continuous).norm.aestronglyMeasurable
    have e2 : âˆ€á¶  (n : â„) in atTop, âˆ€áµ (a : â„), â€–F n aâ€– â‰¤ â€–f aâ€– := by
      apply eventually_of_forall ; intro R
      apply eventually_of_forall ; intro v
      simpa [F] using mul_le_mul (hh1 R v) le_rfl (by simp) zero_le_one
    have e4 : âˆ€áµ (a : â„), Tendsto (fun n â†¦ F n a) atTop (ğ“ 0) := by
      apply eventually_of_forall ; intro v
      apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh v] with R hR ; simp [F, hR]
    simpa [F] using tendsto_integral_filter_of_dominated_convergence _ e1 e2 f.hf.norm e4
  Â· simp_rw [l16]
    let F R v := â€–h'' R v * f v + 2 * h' R v * f' v + h R v * f'' vâ€–
    let bound v := c2 * â€–f vâ€– + 2 * c1 * â€–f' vâ€– + â€–f'' vâ€–
    have e1 : âˆ€á¶  (n : â„) in atTop, AEStronglyMeasurable (F n) volume := by
      apply eventually_of_forall ; intro R
      exact (((ch''.mul cf).add ((continuous_const.mul ch').mul cf')).add (ch.mul cf'')).norm.aestronglyMeasurable
    have e2 : âˆ€á¶  (n : â„) in atTop, âˆ€áµ (a : â„), â€–F n aâ€– â‰¤ bound a := by
      filter_upwards [hc1, hc2] with R hc1 hc2
      apply eventually_of_forall ; intro v ; specialize hc1 v ; specialize hc2 v
      simp only [F, bound, norm_norm]
      refine (norm_add_le _ _).trans ?_ ; apply add_le_add
      Â· refine (norm_add_le _ _).trans ?_ ; apply add_le_add <;> simp <;> gcongr
      Â· simpa using mul_le_mul (hh1 R v) le_rfl (by simp) zero_le_one
    have e3 : Integrable bound volume := (((f.hf.norm).const_mul _).add ((f.hf'.norm).const_mul _)).add f.hf''.norm
    have e4 : âˆ€áµ (a : â„), Tendsto (fun n â†¦ F n a) atTop (ğ“ 0) := by
      apply eventually_of_forall ; intro v
      refine tendsto_norm_zero.comp <| (ZeroAtFilter.add ?_ ?_).add ?_
      Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh'' v] with R hR ; simp [hR]
      Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh' v] with R hR ; simp [hR]
      Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh v] with R hR ; simp [hR]
    simpa [F] using tendsto_integral_filter_of_dominated_convergence bound e1 e2 e3 e4

@[simp] lemma deriv_ofReal : deriv ofReal' = fun _ => 1 := by
  ext x ; exact ((hasDerivAt_id x).ofReal_comp).deriv

theorem bla (a : â„‚) (f : â„ â†’ â„‚) (n : â„•) (hf : ContDiff â„ n f) :
    iteratedDeriv n (fun x â†¦ a * x * f x) = fun x =>
      a * x * iteratedDeriv n f x + n * a * iteratedDeriv (n - 1) f x := by

  induction n with
  | zero => simp
  | succ n ih =>
    have l0 : ContDiff â„ n f := hf.of_succ
    rw [iteratedDeriv_succ, ih l0] ; ext x
    have l5 : ContDiff â„ (â†‘(1 + n)) f := by convert hf using 1 ; simp ; ring
    have l4 : DifferentiableAt â„ (fun x â†¦ iteratedDeriv n f x) x := by
      have := ((l5.iterate_deriv' 1 n).differentiable le_rfl).differentiableAt (x := x)
      simpa [iteratedDeriv_eq_iterate] using this
    have l3 : DifferentiableAt â„ (fun x â†¦ a * â†‘x) x := by
      apply DifferentiableAt.const_mul
      exact (contDiff_ofReal.differentiable le_top).differentiableAt
    have l1 : DifferentiableAt â„ (fun x â†¦ a * â†‘x * iteratedDeriv n f x) x := l3.mul l4
    have l2 : DifferentiableAt â„ (fun x â†¦ â†‘n * a * iteratedDeriv (n - 1) f x) x := by
      apply DifferentiableAt.const_mul
      apply l5.differentiable_iteratedDeriv
      norm_cast ; exact Nat.sub_le _ _ |>.trans_lt (by simp)
    simp [deriv_add l1 l2, deriv_mul l3 l4, â† iteratedDeriv_succ]
    cases n <;> simp <;> ring

noncomputable def MS (a : â„‚) (f : ğ“¢(â„, â„‚)) : ğ“¢(â„, â„‚) where
  toFun x := a * x * f x
  smooth' := contDiff_const.mul contDiff_ofReal |>.mul f.smooth'
  decay' k n := by
    simp only [norm_iteratedFDeriv_eq_norm_iteratedDeriv]
    simp_rw [bla a f n <| f.smooth'.of_le le_top]
    obtain âŸ¨Câ‚, hCâ‚âŸ© := f.decay' (k + 1) n
    obtain âŸ¨Câ‚‚, hCâ‚‚âŸ© := f.decay' k (n - 1)
    use â€–aâ€– * Câ‚ + â€–aâ€– * n * Câ‚‚ ; intro x
    have l2 := norm_add_le (a * x * iteratedDeriv n f x) (n * a * iteratedDeriv (n - 1) f x)
    have l3 : 0 â‰¤ â€–xâ€– ^ k := by positivity
    apply (mul_le_mul_of_nonneg_left l2 l3).trans ; rw [mul_add] ; apply add_le_add
    Â· have : 0 â‰¤ â€–aâ€– := by positivity
      convert mul_le_mul_of_nonneg_left (hCâ‚ x) this using 1
      simp [norm_iteratedFDeriv_eq_norm_iteratedDeriv, abs_eq_self.mpr pi_nonneg] ; ring_nf ; rfl
    Â· have : 0 â‰¤ â€–aâ€– * n := by positivity
      convert mul_le_mul_of_nonneg_left (hCâ‚‚ x) this using 1
      simp [norm_iteratedFDeriv_eq_norm_iteratedDeriv, abs_eq_self.mpr pi_nonneg] ; ring_nf ; rfl

@[simp] lemma MS_apply (a : â„‚) (f : ğ“¢(â„, â„‚)) (x : â„) : MS a f x = (a * x) â€¢ f x := rfl

lemma MS_iterate (a : â„‚) (f : ğ“¢(â„, â„‚)) (n : â„•) : (MS a)^[n] f = fun x : â„ => (a * x) ^ n â€¢ f x := by
  induction n generalizing f with
  | zero => simp
  | succ n ih => ext x ; simp [ih, pow_succ] ; ring

lemma fourierIntegral_decay_aux (f : â„ â†’ â„‚) (k : â„•) (h1 : ContDiff â„ k f)
    (h2 : âˆ€ n â‰¤ k, Integrable (iteratedDeriv n f)) (x : â„) :
    â€–(2 * Ï€ * I * x) ^ k â€¢ ğ“• f xâ€– â‰¤ (âˆ« y : â„, â€–iteratedDeriv k f yâ€–) := by
  have l2 (x : â„) : (2 * Ï€ * I * x) ^ k â€¢ ğ“• f x = ğ“• (iteratedDeriv k f) x := by
    simp [Real.fourierIntegral_iteratedDeriv h1 (fun n hn => h2 n <| Nat.cast_le.mp hn) le_rfl]
  simpa only [l2] using Fourier.norm_fourierIntegral_le_integral_norm ..

lemma iteratedDeriv_schwartz (f : ğ“¢(â„, â„‚)) (n : â„•) : iteratedDeriv n f = (SchwartzMap.derivCLM â„)^[n] f := by
  induction n with
  | zero => rfl
  | succ n ih => rw [iteratedDeriv_succ, ih, Function.iterate_succ'] ; rfl

theorem fourierIntegral_decay (f : ğ“¢(â„, â„‚)) (k : â„•) : âˆƒ C, âˆ€ (x : â„), â€–xâ€– ^ k * â€–ğ“• f xâ€– â‰¤ C := by
  convert_to âˆƒ C, âˆ€ x : â„, â€–x ^ k * ğ“• f xâ€– â‰¤ C ; Â· simp
  convert_to âˆƒ C, âˆ€ x : â„, â€–(2 * Ï€ * I * x) ^ k * ğ“• f xâ€– / (2 * Ï€) ^ k â‰¤ C using 4
  Â· field_simp [mul_pow, abs_eq_self.mpr pi_nonneg] ; ring
  convert_to âˆƒ C, âˆ€ x : â„, â€–(2 * Ï€ * I * x) ^ k â€¢ ğ“• f xâ€– / (2 * Ï€) ^ k â‰¤ C
  use (âˆ« (y : â„), â€–iteratedDeriv k (â‡‘f) yâ€–) / (2 * Ï€) ^ k ; intro x
  have l1 : âˆ€ n â‰¤ k, Integrable (iteratedDeriv n f) volume := by
    simp_rw [iteratedDeriv_schwartz] ; simp [SchwartzMap.integrable]
  have := fourierIntegral_decay_aux f k (f.smooth'.of_le le_top) l1 x
  apply div_le_div_of_nonneg_right this (by positivity)

noncomputable def FS (f : ğ“¢(â„, â„‚)) : ğ“¢(â„, â„‚) where
  toFun := ğ“• f
  smooth' := by
    rw [contDiff_top] ; intro n
    apply Real.contDiff_fourierIntegral ; intro k _
    apply SchwartzMap.integrable_pow_mul
  decay' := by
    simp only [norm_iteratedFDeriv_eq_norm_iteratedDeriv]
    intro k n
    have l1 (k : â„•) (_ : k â‰¤ (n : â„•âˆ)) : Integrable (fun x â†¦ x ^ k â€¢ f x) volume := by
      convert_to Integrable ((MS 1)^[k] f) ; Â· simp [MS_iterate]
      apply SchwartzMap.integrable
    simp_rw [@Real.iteratedDeriv_fourierIntegral â„‚ _ _ f n n l1 le_rfl]
    convert_to âˆƒ C, âˆ€ (x : â„), â€–xâ€– ^ k * â€–ğ“• ((MS (-2 * Ï€ * I))^[n] f) xâ€– â‰¤ C ; Â· simp [MS_iterate]
    apply fourierIntegral_decay

@[simp] lemma FS_apply (f : ğ“¢(â„, â„‚)) (x : â„) : FS f x = ğ“• f x := rfl

@[simp] lemma FS_toFun (f : ğ“¢(â„, â„‚)) : â‡‘(FS f) = ğ“• f := rfl

@[simp] lemma schwarz_reduce (f : â„ â†’ â„‚) h1 h2 x : SchwartzMap.mk f h1 h2 x = f x := rfl

theorem fourierfourier {f : â„ â†’ â„‚} (hfi : Integrable f) (hfi' : Integrable (ğ“• f))
    (hfc : Continuous f) (x : â„) :
    ğ“• (ğ“• f) x = f (-x) := by
  rw [â† MeasureTheory.Integrable.fourier_inversion (v := -x) hfi hfi' hfc.continuousAt]
  simp [fourierIntegralInv, Real.fourierIntegral, VectorFourier.fourierIntegral]

lemma FS4 (f : ğ“¢(â„, â„‚)) : FS^[4] f = f := by
  have li0 : Integrable (â‡‘f) volume := f.integrable
  have li1 : Integrable (ğ“• â‡‘f) := (FS f).integrable
  have li2 : Integrable (ğ“• (ğ“• â‡‘f)) := (FS (FS f)).integrable
  have li3 : Integrable (ğ“• (ğ“• (ğ“• â‡‘f))) volume := (FS (FS (FS f))).integrable
  have lc2 : Continuous (ğ“• (ğ“• â‡‘f)) := (FS (FS f)).continuous
  ext x ; change ğ“• (ğ“• (ğ“• (ğ“• f))) x = f x
  rw [fourierfourier li2 li3 lc2, fourierfourier li0 li1 f.continuous]
  simp
