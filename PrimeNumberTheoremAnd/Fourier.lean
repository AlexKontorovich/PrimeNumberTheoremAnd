import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.Topology.ContinuousFunction.Bounded
import Mathlib.Order.Filter.ZeroAndBoundedAtFilter

import PrimeNumberTheoremAnd.Mathlib.Analysis.Fourier.FourierTransformDeriv

open FourierTransform Real Complex MeasureTheory Filter Topology BoundedContinuousFunction SchwartzMap VectorFourier

@[simp]
theorem nnnorm_eq_of_mem_circle (z : circle) : â€–z.valâ€–â‚Š = 1 := NNReal.coe_eq_one.mp (by simp)

@[simp]
theorem nnnorm_circle_smul (z : circle) (s : â„‚) : â€–z â€¢ sâ€–â‚Š = â€–sâ€–â‚Š := by
  simp [show z â€¢ s = z.val * s from rfl]

noncomputable def e (u : â„) : â„ â†’áµ‡ â„‚ where
  toFun v := ğ (-v * u)
  map_bounded' := âŸ¨2, fun x y => (dist_le_norm_add_norm _ _).trans (by simp [one_add_one_eq_two])âŸ©

@[simp] lemma e_apply (u : â„) (v : â„) : e u v = ğ (-v * u) := rfl

theorem hasDerivAt_e {u x : â„} : HasDerivAt (e u) (-2 * Ï€ * u * I * e u x) x := by
  have l2 : HasDerivAt (fun v => -v * u) (-u) x := by simpa only [neg_mul_comm] using hasDerivAt_mul_const (-u)
  convert (hasDerivAt_fourierChar (-x * u)).scomp x l2 using 1
  simp ; ring

lemma fourierIntegral_deriv_aux2 (e : â„ â†’áµ‡ â„‚) {f : â„ â†’ â„‚} (hf : Integrable f) : Integrable (â‡‘e * f) :=
  hf.bdd_mul e.continuous.aestronglyMeasurable âŸ¨_, e.norm_coe_le_normâŸ©

@[simp] lemma F_neg {f : â„ â†’ â„‚} {u : â„} : ğ“• (fun x => -f x) u = - ğ“• f u := by
  simp [fourierIntegral_eq, integral_neg]

@[simp] lemma F_add {f g : â„ â†’ â„‚} (hf : Integrable f) (hg : Integrable g) (x : â„) :
    ğ“• (fun x => f x + g x) x = ğ“• f x + ğ“• g x :=
  congr_fun (fourierIntegral_add continuous_fourierChar (by exact continuous_mul) hf hg).symm x

@[simp] lemma F_sub {f g : â„ â†’ â„‚} (hf : Integrable f) (hg : Integrable g) (x : â„) :
    ğ“• (fun x => f x - g x) x = ğ“• f x - ğ“• g x := by
  simp_rw [sub_eq_add_neg] ; rw [F_add] ; simp ; exact hf ; exact hg.neg

@[simp] lemma F_mul {f : â„ â†’ â„‚} {c : â„‚} {u : â„} : ğ“• (fun x => c * f x) u = c * ğ“• f u := by
  simp [fourierIntegral_real_eq, â† integral_mul_left] ; congr ; ext
  simp [Real.fourierChar, expMapCircle] ; ring

structure W21 (f : â„ â†’ â„‚) : Prop where
  hh : ContDiff â„ 2 f
  hf : Integrable f
  hf' : Integrable (deriv f)
  hf'' : Integrable (deriv (deriv f))
  h3 : Tendsto f (cocompact â„) (ğ“ 0)
  h4 : Tendsto (deriv f) (cocompact â„) (ğ“ 0)

lemma W21.sub {f g : â„ â†’ â„‚} (hf : W21 f) (hg : W21 g) : W21 (f - g) := by
  have l1 : deriv (f - g) = deriv f - deriv g := by
    ext x ; apply deriv_sub
    Â· exact (hf.hh.differentiable one_le_two).differentiableAt
    Â· exact (hg.hh.differentiable one_le_two).differentiableAt
  have l2 : deriv (deriv (f - g)) = deriv (deriv f) - deriv (deriv g) := by
    rw [l1] ; ext x ; apply deriv_sub
    Â· exact (hf.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt
    Â· exact (hg.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt
  refine âŸ¨hf.hh.sub hg.hh, hf.hf.sub hg.hf, ?_, ?_, ?_, ?_âŸ©
  Â· simpa [l1] using hf.hf'.sub hg.hf'
  Â· simpa [l2] using hf.hf''.sub hg.hf''
  Â· simpa using hf.h3.sub hg.h3
  Â· simpa [l1] using hf.h4.sub hg.h4

noncomputable def W21.norm (f : â„ â†’ â„‚) : â„ := (âˆ« v, â€–f vâ€–) + (4 * Ï€ ^ 2)â»Â¹ * (âˆ« v, â€–deriv (deriv f) vâ€–)

lemma W21.norm_nonneg {f : â„ â†’ â„‚} : 0 â‰¤ W21.norm f :=
  add_nonneg (integral_nonneg (fun t => by simp)) (mul_nonneg (by positivity) (integral_nonneg (fun t => by simp)))

noncomputable def W21_of_schwartz (f : ğ“¢(â„, â„‚)) : W21 f where
  hh := f.smooth 2
  hf := f.integrable
  hf' := (SchwartzMap.derivCLM â„ f).integrable
  hf'' := (SchwartzMap.derivCLM â„ (SchwartzMap.derivCLM â„ f)).integrable
  h3 := f.toZeroAtInfty.zero_at_infty'
  h4 := (SchwartzMap.derivCLM â„ f).toZeroAtInfty.zero_at_infty'

noncomputable def W21_of_compactSupport {f : â„ â†’ â„‚} (h1 : ContDiff â„ 2 f) (h2 : HasCompactSupport f) : W21 f where
  hh := h1
  hf := h1.continuous.integrable_of_hasCompactSupport h2
  hf' := (h1.continuous_deriv one_le_two).integrable_of_hasCompactSupport h2.deriv
  hf'' := (h1.iterate_deriv' 0 2).continuous.integrable_of_hasCompactSupport h2.deriv.deriv
  h3 := h2.is_zero_at_infty
  h4 := h2.deriv.is_zero_at_infty

theorem fourierIntegral_self_add_deriv_deriv {f : â„ â†’ â„‚} (hf : W21 f) (u : â„) :
    (1 + u ^ 2) * ğ“• f u = ğ“• (fun u => f u - (1 / (4 * Ï€ ^ 2)) * deriv^[2] f u) u := by
  have l1 : Integrable (fun x => (((Ï€ : â„‚) ^ 2)â»Â¹ * 4â»Â¹) * deriv (deriv f) x) := (hf.hf''.const_mul _)
  have l4 : Differentiable â„ f := hf.hh.differentiable one_le_two
  have l5 : Differentiable â„ (deriv f) := (hf.hh.iterate_deriv' 1 1).differentiable le_rfl
  simp [hf.hf, l1, add_mul, Real.fourierIntegral_deriv hf.hf' l5 hf.hf'', Real.fourierIntegral_deriv hf.hf l4 hf.hf']
  field_simp [pi_ne_zero] ; ring_nf ; simp

structure trunc (g : â„ â†’ â„) : Prop :=
  h1 : ContDiff â„ âŠ¤ g
  h2 : HasCompactSupport g
  h3 : (Set.Icc (-1) (1)).indicator 1 â‰¤ g
  h4 : g â‰¤ Set.indicator (Set.Ioo (-2) (2)) 1

lemma W21.mul_compact_support {f g : â„ â†’ â„‚} (hf : W21 f) (hg1 : ContDiff â„ 2 g) (hg2 : HasCompactSupport g) :
    W21 (fun x => g x * f x) := by

  let f' := deriv f
  let f'' := deriv (deriv f)
  have f_d x : HasDerivAt f (f' x) x := hf.hh.differentiable one_le_two |>.differentiableAt.hasDerivAt
  have f_i : Integrable f := hf.hf
  have f'_d x : HasDerivAt f' (f'' x) x := (hf.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt.hasDerivAt
  have f'_i : Integrable f' := hf.hf'
  have f''_i : Integrable f'' := hf.hf''

  let g' := deriv g
  let g'' := deriv (deriv g)
  have g_0 : g =á¶ [cocompact â„] 0 := by simpa using hasCompactSupport_iff_eventuallyEq.mp hg2
  have g_c : Continuous g := hg1.continuous
  have g_b : âˆƒ C, âˆ€ x, â€–g xâ€– â‰¤ C := g_c.bounded_above_of_compact_support hg2
  have g_d x : HasDerivAt g (g' x) x := hg1.differentiable one_le_two |>.differentiableAt.hasDerivAt
  have g_a : AEStronglyMeasurable g volume := g_c.aestronglyMeasurable
  have g'_0 : g' =á¶ [cocompact â„] 0 := by simpa using hasCompactSupport_iff_eventuallyEq.mp hg2.deriv
  have g'_c : Continuous g' := hg1.continuous_deriv one_le_two
  have g'_d x : HasDerivAt g' (g'' x) x := (hg1.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt.hasDerivAt
  have g'_a : AEStronglyMeasurable g' volume := g'_c.aestronglyMeasurable
  have g'_b : âˆƒ C, âˆ€ x, â€–g' xâ€– â‰¤ C := g'_c.bounded_above_of_compact_support hg2.deriv
  have g''_c : Continuous g'' := hg1.iterate_deriv' 0 2 |>.continuous
  have g''_a : AEStronglyMeasurable g'' volume := g''_c.aestronglyMeasurable
  have g''_b : âˆƒ C, âˆ€ x, â€–g'' xâ€– â‰¤ C := g''_c.bounded_above_of_compact_support hg2.deriv.deriv

  let h := fun x => g x * f x
  let h' := fun x => g' x * f x + g x * f' x
  let h'' := fun x => g'' x * f x + 2 * g' x * f' x + g x * f'' x
  have h_d x : HasDerivAt h (h' x) x := (g_d x).mul (f_d x)
  have h_d' : deriv h = h' := funext (fun x => (h_d x).deriv)
  have h'_d x : HasDerivAt h' (h'' x) x := by
    convert ((g'_d x).mul (f_d x)).add ((g_d x).mul (f'_d x)) using 1 ; simp [h', h''] ; ring
  have h'_d' : deriv h' = h'' := funext (fun x => (h'_d x).deriv)

  refine âŸ¨hg1.mul hf.hh, ?_, ?_, ?_, ?_, ?_âŸ©
  Â· exact hf.hf.bdd_mul g_c.aestronglyMeasurable g_b
  Â· rw [h_d'] ; exact (f_i.bdd_mul g'_a g'_b).add (f'_i.bdd_mul g_a g_b)
  Â· rw [h_d', h'_d'] ; refine Integrable.add ?_ (f''_i.bdd_mul g_a g_b)
    apply (f_i.bdd_mul g''_a g''_b).add
    simp_rw [mul_assoc] ; apply (f'_i.bdd_mul g'_a g'_b).const_mul
  Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [g_0] with x hgx ; simp [hgx]
  Â· simp only [h_d', h'] ; apply tendsto_nhds_of_eventually_eq
    filter_upwards [g_0, g'_0] with x hgx hg'x; simp [hgx, hg'x]

theorem W21_approximation {f : â„ â†’ â„‚} (hf : W21 f) {g : â„ â†’ â„} (hg : trunc g) :
    Tendsto (fun R => W21.norm (fun v => (1 - g (v * Râ»Â¹)) * f v)) atTop (ğ“ 0) := by

  -- Preliminaries
  have cR {R : â„} : Continuous (fun v => v * Râ»Â¹) := continuous_id.mul continuous_const
  have vR v : Tendsto (fun R : â„ => v * Râ»Â¹) atTop (ğ“ 0) := by simpa using tendsto_inv_atTop_zero.const_mul v

  -- About f
  let f' v := deriv f v
  let f'' v := deriv (deriv f) v
  have cf : Continuous f := hf.hh.continuous
  have cf' : Continuous f' := (hf.hh.iterate_deriv' 1 1).continuous
  have cf'' : Continuous f'' := (hf.hh.iterate_deriv' 0 2).continuous
  have df v : HasDerivAt f (f' v) v := hf.hh.differentiable one_le_two |>.differentiableAt.hasDerivAt
  have df' v : HasDerivAt f' (f'' v) v := (hf.hh.iterate_deriv' 1 1).differentiable le_rfl |>.differentiableAt.hasDerivAt

  -- About g
  let g' := deriv g
  let g'' v := deriv (deriv g) v
  have cg : Continuous g := hg.h1.continuous
  have cg' : Continuous g' := (hg.h1.iterate_deriv 1).continuous
  have cg'' : Continuous g'' := (hg.h1.iterate_deriv 2).continuous
  have dg v : HasDerivAt g (g' v) v := hg.h1.hasStrictDerivAt le_top |>.hasDerivAt
  have dg' v : HasDerivAt g' (g'' v) v := (hg.h1.iterate_deriv 1).hasStrictDerivAt le_top |>.hasDerivAt
  have mg' : âˆƒ c1, âˆ€ v, |g' v| â‰¤ c1 := by
    obtain âŸ¨x, hxâŸ© := cg'.abs.exists_forall_ge_of_hasCompactSupport hg.h2.deriv.norm ; exact âŸ¨_, hxâŸ©
  have mg'' : âˆƒ c2, âˆ€ v, |g'' v| â‰¤ c2 := by
    obtain âŸ¨x, hxâŸ© := cg''.abs.exists_forall_ge_of_hasCompactSupport hg.h2.deriv.deriv.norm ; exact âŸ¨_, hxâŸ©
  obtain âŸ¨c1, mg'âŸ© := mg' ; obtain âŸ¨c2, mg''âŸ© := mg''

  have g0 v : 0 â‰¤ g v := by have := hg.h3 v ; by_cases h : v âˆˆ Set.Icc (-1) 1 <;> simp [h] at this <;> linarith
  have g1 v : g v â‰¤ 1 := by have := hg.h4 v ; by_cases h : v âˆˆ Set.Ioo (-2) 2 <;> simp [h] at this <;> linarith
  have evg : g =á¶ [ğ“ 0] 1 := by
    have : Set.Icc (-1) 1 âˆˆ ğ“ (0 : â„) := by apply Icc_mem_nhds <;> linarith
    exact eventually_of_mem this (fun x hx => le_antisymm (g1 x) (by simpa [hx] using hg.h3 x))
  have evg' : g' =á¶ [ğ“ 0] 0 := by convert â† evg.deriv ; exact deriv_const' _
  have evg'' : g'' =á¶ [ğ“ 0] 0 := by convert â† evg'.deriv ; exact deriv_const' _

  -- About h
  let h R v := 1 - g (v * Râ»Â¹)
  let h' R v := - g' (v * Râ»Â¹) * Râ»Â¹
  let h'' R v := - g'' (v * Râ»Â¹) * Râ»Â¹ * Râ»Â¹
  have ch {R} : Continuous (fun v => (h R v : â„‚)) := continuous_ofReal.comp <| continuous_const.sub <| cg.comp cR
  have ch' {R} : Continuous (fun v => (h' R v : â„‚)) := continuous_ofReal.comp <| (cg'.comp cR).neg.mul continuous_const
  have ch'' {R} : Continuous (fun v => (h'' R v : â„‚)) :=
    continuous_ofReal.comp <| ((cg''.comp cR).neg.mul continuous_const).mul continuous_const
  have dh R v : HasDerivAt (h R) (h' R v) v := by
    simpa [h, h'] using ((dg _).comp _ <| hasDerivAt_mul_const _).const_sub _
  have dh' R v : HasDerivAt (h' R) (h'' R v) v := by
    simpa [h', h''] using HasDerivAt.mul_const ((dg' _).comp _ <| hasDerivAt_mul_const _).neg (Râ»Â¹)
  have hc1 : âˆ€á¶  R in atTop, âˆ€ v, |h' R v| â‰¤ c1 := by
    filter_upwards [eventually_ge_atTop 1] with R hR v
    have : 0 â‰¤ R := by linarith
    simp [h', abs_mul, abs_inv, abs_eq_self.mpr this]
    convert_to _ â‰¤ c1 * 1 ; simp
    apply mul_le_mul (mg' _) (inv_le_of_inv_le (by linarith) (by simpa using hR)) (by positivity)
    exact (abs_nonneg _).trans (mg' 0)
  have hc2 : âˆ€á¶  R in atTop, âˆ€ v, |h'' R v| â‰¤ c2 := by
    filter_upwards [eventually_ge_atTop 1] with R hR v
    have e1 : 0 â‰¤ R := by linarith
    have e2 : Râ»Â¹ â‰¤ 1 := inv_le_of_inv_le (by linarith) (by simpa using hR)
    simp [h'', abs_mul, abs_inv, abs_eq_self.mpr e1, mul_assoc]
    convert_to _ â‰¤ c2 * (1 * 1) ; simp
    apply mul_le_mul (mg'' _) ?_ (by positivity) ((abs_nonneg _).trans (mg'' 0))
    exact mul_le_mul e2 e2 (by positivity) zero_le_one

  have h0 R v : 0 â‰¤ h R v := by simpa [h] using g1 _
  have h1 R v : h R v â‰¤ 1 := by simpa [h] using g0 _
  have hh1 R v : |h R v| â‰¤ 1 := by rw [abs_le] ; constructor <;> linarith [h0 R v, h1 R v]
  have eh v : âˆ€á¶  R in atTop, h R v = 0 := by filter_upwards [(vR v).eventually evg] with R hR ; simp [h, hR]
  have eh' v : âˆ€á¶  R in atTop, h' R v = 0 := by filter_upwards [(vR v).eventually evg'] with R hR ; simp [h', hR]
  have eh'' v : âˆ€á¶  R in atTop, h'' R v = 0 := by filter_upwards [(vR v).eventually evg''] with R hR ; simp [h'', hR]

  -- Computations
  have l16 R v : deriv (deriv (fun v => h R v * f v)) v = h'' R v * f v + 2 * h' R v * f' v + h R v * f'' v := by
    have l3 v : HasDerivAt (fun v => h R v * f v) (h' R v * f v + h R v * f' v) v := (dh R v).ofReal_comp.mul (df v)
    have l5 : HasDerivAt (fun v => h' R v * f v) (h'' R v * f v + h' R v * f' v) v := (dh' R v).ofReal_comp.mul (df v)
    have l7 : HasDerivAt (fun v => h R v * f' v) (h' R v * f' v + h R v * f'' v) v := (dh R v).ofReal_comp.mul (df' v)
    have d1 : deriv (fun v => h R v * f v) = fun v => h' R v * f v + h R v * f' v := funext (fun v => (l3 v).deriv)
    rw [d1] ; convert (l5.add l7).deriv using 1 ; ring

  -- Proof
  convert_to Tendsto (fun R => W21.norm (fun v => h R v * f v)) atTop (ğ“ 0) ; simp [h]
  rw [show (0 : â„) = 0 + ((4 * Ï€ ^ 2)â»Â¹ : â„) * 0 by simp]
  refine Tendsto.add ?_ (Tendsto.const_mul _ ?_)
  Â· let F R v := â€–h R v * f vâ€–
    have e1 : âˆ€á¶  (n : â„) in atTop, AEStronglyMeasurable (F n) volume := by
      apply eventually_of_forall ; intro R
      exact (ch.mul hf.hh.continuous).norm.aestronglyMeasurable
    have e2 : âˆ€á¶  (n : â„) in atTop, âˆ€áµ (a : â„), â€–F n aâ€– â‰¤ â€–f aâ€– := by
      apply eventually_of_forall ; intro R
      apply eventually_of_forall ; intro v
      simpa [F] using mul_le_mul (hh1 R v) le_rfl (by simp) zero_le_one
    have e4 : âˆ€áµ (a : â„), Tendsto (fun n â†¦ F n a) atTop (ğ“ 0) := by
      apply eventually_of_forall ; intro v
      apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh v] with R hR ; simp [F, hR]
    simpa [F] using tendsto_integral_filter_of_dominated_convergence _ e1 e2 hf.hf.norm e4
  Â· simp_rw [l16]
    let F R v := â€–h'' R v * f v + 2 * h' R v * f' v + h R v * f'' vâ€–
    let bound v := c2 * â€–f vâ€– + 2 * c1 * â€–f' vâ€– + â€–f'' vâ€–
    have e1 : âˆ€á¶  (n : â„) in atTop, AEStronglyMeasurable (F n) volume := by
      apply eventually_of_forall ; intro R
      exact (((ch''.mul cf).add ((continuous_const.mul ch').mul cf')).add (ch.mul cf'')).norm.aestronglyMeasurable
    have e2 : âˆ€á¶  (n : â„) in atTop, âˆ€áµ (a : â„), â€–F n aâ€– â‰¤ bound a := by
      filter_upwards [hc1, hc2] with R hc1 hc2
      apply eventually_of_forall ; intro v ; specialize hc1 v ; specialize hc2 v
      simp only [F, bound, norm_norm]
      refine (norm_add_le _ _).trans ?_ ; apply add_le_add
      Â· refine (norm_add_le _ _).trans ?_ ; apply add_le_add <;> simp <;> gcongr
      Â· simpa using mul_le_mul (hh1 R v) le_rfl (by simp) zero_le_one
    have e3 : Integrable bound volume := (((hf.hf.norm).const_mul _).add ((hf.hf'.norm).const_mul _)).add hf.hf''.norm
    have e4 : âˆ€áµ (a : â„), Tendsto (fun n â†¦ F n a) atTop (ğ“ 0) := by
      apply eventually_of_forall ; intro v
      refine tendsto_norm_zero.comp <| (ZeroAtFilter.add ?_ ?_).add ?_
      Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh'' v] with R hR ; simp [hR]
      Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh' v] with R hR ; simp [hR]
      Â· apply tendsto_nhds_of_eventually_eq ; filter_upwards [eh v] with R hR ; simp [hR]
    simpa [F] using tendsto_integral_filter_of_dominated_convergence bound e1 e2 e3 e4

-- Things we should use, most of them from SÃ©bastien GouÃ«zel:
-- Real.iteratedDeriv_fourierIntegral
-- Real.fourierIntegral_iteratedDeriv
-- contDiff_fourierIntegral

noncomputable def FS (f : ğ“¢(â„, â„‚)) : ğ“¢(â„, â„‚) where
  toFun := ğ“• f
  smooth' := sorry
  decay' := sorry
