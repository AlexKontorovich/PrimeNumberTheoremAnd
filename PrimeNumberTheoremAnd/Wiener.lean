import EulerProducts.PNT
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Mathlib.NumberTheory.ArithmeticFunction
import Mathlib.Topology.Support
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Geometry.Manifold.PartitionOfUnity
import Mathlib.Tactic.FunProp.AEMeasurable
import Mathlib.Tactic.FunProp.Measurable

open Nat Real BigOperators ArithmeticFunction MeasureTheory Filter Set FourierTransform
open Complex hiding log
-- note: the opening of ArithmeticFunction introduces a notation œÉ that seems impossible to hide, and hence parameters that are traditionally called œÉ will have to be called œÉ' instead in this file.

open scoped Topology

-- This version makes the support of Œ® explicit, and this is easier for some later proofs
lemma smooth_urysohn_support_Ioo {a b c d : ‚Ñù} (h1 : a < b) (h3: c < d) :
    ‚àÉ Œ® : ‚Ñù ‚Üí ‚Ñù, (‚àÄ n, ContDiff ‚Ñù n Œ®) ‚àß (HasCompactSupport Œ®) ‚àß Set.indicator (Set.Icc b c) 1 ‚â§ Œ® ‚àß
    Œ® ‚â§ Set.indicator (Set.Ioo a d) 1 ‚àß (Function.support Œ® = Set.Ioo a d) := by

  have := exists_msmooth_zero_iff_one_iff_of_isClosed
    (modelWithCornersSelf ‚Ñù ‚Ñù) (s := Set.Iic a ‚à™ Set.Ici d) (t := Set.Icc b c)
    (IsClosed.union isClosed_Iic isClosed_Ici)
    (isClosed_Icc)
    (by
      simp_rw [Set.disjoint_union_left, Set.disjoint_iff, Set.subset_def, Set.mem_inter_iff, Set.mem_Iic, Set.mem_Icc,
        Set.mem_empty_iff_false, and_imp, imp_false, not_le, Set.mem_Ici]
      constructor <;> intros <;> linarith)

  rcases this with ‚ü®Œ®, hŒ®Smooth, hŒ®range, hŒ®0, hŒ®1‚ü©

  simp only [Set.EqOn, Set.mem_setOf_eq, Set.mem_union, Set.mem_Iic, Set.mem_Ici,
    ContMDiffMap.coeFn_mk, Pi.zero_apply, Set.mem_Icc, Pi.one_apply, and_imp] at *
  use Œ®
  constructor
  ¬∑ rw [contDiff_all_iff_nat, ‚ÜêcontDiff_top]
    exact ContMDiff.contDiff hŒ®Smooth
  ¬∑ constructor
    ¬∑ rw [hasCompactSupport_def]
      apply IsCompact.closure_of_subset (K := Set.Icc a d) isCompact_Icc
      simp_rw [Function.support_subset_iff, ne_eq, <-hŒ®0]
      intro x hx
      contrapose! hx
      simp only [Set.mem_Icc, not_and_or] at hx
      by_contra! h'
      cases' hx <;> linarith
    ¬∑ constructor
      ¬∑ intro x
        rw [Set.indicator_apply]
        split_ifs with h
        ¬∑ simp only [Set.mem_Icc, Pi.one_apply] at *
          simp_rw [hŒ®1 x] at h
          exact Eq.le (_root_.id h.symm)
        ¬∑ have : Œ® x ‚àà Set.range Œ® := by simp only [Set.mem_range, exists_apply_eq_apply]
          have : Œ® x ‚àà Set.Icc 0 1 := hŒ®range this
          exact this.left
      ¬∑ constructor
        ¬∑ intro x
          rw [Set.indicator_apply]
          split_ifs with h
          ¬∑ have : Œ® x ‚àà Set.range Œ® := by simp only [Set.mem_range, exists_apply_eq_apply]
            have : Œ® x ‚àà Set.Icc 0 1 := by exact hŒ®range this
            simpa using this.2
          ¬∑ simp only [Set.mem_Ioo, Pi.one_apply] at *
            simp only [not_and_or, not_lt] at h
            simp_rw [hŒ®0 x] at h
            exact Eq.le h
        ¬∑ simp_rw [Function.support, ne_eq, ‚ÜêhŒ®0]
          push_neg
          simp [Set.ext_iff]
  done


/-%%
The Fourier transform of an absolutely integrable function $\psi: \R \to \C$ is defined by the formula
$$ \hat \psi(u) := \int_\R e(-tu) \psi(t)\ dt$$
where $e(\theta) := e^{2\pi i \theta}$.

Let $f: \N \to \C$ be an arithmetic function such that $\sum_{n=1}^\infty \frac{|f(n)|}{n^\sigma} < \infty$ for all $\sigma>1$.  Then the Dirichlet series
$$ F(s) := \sum_{n=1}^\infty \frac{f(n)}{n^s}$$
is absolutely convergent for $\sigma>1$.
%%-/

variable {f: ArithmeticFunction ‚Ñù} (hf: ‚àÄ (œÉ':‚Ñù), 1 < œÉ' ‚Üí Summable (fun n ‚Ü¶ |f n| / n^œÉ'))

@[simp]
theorem nnnorm_eq_of_mem_circle (z : circle) : ‚Äñz.val‚Äñ‚Çä = 1 := NNReal.coe_eq_one.mp (by simp)

@[simp]
theorem nnnorm_circle_smul (z : circle) (s : ‚ÑÇ) : ‚Äñz ‚Ä¢ s‚Äñ‚Çä = ‚Äñs‚Äñ‚Çä := by
  simp [show z ‚Ä¢ s = z.val * s from rfl]

lemma hf_coe1 {œÉ' : ‚Ñù} (hœÉ : 1 < œÉ') :
    (‚àë' (i : ‚Ñï), ‚Üë‚Äñ(f i : ‚ÑÇ) / ‚Üë((i : ‚Ñù) ^ œÉ')‚Äñ‚Çä : ENNReal) ‚â† ‚ä§ := by
  apply ENNReal.tsum_coe_ne_top_iff_summable_coe.mpr
  norm_cast
  simp_rw [‚Üê norm_toNNReal]
  apply Summable.toNNReal
  have (n : ‚Ñï) : 0 ‚â§ (n : ‚Ñù) ^ œÉ' := by positivity
  simp_rw [norm_div, Real.norm_eq_abs, _root_.abs_of_nonneg (this _), hf œÉ' hœÉ]

lemma first_fourier_aux1 {œà : ‚Ñù ‚Üí ‚ÑÇ} (hœà: Continuous œà) {x : ‚Ñù} (n : ‚Ñï) : Measurable fun (u : ‚Ñù) ‚Ü¶
    (‚ÄñfourierChar (-(u * ((1 : ‚Ñù) / ((2 : ‚Ñù) * œÄ) * (n / x).log))) ‚Ä¢ œà u‚Äñ‚Çä : ENNReal) := by
  -- TODO: attribute [fun_prop] Real.continuous_fourierChar once `fun_prop` bugfix is merged
  refine Measurable.comp ?_ (by fun_prop) |>.smul (by fun_prop)
    |>.nnnorm |>.coe_nnreal_ennreal
  exact Continuous.measurable Real.continuous_fourierChar

lemma first_fourier_aux2a {x y : ‚Ñù} {n : ‚Ñï} :
    (2 : ‚ÑÇ) * œÄ * -(y * (1 / (2 * œÄ) * Real.log ((n + 1) / x))) = -(y * ((n + 1) / x).log) := by
  calc
    _ = -(y * (((2 : ‚ÑÇ) * œÄ) / (2 * œÄ) * Real.log ((n + 1) / x))) := by ring
    _ = _ := by rw [div_self (by norm_num; exact pi_ne_zero), one_mul]

lemma first_fourier_aux2 {œà : ‚Ñù ‚Üí ‚ÑÇ} {œÉ' x y : ‚Ñù} (hœÉ : 1 < œÉ') (hx : 0 < x) (n : ‚Ñï) :
    (f n : ‚ÑÇ) / ‚Üë((n : ‚Ñù) ^ œÉ') *
    fourierChar (-(y * ((1 : ‚Ñù) / ((2 : ‚Ñù) * œÄ) * Real.log (‚Üën / x)))) ‚Ä¢ œà y =
    ((f n : ‚ÑÇ) / n ^ (‚ÜëœÉ' + ‚Üëy * I)) ‚Ä¢ (œà y * ‚Üëx ^ (‚Üëy * I)) := by
  show _ * ((fourierChar <| Multiplicative.ofAdd (_) : ‚ÑÇ) ‚Ä¢ œà y) = ((f n : ‚ÑÇ) / _) ‚Ä¢ _
  rw [fourierChar_apply]
  show _ / _ * cexp (‚Üë(_ * -(y * _)) * I) ‚Ä¢ œà y = _
  calc
    _ = ((f n : ‚ÑÇ) *
        (cexp (‚Üë((2 : ‚Ñù) * œÄ * -(y * ((1 : ‚Ñù) / ((2 : ‚Ñù) * œÄ) * Real.log (n / x)))) * I) /
        ‚Üë((n : ‚Ñù) ^ œÉ'))) ‚Ä¢ œà y := by
      simp_rw [smul_eq_mul]; group
    _ = ((f n : ‚ÑÇ) * (‚Üëx ^ (‚Üëy * I) / ‚Üën ^ (‚ÜëœÉ' + ‚Üëy * I))) ‚Ä¢ œà y := by
      congr 2
      cases n with
      | zero =>
        have : œÉ' = 0 ‚Üí ¬¨y = 0 := fun _ ‚Ü¶ False.elim (by linarith)
        simp [Real.zero_rpow (by linarith : œÉ' ‚â† 0),
          Complex.zero_cpow (by simpa [Complex.ext_iff] using this : œÉ' + ‚Üëy * I ‚â† 0)]
      | succ n =>
        rw [Real.rpow_def_of_pos (cast_pos.mpr (Nat.zero_lt_succ n)),
          Complex.cpow_def_of_ne_zero (ofReal_ne_zero.mpr (ne_of_gt hx)),
          Complex.cpow_def_of_ne_zero (NeZero.natCast_ne (succ n) ‚ÑÇ)]
        push_cast
        simp_rw [‚Üê Complex.exp_sub]
        congr 1
        rw [first_fourier_aux2a, Real.log_div (cast_add_one_ne_zero n) (ne_of_gt hx)]
        push_cast
        rw [Complex.ofReal_log (by linarith), Complex.ofReal_log hx.le]
        push_cast
        ring
    _ = _ := by simp_rw [smul_eq_mul]; group


/-%%
\begin{lemma}[First Fourier identity]\label{first-fourier}\lean{first_fourier}\leanok  If $\psi: \R \to \C$ is continuous and compactly supported and $x > 0$, then for any $\sigma>1$
  $$ \sum_{n=1}^\infty \frac{f(n)}{n^\sigma} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) = \int_\R F(\sigma + it) \psi(t) x^{it}\ dt.$$
\end{lemma}
%%-/
lemma first_fourier {œà : ‚Ñù ‚Üí ‚ÑÇ} (hcont: Continuous œà) (hsupp: HasCompactSupport œà)
    {x œÉ':‚Ñù} (hx: 0 < x) (hœÉ: 1 < œÉ') :
    ‚àë' n : ‚Ñï, f n / (n^œÉ':‚Ñù) * (fourierIntegral œà (1 / (2 * œÄ) * log (n / x))) =
    ‚à´ t:‚Ñù, ArithmeticFunction.LSeries f (œÉ' + t * I) * œà t * x^(t * I) ‚àÇ volume := by
/-%%
\begin{proof}\leanok  By the definition of the Fourier transform, the left-hand side expands as
$$ \sum_{n=1}^\infty \int_\R \frac{f(n)}{n^\sigma} \psi(t) e( - \frac{1}{2\pi} t \log \frac{n}{x})\ dt$$
while the right-hand side expands as
$$ \int_\R \sum_{n=1}^\infty \frac{f(n)}{n^{\sigma+it}} \psi(t) x^{it}\ dt.$$
Since
$$\frac{f(n)}{n^\sigma} \psi(t) e( - \frac{1}{2\pi} t \log \frac{n}{x}) = \frac{f(n)}{n^{\sigma+it}} \psi(t) x^{it}$$
the claim then follows from Fubini's theorem.
\end{proof}
%%-/
  calc
    _ = ‚àë' (n : ‚Ñï), (f n : ‚ÑÇ) / ‚Üë((n : ‚Ñù) ^ œÉ') *
        ‚à´ (v : ‚Ñù), fourierChar (-(v * ((1 : ‚Ñù) / ((2 : ‚Ñù) * œÄ) * Real.log (n / x)))) ‚Ä¢ œà v := by
      rfl
    _ = ‚àë' (n : ‚Ñï), ‚à´ (v : ‚Ñù), (f n : ‚ÑÇ) / ‚Üë((n : ‚Ñù) ^ œÉ') *
        fourierChar (-(v * ((1 : ‚Ñù) / ((2 : ‚Ñù) * œÄ) * Real.log (n / x)))) ‚Ä¢ œà v := by
      congr 1; ext : 1; exact (integral_mul_left _ _).symm
    _ = ‚à´ (v : ‚Ñù), ‚àë' (n : ‚Ñï), (f n : ‚ÑÇ) / ‚Üë((n : ‚Ñù) ^ œÉ') *
        fourierChar (-(v * ((1 : ‚Ñù) / ((2 : ‚Ñù) * œÄ) * Real.log (n / x)))) ‚Ä¢ œà v := by
      refine (integral_tsum ?_ ?_).symm
      ¬∑ -- TODO: attribute [fun_prop] Real.continuous_fourierChar once `fun_prop` bugfix is merged
        refine fun _ ‚Ü¶ Measurable.aestronglyMeasurable ?_
        refine Measurable.mul (by fun_prop) ((Measurable.comp ?_ (by fun_prop)).smul (by fun_prop))
        exact Continuous.measurable Real.continuous_fourierChar
      ¬∑ simp_rw [nnnorm_mul]
        push_cast
        simp_rw [lintegral_const_mul _ (first_fourier_aux1 hcont _)]
        calc
          _ = (‚àë' (i : ‚Ñï), (‚Äñ(f i : ‚ÑÇ) / ‚Üë((i : ‚Ñù) ^ œÉ')‚Äñ‚Çä : ENNReal)) *
              ‚à´‚Åª (a : ‚Ñù), ‚Äñœà a‚Äñ‚Çä ‚àÇvolume := by
            simp [ENNReal.tsum_mul_right]
          _ ‚â† ‚ä§ := ENNReal.mul_ne_top
            (hf_coe1 hf hœÉ) (ne_top_of_lt (hcont.integrable_of_hasCompactSupport hsupp).2)
    _ = _ := by
      congr 1; ext y
      simp_rw [mul_assoc (L _ _), ‚Üê smul_eq_mul (a := (L _ _)), ArithmeticFunction.LSeries]
      rw [‚Üê tsum_smul_const]
      ¬∑ congr 1; ext n
        exact first_fourier_aux2 hœÉ hx n
      ¬∑ apply Summable.of_norm
        convert hf œÉ' hœÉ with n
        cases n <;> simp [-cast_succ, Complex.abs_cpow_of_ne_zero (NeZero.natCast_ne _ ‚ÑÇ)]

/-%%
\begin{lemma}[Second Fourier identity]\label{second-fourier}\lean{second_fourier}\leanok If $\psi: \R \to \C$ is continuous and compactly supported and $x > 0$, then for any $\sigma>1$
$$ \int_{-\log x}^\infty e^{-u(\sigma-1)} \hat \psi(\frac{u}{2\pi})\ du = x^{\sigma - 1} \int_\R \frac{1}{\sigma+it-1} \psi(t) x^{it}\ dt.$$
\end{lemma}
%%-/

@[continuity]
lemma continuous_multiplicative_ofAdd : Continuous (‚áëMultiplicative.ofAdd : ‚Ñù ‚Üí ‚Ñù) := ‚ü®fun _ ‚Ü¶ id‚ü©

attribute [fun_prop] measurable_coe_nnreal_ennreal

lemma second_fourier_integrable_aux1a {x œÉ' : ‚Ñù} (hœÉ : 1 < œÉ') :
    IntegrableOn (fun (x : ‚Ñù) ‚Ü¶ cexp (-((x : ‚ÑÇ) * ((œÉ' : ‚ÑÇ) - 1)))) (Ici (-Real.log x)) := by
  norm_cast
  suffices IntegrableOn (fun (x : ‚Ñù) ‚Ü¶ (rexp (-(x * (œÉ' - 1))))) (Ici (-x.log)) _ from this.ofReal
  simp_rw [fun (a x : ‚Ñù) ‚Ü¶ (by ring : -(x * a) = -a * x), integrableOn_Ici_iff_integrableOn_Ioi]
  apply exp_neg_integrableOn_Ioi
  linarith

lemma second_fourier_integrable_aux1 {œà : ‚Ñù ‚Üí ‚ÑÇ}
    (hcont: Continuous œà) (hsupp: HasCompactSupport œà) {œÉ' x : ‚Ñù} (hœÉ : 1 < œÉ') :
    let ŒΩ : Measure (‚Ñù √ó ‚Ñù) := (volume.restrict (Ici (-Real.log x))).prod volume
    Integrable (Function.uncurry fun (u : ‚Ñù) (a : ‚Ñù) ‚Ü¶ ((rexp (-u * (œÉ' - 1))) : ‚ÑÇ) ‚Ä¢
    (fourierChar (Multiplicative.ofAdd (-(a * (u / (2 * œÄ))))) : ‚ÑÇ) ‚Ä¢ œà a) ŒΩ := by
  intro ŒΩ
  constructor
  ¬∑ apply Measurable.aestronglyMeasurable
    apply MeasureTheory.measurable_uncurry_of_continuous_of_measurable <;> intro i
    swap; apply Continuous.measurable
    all_goals exact Continuous.smul (by fun_prop) <|
      (Continuous.subtype_val (by continuity)).smul (by fun_prop)
  ¬∑ let f1 : ‚Ñù ‚Üí ENNReal := fun a1 ‚Ü¶ ‚Üë‚Äñcexp (-(‚Üëa1 * (‚ÜëœÉ' - 1)))‚Äñ‚Çä
    let f2 : ‚Ñù ‚Üí ENNReal := fun a2 ‚Ü¶ ‚Üë‚Äñœà a2‚Äñ‚Çä
    suffices ‚à´‚Åª (a : ‚Ñù √ó ‚Ñù), f1 a.1 * f2 a.2 ‚àÇŒΩ < ‚ä§ by simpa [Function.uncurry, HasFiniteIntegral]
    refine (lintegral_prod_mul ?_ ?_).trans_lt ?_ <;> unfold_let f1 f2; fun_prop; fun_prop
    exact ENNReal.mul_lt_top (ne_top_of_lt (second_fourier_integrable_aux1a hœÉ).2)
      (ne_top_of_lt (hcont.integrable_of_hasCompactSupport hsupp).2)

lemma second_fourier_integrable_aux2 {œÉ' t x : ‚Ñù} (hœÉ : 1 < œÉ') :
    IntegrableOn (fun (u : ‚Ñù) ‚Ü¶ cexp ((1 - ‚ÜëœÉ' - ‚Üët * I) * ‚Üëu)) (Ioi (-Real.log x)) := by
  refine (integrable_norm_iff (Measurable.aestronglyMeasurable <| by fun_prop)).mp ?_
  suffices IntegrableOn (fun a ‚Ü¶ rexp (-(œÉ' - 1) * a)) (Ioi (-x.log)) _ by simpa [Complex.abs_exp]
  apply exp_neg_integrableOn_Ioi
  linarith

lemma second_fourier_aux {x œÉ' t : ‚Ñù} (hx : 0 < x) :
    -(cexp (-((1 - ‚ÜëœÉ' - ‚Üët * I) * ‚Üë(Real.log x))) / (1 - ‚ÜëœÉ' - ‚Üët * I)) =
    ‚Üë(x ^ (œÉ' - 1)) * (‚ÜëœÉ' + ‚Üët * I - 1)‚Åª¬π * ‚Üëx ^ (‚Üët * I) := by
  calc
    _ = cexp (‚Üë(Real.log x) * ((‚ÜëœÉ' - 1) + ‚Üët * I)) * (‚ÜëœÉ' + ‚Üët * I - 1)‚Åª¬π := by rw [‚Üê div_neg]; ring_nf
    _ = (x ^ ((‚ÜëœÉ' - 1) + ‚Üët * I)) * (‚ÜëœÉ' + ‚Üët * I - 1)‚Åª¬π := by
      rw [Complex.cpow_def_of_ne_zero (ofReal_ne_zero.mpr (ne_of_gt hx)), Complex.ofReal_log hx.le]
    _ = (x ^ ((œÉ' : ‚ÑÇ) - 1)) * (x ^ (‚Üët * I)) * (‚ÜëœÉ' + ‚Üët * I - 1)‚Åª¬π := by
      rw [Complex.cpow_add _ _ (ofReal_ne_zero.mpr (ne_of_gt hx))]
    _ = _ := by rw [ofReal_cpow hx.le]; push_cast; ring

lemma second_fourier {œà : ‚Ñù ‚Üí ‚ÑÇ} (hcont: Continuous œà) (hsupp: HasCompactSupport œà)
    {x œÉ' : ‚Ñù} (hx : 0 < x) (hœÉ : 1 < œÉ') :
    ‚à´ u in Ici (-log x), Real.exp (-u * (œÉ' - 1)) * fourierIntegral œà (u / (2 * œÄ)) =
    (x^(œÉ' - 1) : ‚Ñù) * ‚à´ t, (1 / (œÉ' + t * I - 1)) * œà t * x^(t * I) ‚àÇ volume := by
/-%%
\begin{proof}\leanok
The left-hand side expands as
$$ \int_{-\log x}^\infty \int_\R e^{-u(\sigma-1)} \psi(t) e(-\frac{tu}{2\pi})\ dt\ du =
x^{\sigma - 1} \int_\R \frac{1}{\sigma+it-1} \psi(t) x^{it}\ dt$$
so by Fubini's theorem it suffices to verify the identity
\begin{align*}
\int_{-\log x}^\infty e^{-u(\sigma-1)} e(-\frac{tu}{2\pi})\ du
&= \int_{-\log x}^\infty e^{(it - \sigma + 1)u}\ du \\
&= \frac{1}{it - \sigma + 1} e^{(it - \sigma + 1)u}\ \Big|_{-\log x}^\infty \\
&= x^{\sigma - 1} \frac{1}{\sigma+it-1} x^{it}
\end{align*}
\end{proof}
%%-/
  conv in ‚Üë(rexp _) * _ => { rw [fourierIntegral_def, ‚Üê smul_eq_mul, ‚Üê integral_smul] }
  rw [MeasureTheory.integral_integral_swap (second_fourier_integrable_aux1 hcont hsupp hœÉ),
    ‚Üê integral_mul_left]
  congr 1; ext t
  simp_rw [fourierChar_apply, smul_eq_mul, ‚Üê mul_assoc _ _ (œà _), integral_mul_right]
  rw [fun (a b d : ‚ÑÇ) ‚Ü¶ show a * (b * (œà t) * d) = (a * b * d) * œà t by ring]
  congr 1
  push_cast
  simp_rw [‚Üê Complex.exp_add]
  have (u : ‚Ñù) :
      -‚Üëu * (‚ÜëœÉ' - 1) + 2 * ‚ÜëœÄ * -(‚Üët * (‚Üëu / (2 * ‚ÜëœÄ))) * I = (1 - œÉ' - t * I) * u := calc
    _ = -‚Üëu * (‚ÜëœÉ' - 1) + (2 * ‚ÜëœÄ) / (2 * ‚ÜëœÄ) * -(‚Üët * ‚Üëu) * I := by ring
    _ = -‚Üëu * (‚ÜëœÉ' - 1) + 1 * -(‚Üët * ‚Üëu) * I := by rw [div_self (by norm_num; exact pi_ne_zero)]
    _ = _ := by ring
  simp_rw [this]
  let c : ‚ÑÇ := (1 - ‚ÜëœÉ' - ‚Üët * I)
  have : c ‚â† 0 := by simpa [Complex.ext_iff] using fun h ‚Ü¶ False.elim (by linarith)
  let f' (u : ‚Ñù) := cexp (c * u)
  let f := fun (u : ‚Ñù) ‚Ü¶ (f' u) / c
  have hderiv : ‚àÄ u ‚àà Ici (-Real.log x), HasDerivAt f (f' u) u := by
    intro u _
    rw [show f' u = cexp (c * u) * (c * 1) / c by field_simp]
    exact (hasDerivAt_id' u).ofReal_comp.const_mul c |>.cexp.div_const c
  have hf : Tendsto f atTop (ùìù 0) := by
    apply tendsto_zero_iff_norm_tendsto_zero.mpr
    suffices Tendsto (fun (x : ‚Ñù) ‚Ü¶ abs (cexp (c * ‚Üëx)) / abs c) atTop (ùìù (0 / abs c)) by simpa
    apply Filter.Tendsto.div_const
    suffices Tendsto (. * (1 - œÉ')) atTop atBot by simpa [Complex.abs_exp, mul_comm (1 - œÉ')]
    exact Tendsto.atTop_mul_neg_const (by linarith) fun ‚¶És‚¶Ñ h ‚Ü¶ h
  rw [integral_Ici_eq_integral_Ioi,
    integral_Ioi_of_hasDerivAt_of_tendsto' hderiv (second_fourier_integrable_aux2 hœÉ) hf]
  simpa using second_fourier_aux hx

/-%%
Now let $A \in \C$, and suppose that there is a continuous function $G(s)$ defined on $\mathrm{Re} s \geq 1$ such that $G(s) = F(s) - \frac{A}{s-1}$ whenever $\mathrm{Re} s > 1$.  We also make the Chebyshev-type hypothesis
\begin{equation}\label{cheby}
\sum_{n \leq x} |f(n)| \ll x
\end{equation}
for all $x \geq 1$ (this hypothesis is not strictly necessary, but simplifies the arguments and can be obtained fairly easily in applications).
%%-/

variable {A:‚Ñù} {G:‚ÑÇ ‚Üí ‚ÑÇ} (hG: ContinuousOn G {s | 1 ‚â§ s.re}) (hG' : Set.EqOn G (fun s ‚Ü¶ ArithmeticFunction.LSeries f s - A / (s - 1)) {s | 1 < s.re})

-- variable (hcheby: ‚àÉ C:‚Ñù, ‚àÄ x:‚Ñï, ‚àë n in Finset.Iic x, |f n| ‚â§ C * x)

-- This is in #10099, up to some plumbing
theorem HasCompactSupport.integral_mul_deriv {u v : ‚Ñù ‚Üí ‚ÑÇ} (hu : ContDiff ‚Ñù 1 u) (hv : ContDiff ‚Ñù 1 v)
    (h : HasCompactSupport v) : ‚à´ x, u x * deriv v x = - ‚à´ x, deriv u x * v x := by sorry

theorem contDiff_ofReal : ContDiff ‚Ñù ‚ä§ (ofReal : ‚Ñù ‚Üí ‚ÑÇ) := by
  have l0 (x : ‚Ñù) : HasDerivAt (ofReal : ‚Ñù ‚Üí ‚ÑÇ) 1 x := (hasDerivAt_id (x : ‚ÑÇ)).comp_ofReal
  refine contDiff_top_iff_deriv.mpr ‚ü®fun x => (l0 x).differentiableAt, ?_‚ü©
  simpa only [(funext (fun x => (l0 x).deriv) : deriv _ = _)] using contDiff_const

lemma fourierChar_eq_cexp {u v : ‚Ñù} : fourierChar [-v * u] = cexp (-2 * œÄ * v * u * I) := by
  rw [fourierChar_apply] ; simp [mul_assoc]

theorem hasDerivAt_fourierChar' {u x : ‚Ñù} : let e (v : ‚Ñù) := fourierChar [-v * u];
    HasDerivAt e (-2 * œÄ * u * I * e x) x := by
  have l2 : HasDerivAt (fun v => -v * u) (-u) x := by simpa only [neg_mul_comm] using hasDerivAt_mul_const (-u)
  convert (hasDerivAt_fourierChar (-x * u)).scomp x l2 using 1 ; simp ; ring

theorem contDiff_fourierChar' {u : ‚Ñù} : ContDiff ‚Ñù 1 (fun v => fourierChar [-v * u]) := by
  have l3 (x : ‚Ñù) := (hasDerivAt_fourierChar' (u := u) (x := x)).deriv
  refine contDiff_one_iff_deriv.mpr ‚ü®fun x => hasDerivAt_fourierChar'.differentiableAt, ?_‚ü©
  rw [(funext l3 : deriv _ = _)]
  exact continuous_const.mul <| continuous_iff_continuousAt.mpr (fun x => hasDerivAt_fourierChar'.continuousAt)

lemma decay_bounds_aux3 {œà : ‚Ñù ‚Üí ‚ÑÇ} (h1 : ContDiff ‚Ñù 1 œà) (h2 : HasCompactSupport œà) {u : ‚Ñù} :
    ùìï (deriv œà) u = 2 * œÄ * I * u * ùìï œà u := by
  let e (v : ‚Ñù) := fourierChar [-v * u]
  convert_to ‚à´ (v : ‚Ñù), e v * deriv œà v = 2 * ‚ÜëœÄ * I * ‚Üëu * ‚à´ (v : ‚Ñù), e v * œà v
  ¬∑ simp [fourierIntegral, Fourier.fourierIntegral, VectorFourier.fourierIntegral]
  ¬∑ simp [fourierIntegral, Fourier.fourierIntegral, VectorFourier.fourierIntegral]
  have l3 (x : ‚Ñù) : deriv e x = -2 * œÄ * u * I * e x := hasDerivAt_fourierChar'.deriv
  have l2 : ContDiff ‚Ñù 1 e := by
    refine contDiff_one_iff_deriv.mpr ‚ü®fun x => hasDerivAt_fourierChar'.differentiableAt, ?_‚ü©
    rw [(funext l3 : deriv _ = _)]
    exact continuous_const.mul <| continuous_iff_continuousAt.mpr (fun x => hasDerivAt_fourierChar'.continuousAt)
  simp_rw [HasCompactSupport.integral_mul_deriv l2 h1 h2, l3, ‚Üê integral_mul_left, ‚Üê integral_neg]
  congr ; ext ; ring

lemma decay_bounds_aux2 {u : ‚Ñù} {œà : ‚Ñù ‚Üí ‚ÑÇ} (h1 : ContDiff ‚Ñù 2 œà) (h2 : HasCompactSupport œà) :
    u ^ 2 * ùìï œà u = - (1 / (4 * œÄ ^ 2) * ‚à´ (t : ‚Ñù), deriv^[2] œà t * fourierChar [-t * u]) := by
  convert_to ‚Üëu ^ 2 * ùìï œà u = - (1 / (4 * ‚ÜëœÄ ^ 2) * ùìï (deriv^[2] œà) u)
  ¬∑ congr ; ext ; field_simp
  have l1 : ContDiff ‚Ñù 1 (deriv œà) := (contDiff_succ_iff_deriv.mp h1).2
  have l2 : HasCompactSupport (deriv œà) := h2.deriv
  simp_rw [iterate, decay_bounds_aux3 l1 l2, decay_bounds_aux3 h1.of_succ h2]
  field_simp [pi_ne_zero] ; ring_nf ; simp

lemma decay_bounds_aux1 {u : ‚Ñù} {œà : ‚Ñù ‚Üí ‚ÑÇ} (h1 : ContDiff ‚Ñù 2 œà) (h2 : HasCompactSupport œà) :
    (1 + u ^ 2) * ùìï œà u = ‚à´ (t : ‚Ñù), (œà t - (1 / (4 * œÄ ^ 2)) * deriv^[2] œà t) * fourierChar [-t * u] := by
  have l0 : Continuous fun t ‚Ü¶ fourierChar [-t * u] := contDiff_fourierChar'.continuous
  have l1 : Integrable fun t ‚Ü¶ fourierChar [-t * u] * œà t :=
    l0.mul h1.continuous |>.integrable_of_hasCompactSupport h2.mul_left
  have l2 : Integrable fun t ‚Ü¶ 1 / (4 * œÄ ^ 2) * (deriv^[2] œà t * fourierChar [-t * u]) := by
    refine Continuous.integrable_of_hasCompactSupport ?_ h2.deriv.deriv.mul_right.mul_left
    exact continuous_const.mul <| (h1.iterate_deriv' 0 2).continuous.mul l0
  simp_rw [sub_mul, mul_assoc, add_mul, one_mul, mul_comm (œà _)]
  rw [integral_sub l1 l2, integral_mul_left, sub_eq_add_neg, ‚Üê decay_bounds_aux2 h1 h2]
  simp [fourierIntegral, Fourier.fourierIntegral, VectorFourier.fourierIntegral]

/-%%
\begin{lemma}[Decay bounds]\label{decay}\lean{decay_bounds}\leanok  If $\psi:\R \to \C$ is $C^2$ and obeys the bounds
  $$ |\psi(t)|, |\psi''(t)| \leq A / (1 + |t|^2)$$
  for all $t \in \R$, then
$$ |\hat \psi(u)| \leq C A / (1+|u|^2)$$
for all $u \in \R$, where $C$ is an absolute constant.
\end{lemma}
%%-/

lemma decay_bounds : ‚àÉ C : ‚Ñù, ‚àÄ (œà : ‚Ñù ‚Üí ‚ÑÇ) (hœà: ContDiff ‚Ñù 2 œà) (hsupp: HasCompactSupport œà) (A : ‚Ñù)
    (hA : ‚àÄ t, ‚Äñœà t‚Äñ ‚â§ A / (1 + t ^ 2)) (hA' : ‚àÄ t, ‚Äñderiv^[2] œà t‚Äñ ‚â§ A / (1 + t ^ 2)) (u : ‚Ñù),
    ‚Äñùìï œà u‚Äñ ‚â§ C * A / (1 + u^2) := by
  use œÄ + 1 / (4 * œÄ)
  intro œà h1 h2 A hA hA' u
  have key := decay_bounds_aux1 (u := u) h1 h2
  have l1 : 0 < 1 + u ^ 2 := zero_lt_one.trans_le (by simpa using sq_nonneg u)
  have l2 : 1 + u ^ 2 = ‚Äñ(1 : ‚ÑÇ) + u ^ 2‚Äñ := by
    simp only [Complex.norm_eq_abs]
    norm_cast
    exact (abs_eq_self.2 l1.le).symm
  rw [le_div_iff l1, mul_comm, l2, ‚Üê norm_mul, key]
  let f (t : ‚Ñù) := (œà t - 1 / (4 * œÄ ^ 2) * deriv^[2] œà t) * ‚Üë(fourierChar (Multiplicative.ofAdd (-t * u)))
  let g (t : ‚Ñù) := A * (1 + 1 / (4 * œÄ ^ 2)) / (1 + t ^ 2)
  have l5 (t : ‚Ñù) : ‚Äñ(fourierChar (Multiplicative.ofAdd (-t * u)) : ‚ÑÇ)‚Äñ = 1 := by simp
  have l4 (t : ‚Ñù) : ‚Äñf t‚Äñ ‚â§ g t := by
    simp only [norm_mul, l5, mul_one, mul_add, _root_.add_div]
    apply (norm_sub_le _ _).trans
    apply _root_.add_le_add (hA t)
    rw [norm_mul]
    convert mul_le_mul_of_nonneg_left (hA' t) (norm_nonneg _) using 1
    field_simp
  have l5 : Integrable g := by simpa [g, div_eq_mul_inv] using integrable_inv_one_add_sq.const_mul _
  convert norm_integral_le_of_norm_le l5 (eventually_of_forall l4)
  dsimp [g]
  simp_rw [div_eq_mul_inv, integral_mul_left, integral_univ_inv_one_add_sq]
  field_simp [pi_ne_zero] ; ring

/-%%
\begin{proof} From two integration by parts we obtain the identity
$$ (1+u^2) \hat \psi(u) = \int_{\bf R} (\psi(t) - \frac{u}{4\pi^2} \psi''(t)) e(-tu)\ dt.$$
Now apply the triangle inequality and the identity $\int_{\bf R} \frac{dt}{1+t^2}\ dt = \pi$ to obtain the claim with $C = \pi + 1 / 4 \pi$.
\end{proof}
%%-/

/-%%
\begin{lemma}[Limiting Fourier identity]\label{limiting}\lean{limiting_fourier}\leanok  If $\psi: \R \to \C$ is $C^2$ and compactly supported and $x \geq 1$, then
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) - A \int_{-\log x}^\infty \hat \psi(\frac{u}{2\pi})\ du =  \int_\R G(1+it) \psi(t) x^{it}\ dt.$$
\end{lemma}
%%-/


lemma limiting_fourier {œà:‚Ñù ‚Üí ‚ÑÇ} (hœà: ContDiff ‚Ñù 2 œà) (hsupp: HasCompactSupport œà) {x:‚Ñù} (hx: 1 ‚â§ x) : ‚àë' n, f n / n * fourierIntegral œà (1/(2*œÄ) * log (n/x)) - A * ‚à´ u in Set.Ici (-log x), fourierIntegral œà (u / (2*œÄ)) ‚àÇ volume = ‚à´ (t : ‚Ñù), (G (1 + I*t)) * (œà t) * x^(I * t) ‚àÇ volume := by
  sorry

/-%%
\begin{proof}
\uses{first-fourier,second-fourier,decay}
 By the preceding two lemmas, we know that for any $\sigma>1$, we have
  $$ \sum_{n=1}^\infty \frac{f(n)}{n^\sigma} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) - A x^{1-\sigma} \int_{-\log x}^\infty e^{-u(\sigma-1)} \hat \psi(\frac{u}{2\pi})\ du =  \int_\R G(\sigma+it) \psi(t) x^{it}\ dt.$$
  Now take limits as $\sigma \to 1$ using dominated convergence together with \eqref{cheby} and Lemma \ref{decay} to obtain the result.
\end{proof}
%%-/

/-%%
\begin{corollary}[Corollary of limiting identity]\label{limiting-cor}\lean{limiting_cor}\leanok  With the hypotheses as above, we have
  $$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) = A \int_{-\infty}^\infty \hat \psi(\frac{u}{2\pi})\ du + o(1)$$
  as $x \to \infty$.
\end{corollary}
%%-/

open Filter

lemma limiting_cor {œà:‚Ñù ‚Üí ‚ÑÇ} (hœà: ContDiff ‚Ñù 2 œà) (hsupp: HasCompactSupport œà) : Tendsto (fun x : ‚Ñù ‚Ü¶ ‚àë' n, f n / n * fourierIntegral œà (1/(2*œÄ) * log (n/x)) - A * ‚à´ u in Set.Ici (-log x), fourierIntegral œà (u / (2*œÄ)) ‚àÇ volume) atTop (nhds 0) := by sorry

/-%%
\begin{proof}
\uses{limiting}
 Immediate from the Riemann-Lebesgue lemma, and also noting that $\int_{-\infty}^{-\log x} \hat \psi(\frac{u}{2\pi})\ du = o(1)$.
\end{proof}
%%-/

/-%%
\begin{lemma}[Smooth Urysohn lemma]\label{smooth-ury}\lean{smooth_urysohn}\leanok  If $I$ is a closed interval contained in an open interval $J$, then there exists a smooth function $\Psi: \R \to \R$ with $1_I \leq \Psi \leq 1_J$.
\end{lemma}
%%-/

lemma smooth_urysohn {a b c d:‚Ñù} (h1: a < b) (h2: b<c) (h3: c < d) : ‚àÉ Œ®:‚Ñù ‚Üí ‚Ñù, (‚àÄ n, ContDiff ‚Ñù n Œ®) ‚àß (HasCompactSupport Œ®) ‚àß Set.indicator (Set.Icc b c) 1 ‚â§ Œ® ‚àß Œ® ‚â§ Set.indicator (Set.Ioo a d) 1 := by
  have := exists_smooth_zero_one_of_isClosed (modelWithCornersSelf ‚Ñù ‚Ñù) (s := Set.Iic a ‚à™ Set.Ici d) (t := Set.Icc b c)
    (IsClosed.union isClosed_Iic isClosed_Ici)
    (isClosed_Icc)
    (by
      simp_rw [Set.disjoint_union_left, Set.disjoint_iff, Set.subset_def, Set.mem_inter_iff, Set.mem_Iic, Set.mem_Icc,
        Set.mem_empty_iff_false, and_imp, imp_false, not_le, Set.mem_Ici]
      constructor <;> intros <;> linarith)
  rcases this with ‚ü®‚ü®Œ®, hŒ®contMDiff‚ü©, hŒ®0, hŒ®1, hŒ®01‚ü©
  simp only [Set.EqOn, Set.mem_setOf_eq, Set.mem_union, Set.mem_Iic, Set.mem_Ici,
    ContMDiffMap.coeFn_mk, Pi.zero_apply, Set.mem_Icc, Pi.one_apply, and_imp] at *
  use Œ®
  constructor
  ¬∑ rw [contDiff_all_iff_nat, ‚ÜêcontDiff_top]
    exact ContMDiff.contDiff hŒ®contMDiff
  ¬∑ constructor
    ¬∑ rw [hasCompactSupport_def]
      apply IsCompact.closure_of_subset (K := Set.Icc a d) isCompact_Icc
      rw [Function.support_subset_iff]
      intro x hx
      contrapose! hx
      simp only [Set.mem_Icc, not_and_or] at hx
      apply hŒ®0
      by_contra! h'
      cases' hx <;> linarith
    ¬∑ constructor
      ¬∑ intro x
        rw [Set.indicator_apply]
        split_ifs with h
        simp only [Set.mem_Icc, Pi.one_apply] at *
        rw [hŒ®1 h.left h.right]
        exact (hŒ®01 x).left
      ¬∑ intro x
        rw [Set.indicator_apply]
        split_ifs with h
        simp at *
        exact (hŒ®01 x).right
        rw [hŒ®0]
        simp only [Set.mem_Ioo, not_and_or] at h
        by_contra! h'
        cases' h <;> linarith
  done


/-%%
\begin{proof}  \leanok
A standard analysis lemma, which can be proven by convolving $1_K$ with a smooth approximation to the identity for some interval $K$ between $I$ and $J$. Note that we have ``SmoothBumpFunction''s on smooth manifolds in Mathlib, so this shouldn't be too hard...
\end{proof}
%%-/

/-%%
\begin{lemma}[Limiting identity for Schwartz functions]\label{schwarz-id}\lean{limiting_cor_schwartz}\leanok  The previous corollary also holds for functions $\psi$ that are assumed to be in the Schwartz class, as opposed to being $C^2$ and compactly supported.
\end{lemma}
%%-/

lemma limiting_cor_schwartz {œà: SchwartzMap ‚Ñù ‚ÑÇ} : Tendsto (fun x : ‚Ñù ‚Ü¶ ‚àë' n, f n / n * fourierIntegral œà (1/(2*œÄ) * log (n/x)) - A * ‚à´ u in Set.Ici (-log x), fourierIntegral œà (u / (2*œÄ)) ‚àÇ volume) atTop (nhds 0) := by sorry

/-%%
\begin{proof}
\uses{limiting-cor, smooth-ury}
For any $R>1$, one can use a smooth cutoff function (provided by Lemma \ref{smooth-ury} to write $\psi = \psi_{\leq R} + \psi_{>R}$, where $\psi_{\leq R}$ is $C^2$ (in fact smooth) and compactly supported (on $[-R,R]$), and $\psi_{>R}$ obeys bounds of the form
$$ |\psi_{>R}(t)|, |\psi''_{>R}(t)| \ll R^{-1} / (1 + |t|^2) $$
where the implied constants depend on $\psi$.  By Lemma \ref{decay} we then have
$$ \hat \psi_{>R}(u) \ll R^{-1} / (1+|u|^2).$$
Using this and \eqref{cheby} one can show that
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi_{>R}( \frac{1}{2\pi} \log \frac{n}{x} ), A \int_{-\infty}^\infty \hat \psi_{>R} (\frac{u}{2\pi})\ du \ll R^{-1} $$
(with implied constants also depending on $A$), while from Lemma \ref{limiting-cor} one has
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi_{\leq R}( \frac{1}{2\pi} \log \frac{n}{x} ) = A \int_{-\infty}^\infty \hat \psi_{\leq R} (\frac{u}{2\pi})\ du + o(1).$$
Combining the two estimates and letting $R$ be large, we obtain the claim.
\end{proof}
%%-/

/-%%
\begin{lemma}[Bijectivity of Fourier transform]\label{bij}\lean{fourier_surjection_on_schwartz}\leanok  The Fourier transform is a bijection on the Schwartz class.
\end{lemma}
%%-/

-- just the surjectivity is stated here, as this is all that is needed for the current application, but perhaps one should state and prove bijectivity instead

lemma fourier_surjection_on_schwartz (f : SchwartzMap ‚Ñù ‚ÑÇ) : ‚àÉ g : SchwartzMap ‚Ñù ‚ÑÇ, fourierIntegral g = f := by sorry

/-%%
\begin{proof}
\uses{MellinInversion}
 This is a standard result in Fourier analysis.
It can be proved here by appealing to Mellin inversion, Theorem \ref{MellinInversion}.
In particular, given $f$ in the Schwartz class, let $F : \R_+ \to \C : x \mapsto f(\log x)$ be a function in the ``Mellin space''; then the Mellin transform of $F$ on the imaginary axis $s=it$ is the Fourier transform of $f$.  The Mellin inversion theorem gives Fourier inversion.
\end{proof}
%%-/

/-%%
\begin{corollary}[Smoothed Wiener-Ikehara]\label{WienerIkeharaSmooth}\lean{wiener_ikehara_smooth}\leanok
  If $\Psi: (0,\infty) \to \C$ is smooth and compactly supported away from the origin, then, then
$$ \sum_{n=1}^\infty f(n) \Psi( \frac{n}{x} ) = A x \int_0^\infty \Psi(y)\ dy + o(x)$$
as $u \to \infty$.
\end{corollary}
%%-/

lemma wiener_ikehara_smooth {Œ®: ‚Ñù ‚Üí ‚ÑÇ} (hsmooth: ‚àÄ n, ContDiff ‚Ñù n Œ®) (hsupp: HasCompactSupport Œ®) (hplus: closure (Function.support Œ®) ‚äÜ Set.Ioi (0:‚Ñù)): Tendsto (fun x : ‚Ñù ‚Ü¶ (‚àë' n, f n / n * Œ® (n/x))/x - A * ‚à´ y in Set.Ioi 0, Œ® y ‚àÇ volume) atTop (nhds 0) := by sorry

/-%%
\begin{proof}
\uses{bij,schwarz-id}
 By Lemma \ref{bij}, we can write
$$ y \Psi(y) = \hat \psi( \frac{1}{2\pi} \log y )$$
for all $y>0$ and some Schwartz function $\psi$.  Making this substitution, the claim is then equivalent after standard manipulations to
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) = A \int_{-\infty}^\infty \hat \psi(\frac{u}{2\pi})\ du + o(1)$$
and the claim follows from Lemma \ref{schwarz-id}.
\end{proof}
%%-/


/-%%
Now we add the hypothesis that $f(n) \geq 0$ for all $n$.

\begin{proposition}[Wiener-Ikehara in an interval]
\label{WienerIkeharaInterval}\lean{WienerIkeharaInterval}\leanok
  For any closed interval $I \subset (0,+\infty)$, we have
  $$ \sum_{n=1}^\infty f(n) 1_I( \frac{n}{x} ) = A x |I|  + o(x).$$
\end{proposition}
%%-/

variable (hpos: ‚àÄ n, 0 ‚â§ f n)

lemma WienerIkeharaInterval (a b:‚Ñù) (ha: 0 < a) (hb: a < b) : Tendsto (fun x : ‚Ñù ‚Ü¶ ‚àë' n, f n / n * (Set.indicator (Set.Icc a b) 1 (n/x))/x - A * (b-a)) atTop (nhds 0) := by
  sorry

/-%%
\begin{proof}
\uses{smooth-ury, WienerIkeharaSmooth}
  Use Lemma \ref{smooth-ury} to bound $1_I$ above and below by smooth compactly supported functions whose integral is close to the measure of $|I|$, and use the non-negativity of $f$.
\end{proof}
%%-/

/-%%
\begin{corollary}[Wiener-Ikehara theorem]\label{WienerIkehara}\lean{WienerIkeharaTheorem'}\leanok
  We have
$$ \sum_{n\leq x} f(n) = A x |I|  + o(x).$$
\end{corollary}
%%-/
open Filter Nat ArithmeticFunction in
/-- A version of the *Wiener-Ikehara Tauberian Theorem*: If `f` is a nonnegative arithmetic
function whose L-series has a simple pole at `s = 1` with residue `A` and otherwise extends
continuously to the closed half-plane `re s ‚â• 1`, then `‚àë n < N, f n` is asymptotic to `A*N`. -/
theorem WienerIkeharaTheorem' {f : ArithmeticFunction ‚Ñù} {A : ‚Ñù} {F : ‚ÑÇ ‚Üí ‚ÑÇ} (hf : ‚àÄ n, 0 ‚â§ f n)
    (hF : Set.EqOn F (fun s ‚Ü¶ LSeries f s - A / (s - 1)) {s | 1 < s.re})
    (hF' : ContinuousOn F {s | 1 ‚â§ s.re}) :
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum f) / N) atTop (nhds A) := by
  sorry
/-%%
\begin{proof}
\uses{WienerIkeharaInterval}
  Apply the preceding proposition with $I = [\varepsilon,1]$ and then send $\varepsilon$ to zero (using \eqref{cheby} to control the error).
\end{proof}
%%-/

/-%%
\section{Weak PNT}

\begin{theorem}[Weak PNT]\label{WeakPNT}\lean{WeakPNT}\leanok  We have
$$ \sum_{n \leq x} \Lambda(n) = x + o(x).$$
\end{theorem}
%%-/
theorem WeakPNT :
    Tendsto (fun N : ‚Ñï ‚Ü¶ ((Finset.range N).sum Œõ) / N) atTop (nhds 1) := by
  sorry
/-%%
\begin{proof}
\uses{WienerIkehara, ChebyshevPsi}
  Already done by Stoll, assuming Wiener-Ikehara.
\end{proof}
%%-/
