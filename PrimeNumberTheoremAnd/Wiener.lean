import EulerProducts.PNT
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Mathlib.NumberTheory.ArithmeticFunction
import Mathlib.Topology.Support
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Geometry.Manifold.PartitionOfUnity
import Mathlib.Tactic.FunProp.AEMeasurable
import Mathlib.Tactic.FunProp.Measurable

open Nat Real BigOperators ArithmeticFunction MeasureTheory Filter Set FourierTransform LSeries
open Complex hiding log
-- note: the opening of ArithmeticFunction introduces a notation Ïƒ that seems impossible to hide, and hence parameters that are traditionally called Ïƒ will have to be called Ïƒ' instead in this file.

open scoped Topology

-- This version makes the support of Î¨ explicit, and this is easier for some later proofs
lemma smooth_urysohn_support_Ioo {a b c d : â„} (h1 : a < b) (h3: c < d) :
    âˆƒ Î¨ : â„ â†’ â„, (âˆ€ n, ContDiff â„ n Î¨) âˆ§ (HasCompactSupport Î¨) âˆ§ Set.indicator (Set.Icc b c) 1 â‰¤ Î¨ âˆ§
    Î¨ â‰¤ Set.indicator (Set.Ioo a d) 1 âˆ§ (Function.support Î¨ = Set.Ioo a d) := by

  have := exists_msmooth_zero_iff_one_iff_of_isClosed
    (modelWithCornersSelf â„ â„) (s := Set.Iic a âˆª Set.Ici d) (t := Set.Icc b c)
    (IsClosed.union isClosed_Iic isClosed_Ici)
    (isClosed_Icc)
    (by
      simp_rw [Set.disjoint_union_left, Set.disjoint_iff, Set.subset_def, Set.mem_inter_iff, Set.mem_Iic, Set.mem_Icc,
        Set.mem_empty_iff_false, and_imp, imp_false, not_le, Set.mem_Ici]
      constructor <;> intros <;> linarith)

  rcases this with âŸ¨Î¨, hÎ¨Smooth, hÎ¨range, hÎ¨0, hÎ¨1âŸ©

  simp only [Set.EqOn, Set.mem_setOf_eq, Set.mem_union, Set.mem_Iic, Set.mem_Ici,
    ContMDiffMap.coeFn_mk, Pi.zero_apply, Set.mem_Icc, Pi.one_apply, and_imp] at *
  use Î¨
  constructor
  Â· rw [contDiff_all_iff_nat, â†contDiff_top]
    exact ContMDiff.contDiff hÎ¨Smooth
  Â· constructor
    Â· rw [hasCompactSupport_def]
      apply IsCompact.closure_of_subset (K := Set.Icc a d) isCompact_Icc
      simp_rw [Function.support_subset_iff, ne_eq, <-hÎ¨0]
      intro x hx
      contrapose! hx
      simp only [Set.mem_Icc, not_and_or] at hx
      by_contra! h'
      cases' hx <;> linarith
    Â· constructor
      Â· intro x
        rw [Set.indicator_apply]
        split_ifs with h
        Â· simp only [Set.mem_Icc, Pi.one_apply] at *
          simp_rw [hÎ¨1 x] at h
          exact Eq.le (_root_.id h.symm)
        Â· have : Î¨ x âˆˆ Set.range Î¨ := by simp only [Set.mem_range, exists_apply_eq_apply]
          have : Î¨ x âˆˆ Set.Icc 0 1 := hÎ¨range this
          exact this.left
      Â· constructor
        Â· intro x
          rw [Set.indicator_apply]
          split_ifs with h
          Â· have : Î¨ x âˆˆ Set.range Î¨ := by simp only [Set.mem_range, exists_apply_eq_apply]
            have : Î¨ x âˆˆ Set.Icc 0 1 := by exact hÎ¨range this
            simpa using this.2
          Â· simp only [Set.mem_Ioo, Pi.one_apply] at *
            simp only [not_and_or, not_lt] at h
            simp_rw [hÎ¨0 x] at h
            exact Eq.le h
        Â· simp_rw [Function.support, ne_eq, â†hÎ¨0]
          push_neg
          simp [Set.ext_iff]
  done


/-%%
The Fourier transform of an absolutely integrable function $\psi: \R \to \C$ is defined by the formula
$$ \hat \psi(u) := \int_\R e(-tu) \psi(t)\ dt$$
where $e(\theta) := e^{2\pi i \theta}$.

Let $f: \N \to \C$ be an arithmetic function such that $\sum_{n=1}^\infty \frac{|f(n)|}{n^\sigma} < \infty$ for all $\sigma>1$.  Then the Dirichlet series
$$ F(s) := \sum_{n=1}^\infty \frac{f(n)}{n^s}$$
is absolutely convergent for $\sigma>1$.
%%-/

noncomputable
def nterm (f : â„• â†’ â„‚) (Ïƒ' : â„) (n : â„•) : â„ := if n = 0 then 0 else â€–f nâ€– / n ^ Ïƒ'

variable {f : ArithmeticFunction â„‚} (hf : âˆ€ (Ïƒ' : â„), 1 < Ïƒ' â†’ Summable (nterm f Ïƒ'))

@[simp]
theorem nnnorm_eq_of_mem_circle (z : circle) : â€–z.valâ€–â‚Š = 1 := NNReal.coe_eq_one.mp (by simp)

@[simp]
theorem nnnorm_circle_smul (z : circle) (s : â„‚) : â€–z â€¢ sâ€–â‚Š = â€–sâ€–â‚Š := by
  simp [show z â€¢ s = z.val * s from rfl]

lemma hf_coe1 {Ïƒ' : â„} (hÏƒ : 1 < Ïƒ') : (âˆ‘' (i : â„•), â€–term f Ïƒ' iâ€–â‚Š : ENNReal) â‰  âŠ¤ := by
  apply ENNReal.tsum_coe_ne_top_iff_summable_coe.mpr
  norm_cast
  simp_rw [â† norm_toNNReal]
  apply Summable.toNNReal
  convert hf Ïƒ' hÏƒ using 1 ; ext i
  simp [nterm, term]
  by_cases h : i = 0 <;> simp [h]

lemma first_fourier_aux1 {Ïˆ : â„ â†’ â„‚} (hÏˆ: Continuous Ïˆ) {x : â„} (n : â„•) : Measurable fun (u : â„) â†¦
    (â€–fourierChar (-(u * ((1 : â„) / ((2 : â„) * Ï€) * (n / x).log))) â€¢ Ïˆ uâ€–â‚Š : ENNReal) := by
  -- TODO: attribute [fun_prop] Real.continuous_fourierChar once `fun_prop` bugfix is merged
  refine Measurable.comp ?_ (by fun_prop) |>.smul (by fun_prop)
    |>.nnnorm |>.coe_nnreal_ennreal
  exact Continuous.measurable Real.continuous_fourierChar

lemma first_fourier_aux2a {x y : â„} {n : â„•} :
    (2 : â„‚) * Ï€ * -(y * (1 / (2 * Ï€) * Real.log ((n + 1) / x))) = -(y * ((n + 1) / x).log) := by
  calc
    _ = -(y * (((2 : â„‚) * Ï€) / (2 * Ï€) * Real.log ((n + 1) / x))) := by ring
    _ = _ := by rw [div_self (by norm_num; exact pi_ne_zero), one_mul]

lemma first_fourier_aux2 {Ïˆ : â„ â†’ â„‚} {Ïƒ' x y : â„} (hÏƒ : 1 < Ïƒ') (hx : 0 < x) (n : â„•) :
    term f Ïƒ' n * fourierChar (-(y * ((1 : â„) / ((2 : â„) * Ï€) * Real.log (â†‘n / x)))) â€¢ Ïˆ y =
    (term f (â†‘Ïƒ' + â†‘y * I) n) â€¢ (Ïˆ y * â†‘x ^ (â†‘y * I)) := by
  show _ * ((fourierChar <| Multiplicative.ofAdd (_) : â„‚) â€¢ Ïˆ y) = ((f n : â„‚) / _) â€¢ _
  rw [fourierChar_apply]
  show _ / _ * cexp (â†‘(_ * -(y * _)) * I) â€¢ Ïˆ y = _
  calc
    _ = ((f n : â„‚) *
        (cexp (â†‘((2 : â„) * Ï€ * -(y * ((1 : â„) / ((2 : â„) * Ï€) * Real.log (n / x)))) * I) /
        â†‘((n : â„) ^ Ïƒ'))) â€¢ Ïˆ y := by
      simp_rw [smul_eq_mul]; group
    _ = ((f n : â„‚) * (â†‘x ^ (â†‘y * I) / â†‘n ^ (â†‘Ïƒ' + â†‘y * I))) â€¢ Ïˆ y := by
      congr 2
      cases n with
      | zero =>
        have : Ïƒ' = 0 â†’ Â¬y = 0 := fun _ â†¦ False.elim (by linarith)
        simp [Real.zero_rpow (by linarith : Ïƒ' â‰  0),
          Complex.zero_cpow (by simpa [Complex.ext_iff] using this : Ïƒ' + â†‘y * I â‰  0)]
      | succ n =>
        rw [Real.rpow_def_of_pos (cast_pos.mpr (Nat.zero_lt_succ n)),
          Complex.cpow_def_of_ne_zero (ofReal_ne_zero.mpr (ne_of_gt hx)),
          Complex.cpow_def_of_ne_zero (NeZero.natCast_ne (succ n) â„‚)]
        push_cast
        simp_rw [â† Complex.exp_sub]
        congr 1
        rw [first_fourier_aux2a, Real.log_div (cast_add_one_ne_zero n) (ne_of_gt hx)]
        push_cast
        rw [Complex.ofReal_log (by linarith), Complex.ofReal_log hx.le]
        push_cast
        ring
    _ = _ := by simp_rw [smul_eq_mul]; group


/-%%
\begin{lemma}[First Fourier identity]\label{first-fourier}\lean{first_fourier}\leanok  If $\psi: \R \to \C$ is continuous and compactly supported and $x > 0$, then for any $\sigma>1$
  $$ \sum_{n=1}^\infty \frac{f(n)}{n^\sigma} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) = \int_\R F(\sigma + it) \psi(t) x^{it}\ dt.$$
\end{lemma}
%%-/
lemma first_fourier {Ïˆ : â„ â†’ â„‚} (hcont: Continuous Ïˆ) (hsupp: HasCompactSupport Ïˆ)
    {x Ïƒ':â„} (hx: 0 < x) (hÏƒ: 1 < Ïƒ') :
    âˆ‘' n : â„•, term f Ïƒ' n * (fourierIntegral Ïˆ (1 / (2 * Ï€) * log (n / x))) =
    âˆ« t:â„, LSeries f (Ïƒ' + t * I) * Ïˆ t * x^(t * I) âˆ‚ volume := by
/-%%
\begin{proof}\leanok  By the definition of the Fourier transform, the left-hand side expands as
$$ \sum_{n=1}^\infty \int_\R \frac{f(n)}{n^\sigma} \psi(t) e( - \frac{1}{2\pi} t \log \frac{n}{x})\ dt$$
while the right-hand side expands as
$$ \int_\R \sum_{n=1}^\infty \frac{f(n)}{n^{\sigma+it}} \psi(t) x^{it}\ dt.$$
Since
$$\frac{f(n)}{n^\sigma} \psi(t) e( - \frac{1}{2\pi} t \log \frac{n}{x}) = \frac{f(n)}{n^{\sigma+it}} \psi(t) x^{it}$$
the claim then follows from Fubini's theorem.
\end{proof}
%%-/
  calc
    _ = âˆ‘' (n : â„•), term f Ïƒ' n *
        âˆ« (v : â„), fourierChar (-(v * ((1 : â„) / ((2 : â„) * Ï€) * Real.log (n / x)))) â€¢ Ïˆ v := by
      rfl
    _ = âˆ‘' (n : â„•), âˆ« (v : â„), term f Ïƒ' n *
        fourierChar (-(v * ((1 : â„) / ((2 : â„) * Ï€) * Real.log (n / x)))) â€¢ Ïˆ v := by
      congr 1; ext : 1; exact (integral_mul_left _ _).symm
    _ = âˆ« (v : â„), âˆ‘' (n : â„•), term f Ïƒ' n *
        fourierChar (-(v * ((1 : â„) / ((2 : â„) * Ï€) * Real.log (n / x)))) â€¢ Ïˆ v := by
      refine (integral_tsum ?_ ?_).symm
      Â· -- TODO: attribute [fun_prop] Real.continuous_fourierChar once `fun_prop` bugfix is merged
        refine fun _ â†¦ Measurable.aestronglyMeasurable ?_
        refine Measurable.mul (by fun_prop) ((Measurable.comp ?_ (by fun_prop)).smul (by fun_prop))
        exact Continuous.measurable Real.continuous_fourierChar
      Â· simp_rw [nnnorm_mul]
        push_cast
        simp_rw [lintegral_const_mul _ (first_fourier_aux1 hcont _)]
        calc
          _ = (âˆ‘' (i : â„•), (â€–term f Ïƒ' iâ€–â‚Š : ENNReal)) * âˆ«â» (a : â„), â€–Ïˆ aâ€–â‚Š âˆ‚volume := by
            simp [ENNReal.tsum_mul_right]
          _ â‰  âŠ¤ := ENNReal.mul_ne_top (hf_coe1 hf hÏƒ)
            (ne_top_of_lt (hcont.integrable_of_hasCompactSupport hsupp).2)
    _ = _ := by
      congr 1; ext y
      simp_rw [mul_assoc (LSeries _ _), â† smul_eq_mul (a := (LSeries _ _)), LSeries]
      rw [â† tsum_smul_const]
      Â· congr 1; ext n
        exact first_fourier_aux2 hÏƒ hx n
      Â· apply Summable.of_norm
        convert hf Ïƒ' hÏƒ with n
        cases n <;> simp [-cast_succ, Complex.abs_cpow_of_ne_zero (NeZero.natCast_ne _ â„‚)]

/-%%
\begin{lemma}[Second Fourier identity]\label{second-fourier}\lean{second_fourier}\leanok If $\psi: \R \to \C$ is continuous and compactly supported and $x > 0$, then for any $\sigma>1$
$$ \int_{-\log x}^\infty e^{-u(\sigma-1)} \hat \psi(\frac{u}{2\pi})\ du = x^{\sigma - 1} \int_\R \frac{1}{\sigma+it-1} \psi(t) x^{it}\ dt.$$
\end{lemma}
%%-/

@[continuity]
lemma continuous_multiplicative_ofAdd : Continuous (â‡‘Multiplicative.ofAdd : â„ â†’ â„) := âŸ¨fun _ â†¦ idâŸ©

attribute [fun_prop] measurable_coe_nnreal_ennreal

lemma second_fourier_integrable_aux1a {x Ïƒ' : â„} (hÏƒ : 1 < Ïƒ') :
    IntegrableOn (fun (x : â„) â†¦ cexp (-((x : â„‚) * ((Ïƒ' : â„‚) - 1)))) (Ici (-Real.log x)) := by
  norm_cast
  suffices IntegrableOn (fun (x : â„) â†¦ (rexp (-(x * (Ïƒ' - 1))))) (Ici (-x.log)) _ from this.ofReal
  simp_rw [fun (a x : â„) â†¦ (by ring : -(x * a) = -a * x), integrableOn_Ici_iff_integrableOn_Ioi]
  apply exp_neg_integrableOn_Ioi
  linarith

lemma second_fourier_integrable_aux1 {Ïˆ : â„ â†’ â„‚}
    (hcont: Continuous Ïˆ) (hsupp: HasCompactSupport Ïˆ) {Ïƒ' x : â„} (hÏƒ : 1 < Ïƒ') :
    let Î½ : Measure (â„ Ã— â„) := (volume.restrict (Ici (-Real.log x))).prod volume
    Integrable (Function.uncurry fun (u : â„) (a : â„) â†¦ ((rexp (-u * (Ïƒ' - 1))) : â„‚) â€¢
    (fourierChar (Multiplicative.ofAdd (-(a * (u / (2 * Ï€))))) : â„‚) â€¢ Ïˆ a) Î½ := by
  intro Î½
  constructor
  Â· apply Measurable.aestronglyMeasurable
    apply MeasureTheory.measurable_uncurry_of_continuous_of_measurable <;> intro i
    swap; apply Continuous.measurable
    all_goals exact Continuous.smul (by fun_prop) <|
      (Continuous.subtype_val (by continuity)).smul (by fun_prop)
  Â· let f1 : â„ â†’ ENNReal := fun a1 â†¦ â†‘â€–cexp (-(â†‘a1 * (â†‘Ïƒ' - 1)))â€–â‚Š
    let f2 : â„ â†’ ENNReal := fun a2 â†¦ â†‘â€–Ïˆ a2â€–â‚Š
    suffices âˆ«â» (a : â„ Ã— â„), f1 a.1 * f2 a.2 âˆ‚Î½ < âŠ¤ by simpa [Function.uncurry, HasFiniteIntegral]
    refine (lintegral_prod_mul ?_ ?_).trans_lt ?_ <;> unfold_let f1 f2; fun_prop; fun_prop
    exact ENNReal.mul_lt_top (ne_top_of_lt (second_fourier_integrable_aux1a hÏƒ).2)
      (ne_top_of_lt (hcont.integrable_of_hasCompactSupport hsupp).2)

lemma second_fourier_integrable_aux2 {Ïƒ' t x : â„} (hÏƒ : 1 < Ïƒ') :
    IntegrableOn (fun (u : â„) â†¦ cexp ((1 - â†‘Ïƒ' - â†‘t * I) * â†‘u)) (Ioi (-Real.log x)) := by
  refine (integrable_norm_iff (Measurable.aestronglyMeasurable <| by fun_prop)).mp ?_
  suffices IntegrableOn (fun a â†¦ rexp (-(Ïƒ' - 1) * a)) (Ioi (-x.log)) _ by simpa [Complex.abs_exp]
  apply exp_neg_integrableOn_Ioi
  linarith

lemma second_fourier_aux {x Ïƒ' t : â„} (hx : 0 < x) :
    -(cexp (-((1 - â†‘Ïƒ' - â†‘t * I) * â†‘(Real.log x))) / (1 - â†‘Ïƒ' - â†‘t * I)) =
    â†‘(x ^ (Ïƒ' - 1)) * (â†‘Ïƒ' + â†‘t * I - 1)â»Â¹ * â†‘x ^ (â†‘t * I) := by
  calc
    _ = cexp (â†‘(Real.log x) * ((â†‘Ïƒ' - 1) + â†‘t * I)) * (â†‘Ïƒ' + â†‘t * I - 1)â»Â¹ := by rw [â† div_neg]; ring_nf
    _ = (x ^ ((â†‘Ïƒ' - 1) + â†‘t * I)) * (â†‘Ïƒ' + â†‘t * I - 1)â»Â¹ := by
      rw [Complex.cpow_def_of_ne_zero (ofReal_ne_zero.mpr (ne_of_gt hx)), Complex.ofReal_log hx.le]
    _ = (x ^ ((Ïƒ' : â„‚) - 1)) * (x ^ (â†‘t * I)) * (â†‘Ïƒ' + â†‘t * I - 1)â»Â¹ := by
      rw [Complex.cpow_add _ _ (ofReal_ne_zero.mpr (ne_of_gt hx))]
    _ = _ := by rw [ofReal_cpow hx.le]; push_cast; ring

lemma second_fourier {Ïˆ : â„ â†’ â„‚} (hcont: Continuous Ïˆ) (hsupp: HasCompactSupport Ïˆ)
    {x Ïƒ' : â„} (hx : 0 < x) (hÏƒ : 1 < Ïƒ') :
    âˆ« u in Ici (-log x), Real.exp (-u * (Ïƒ' - 1)) * fourierIntegral Ïˆ (u / (2 * Ï€)) =
    (x^(Ïƒ' - 1) : â„) * âˆ« t, (1 / (Ïƒ' + t * I - 1)) * Ïˆ t * x^(t * I) âˆ‚ volume := by
/-%%
\begin{proof}\leanok
The left-hand side expands as
$$ \int_{-\log x}^\infty \int_\R e^{-u(\sigma-1)} \psi(t) e(-\frac{tu}{2\pi})\ dt\ du =
x^{\sigma - 1} \int_\R \frac{1}{\sigma+it-1} \psi(t) x^{it}\ dt$$
so by Fubini's theorem it suffices to verify the identity
\begin{align*}
\int_{-\log x}^\infty e^{-u(\sigma-1)} e(-\frac{tu}{2\pi})\ du
&= \int_{-\log x}^\infty e^{(it - \sigma + 1)u}\ du \\
&= \frac{1}{it - \sigma + 1} e^{(it - \sigma + 1)u}\ \Big|_{-\log x}^\infty \\
&= x^{\sigma - 1} \frac{1}{\sigma+it-1} x^{it}
\end{align*}
\end{proof}
%%-/
  conv in â†‘(rexp _) * _ => { rw [fourierIntegral_def, â† smul_eq_mul, â† integral_smul] }
  rw [MeasureTheory.integral_integral_swap (second_fourier_integrable_aux1 hcont hsupp hÏƒ),
    â† integral_mul_left]
  congr 1; ext t
  simp_rw [fourierChar_apply, smul_eq_mul, â† mul_assoc _ _ (Ïˆ _), integral_mul_right]
  rw [fun (a b d : â„‚) â†¦ show a * (b * (Ïˆ t) * d) = (a * b * d) * Ïˆ t by ring]
  congr 1
  push_cast
  simp_rw [â† Complex.exp_add]
  have (u : â„) :
      -â†‘u * (â†‘Ïƒ' - 1) + 2 * â†‘Ï€ * -(â†‘t * (â†‘u / (2 * â†‘Ï€))) * I = (1 - Ïƒ' - t * I) * u := calc
    _ = -â†‘u * (â†‘Ïƒ' - 1) + (2 * â†‘Ï€) / (2 * â†‘Ï€) * -(â†‘t * â†‘u) * I := by ring
    _ = -â†‘u * (â†‘Ïƒ' - 1) + 1 * -(â†‘t * â†‘u) * I := by rw [div_self (by norm_num; exact pi_ne_zero)]
    _ = _ := by ring
  simp_rw [this]
  let c : â„‚ := (1 - â†‘Ïƒ' - â†‘t * I)
  have : c â‰  0 := by simpa [Complex.ext_iff] using fun h â†¦ False.elim (by linarith)
  let f' (u : â„) := cexp (c * u)
  let f := fun (u : â„) â†¦ (f' u) / c
  have hderiv : âˆ€ u âˆˆ Ici (-Real.log x), HasDerivAt f (f' u) u := by
    intro u _
    rw [show f' u = cexp (c * u) * (c * 1) / c by field_simp]
    exact (hasDerivAt_id' u).ofReal_comp.const_mul c |>.cexp.div_const c
  have hf : Tendsto f atTop (ğ“ 0) := by
    apply tendsto_zero_iff_norm_tendsto_zero.mpr
    suffices Tendsto (fun (x : â„) â†¦ abs (cexp (c * â†‘x)) / abs c) atTop (ğ“ (0 / abs c)) by simpa
    apply Filter.Tendsto.div_const
    suffices Tendsto (. * (1 - Ïƒ')) atTop atBot by simpa [Complex.abs_exp, mul_comm (1 - Ïƒ')]
    exact Tendsto.atTop_mul_neg_const (by linarith) fun â¦ƒsâ¦„ h â†¦ h
  rw [integral_Ici_eq_integral_Ioi,
    integral_Ioi_of_hasDerivAt_of_tendsto' hderiv (second_fourier_integrable_aux2 hÏƒ) hf]
  simpa using second_fourier_aux hx

/-%%
Now let $A \in \C$, and suppose that there is a continuous function $G(s)$ defined on $\mathrm{Re} s \geq 1$ such that $G(s) = F(s) - \frac{A}{s-1}$ whenever $\mathrm{Re} s > 1$.  We also make the Chebyshev-type hypothesis
\begin{equation}\label{cheby}
\sum_{n \leq x} |f(n)| \ll x
\end{equation}
for all $x \geq 1$ (this hypothesis is not strictly necessary, but simplifies the arguments and can be obtained fairly easily in applications).
%%-/

variable {A:â„} {G:â„‚ â†’ â„‚} (hG: ContinuousOn G {s | 1 â‰¤ s.re}) (hG' : Set.EqOn G (fun s â†¦ ArithmeticFunction.LSeries f s - A / (s - 1)) {s | 1 < s.re})

-- variable (hcheby: âˆƒ C:â„, âˆ€ x:â„•, âˆ‘ n in Finset.Iic x, |f n| â‰¤ C * x)

theorem HasCompactSupport.integral_deriv_eq_zero {u : â„ â†’ â„‚} (h1 : ContDiff â„ 1 u) (h2 : HasCompactSupport u) :
    âˆ« x, deriv u x = 0 := by sorry

theorem HasCompactSupport.integral_mul_deriv {u v : â„ â†’ â„‚} (hu : ContDiff â„ 1 u) (hv : ContDiff â„ 1 v)
    (h : HasCompactSupport v) : âˆ« x, u x * deriv v x = - âˆ« x, deriv u x * v x := by
  have l1 : Integrable fun x â†¦ u x * deriv v x :=
    hu.continuous.mul (contDiff_one_iff_deriv.1 hv).2 |>.integrable_of_hasCompactSupport h.deriv.mul_left
  have l2 : Integrable fun x â†¦ deriv u x * v x :=
    (contDiff_one_iff_deriv.1 hu).2.mul hv.continuous |>.integrable_of_hasCompactSupport h.mul_left
  have l3 (a : â„) : deriv u a * v a + u a * deriv v a = deriv (u * v) a := by
    rw [â† deriv_mul (hu.differentiable le_rfl a) (hv.differentiable le_rfl a)] ; rfl
  rw [eq_neg_iff_add_eq_zero, add_comm, â† integral_add l2 l1]
  simp_rw [l3]
  exact HasCompactSupport.integral_deriv_eq_zero (hu.mul hv) (h.mul_left)

theorem hasDerivAt_fourierChar' {u x : â„} : let e (v : â„) := fourierChar [-v * u];
    HasDerivAt e (-2 * Ï€ * u * I * e x) x := by
  have l2 : HasDerivAt (fun v => -v * u) (-u) x := by simpa only [neg_mul_comm] using hasDerivAt_mul_const (-u)
  convert (hasDerivAt_fourierChar (-x * u)).scomp x l2 using 1 ; simp ; ring

theorem contDiff_fourierChar' {u : â„} : ContDiff â„ 1 (fun v => fourierChar [-v * u]) := by
  have l3 (x : â„) := (hasDerivAt_fourierChar' (u := u) (x := x)).deriv
  refine contDiff_one_iff_deriv.mpr âŸ¨fun x => hasDerivAt_fourierChar'.differentiableAt, ?_âŸ©
  rw [(funext l3 : deriv _ = _)]
  exact continuous_const.mul <| continuous_iff_continuousAt.mpr (fun x => hasDerivAt_fourierChar'.continuousAt)

lemma decay_bounds_aux3 {Ïˆ : â„ â†’ â„‚} (h1 : ContDiff â„ 1 Ïˆ) (h2 : HasCompactSupport Ïˆ) {u : â„} :
    ğ“• (deriv Ïˆ) u = 2 * Ï€ * I * u * ğ“• Ïˆ u := by
  let e (v : â„) := fourierChar [-v * u]
  simp [fourierIntegral, Fourier.fourierIntegral, VectorFourier.fourierIntegral]
  convert_to âˆ« (v : â„), e v * deriv Ïˆ v = 2 * â†‘Ï€ * I * â†‘u * âˆ« (v : â„), e v * Ïˆ v
  Â· simp only [neg_mul, ofAdd_neg, map_inv, coe_inv_unitSphere]
  Â· simp only [neg_mul, ofAdd_neg, map_inv, coe_inv_unitSphere]
  have l3 (x : â„) : deriv e x = -2 * Ï€ * u * I * e x := hasDerivAt_fourierChar'.deriv
  simp_rw [h2.integral_mul_deriv contDiff_fourierChar' h1, l3, â† integral_mul_left, â† integral_neg]
  congr ; ext ; ring

lemma decay_bounds_aux4 {u : â„} {Ïˆ : â„ â†’ â„‚} (h1 : ContDiff â„ 2 Ïˆ) (h2 : HasCompactSupport Ïˆ) :
    u ^ 2 * ğ“• Ïˆ u = - (1 / (4 * Ï€ ^ 2) * ğ“• (deriv^[2] Ïˆ) u) := by
  have l1 : ContDiff â„ 1 (deriv Ïˆ) := (contDiff_succ_iff_deriv.mp h1).2
  simp_rw [iterate, decay_bounds_aux3 l1 h2.deriv, decay_bounds_aux3 h1.of_succ h2]
  field_simp [pi_ne_zero] ; ring_nf ; simp

lemma decay_bounds_aux2 {u : â„} {Ïˆ : â„ â†’ â„‚} (h1 : ContDiff â„ 2 Ïˆ) (h2 : HasCompactSupport Ïˆ) :
    u ^ 2 * ğ“• Ïˆ u = - (1 / (4 * Ï€ ^ 2) * âˆ« (t : â„), deriv^[2] Ïˆ t * fourierChar [-t * u]) := by
  convert decay_bounds_aux4 h1 h2 ; congr ; ext ; field_simp

lemma decay_bounds_aux1 {Ïˆ : â„ â†’ â„‚} (h1 : ContDiff â„ 2 Ïˆ) (h2 : HasCompactSupport Ïˆ) (u : â„) :
    (1 + u ^ 2) * ğ“• Ïˆ u = âˆ« (t : â„), (Ïˆ t - (1 / (4 * Ï€ ^ 2)) * deriv^[2] Ïˆ t) * fourierChar [-t * u] := by
  have l0 : Continuous fun t â†¦ fourierChar [-t * u] := contDiff_fourierChar'.continuous
  have l1 : Integrable fun t â†¦ fourierChar [-t * u] * Ïˆ t :=
    l0.mul h1.continuous |>.integrable_of_hasCompactSupport h2.mul_left
  have l2 : Integrable fun t â†¦ 1 / (4 * Ï€ ^ 2) * (deriv^[2] Ïˆ t * fourierChar [-t * u]) := by
    refine Continuous.integrable_of_hasCompactSupport ?_ h2.deriv.deriv.mul_right.mul_left
    exact continuous_const.mul <| (h1.iterate_deriv' 0 2).continuous.mul l0
  simp_rw [sub_mul, mul_assoc, add_mul, one_mul, mul_comm (Ïˆ _)]
  rw [integral_sub l1 l2, integral_mul_left, sub_eq_add_neg, â† decay_bounds_aux2 h1 h2]
  simp [fourierIntegral, Fourier.fourierIntegral, VectorFourier.fourierIntegral]

/-%%
\begin{lemma}[Decay bounds]\label{decay}\lean{decay_bounds}\leanok  If $\psi:\R \to \C$ is $C^2$ and obeys the bounds
  $$ |\psi(t)|, |\psi''(t)| \leq A / (1 + |t|^2)$$
  for all $t \in \R$, then
$$ |\hat \psi(u)| \leq C A / (1+|u|^2)$$
for all $u \in \R$, where $C$ is an absolute constant.
\end{lemma}
%%-/

lemma decay_bounds {Ïˆ : â„ â†’ â„‚} {A u : â„} (h1 : ContDiff â„ 2 Ïˆ) (h2 : HasCompactSupport Ïˆ)
    (hA : âˆ€ t, â€–Ïˆ tâ€– â‰¤ A / (1 + t ^ 2)) (hA' : âˆ€ t, â€–deriv^[2] Ïˆ tâ€– â‰¤ A / (1 + t ^ 2)) :
    â€–ğ“• Ïˆ uâ€– â‰¤ (Ï€ + 1 / (4 * Ï€)) * A / (1 + u ^ 2) := by
  have key := decay_bounds_aux1 h1 h2 u
  have l1 : 0 < 1 + u ^ 2 := zero_lt_one.trans_le (by simpa using sq_nonneg u)
  have l2 : 1 + u ^ 2 = â€–(1 : â„‚) + u ^ 2â€– := by
    norm_cast ; simp only [Complex.norm_eq_abs, Complex.abs_ofReal, abs_eq_self.2 l1.le]
  rw [le_div_iff l1, mul_comm, l2, â† norm_mul, key]
  let f (t : â„) := (Ïˆ t - 1 / (4 * Ï€ ^ 2) * deriv^[2] Ïˆ t) * fourierChar [-t * u]
  let g (t : â„) := A * (1 + 1 / (4 * Ï€ ^ 2)) / (1 + t ^ 2)
  have l5 (t : â„) : â€–fourierChar [-t * u]â€– = 1 := by simp
  have l4 (t : â„) : â€–f tâ€– â‰¤ g t := by
    simp only [norm_mul, l5, mul_one, mul_add, _root_.add_div]
    refine (norm_sub_le _ _).trans <| _root_.add_le_add (hA t) ?_
    rw [norm_mul]
    convert mul_le_mul_of_nonneg_left (hA' t) (norm_nonneg _) using 1 ; field_simp
  have l5 : Integrable g := by simpa [g, div_eq_mul_inv] using integrable_inv_one_add_sq.const_mul _
  convert norm_integral_le_of_norm_le l5 (eventually_of_forall l4)
  simp_rw [div_eq_mul_inv, integral_mul_left, integral_univ_inv_one_add_sq]
  field_simp [pi_ne_zero] ; ring

/-%%
\begin{proof} From two integration by parts we obtain the identity
$$ (1+u^2) \hat \psi(u) = \int_{\bf R} (\psi(t) - \frac{u}{4\pi^2} \psi''(t)) e(-tu)\ dt.$$
Now apply the triangle inequality and the identity $\int_{\bf R} \frac{dt}{1+t^2}\ dt = \pi$ to obtain the claim with $C = \pi + 1 / 4 \pi$.
\end{proof}
%%-/

/-%%
\begin{lemma}[Limiting Fourier identity]\label{limiting}\lean{limiting_fourier}\leanok  If $\psi: \R \to \C$ is $C^2$ and compactly supported and $x \geq 1$, then
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) - A \int_{-\log x}^\infty \hat \psi(\frac{u}{2\pi})\ du =  \int_\R G(1+it) \psi(t) x^{it}\ dt.$$
\end{lemma}
%%-/


lemma limiting_fourier {Ïˆ:â„ â†’ â„‚} (hÏˆ: ContDiff â„ 2 Ïˆ) (hsupp: HasCompactSupport Ïˆ) {x:â„} (hx: 1 â‰¤ x) : âˆ‘' n, f n / n * fourierIntegral Ïˆ (1/(2*Ï€) * log (n/x)) - A * âˆ« u in Set.Ici (-log x), fourierIntegral Ïˆ (u / (2*Ï€)) âˆ‚ volume = âˆ« (t : â„), (G (1 + I*t)) * (Ïˆ t) * x^(I * t) âˆ‚ volume := by
  sorry

/-%%
\begin{proof}
\uses{first-fourier,second-fourier,decay}
 By the preceding two lemmas, we know that for any $\sigma>1$, we have
  $$ \sum_{n=1}^\infty \frac{f(n)}{n^\sigma} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) - A x^{1-\sigma} \int_{-\log x}^\infty e^{-u(\sigma-1)} \hat \psi(\frac{u}{2\pi})\ du =  \int_\R G(\sigma+it) \psi(t) x^{it}\ dt.$$
  Now take limits as $\sigma \to 1$ using dominated convergence together with \eqref{cheby} and Lemma \ref{decay} to obtain the result.
\end{proof}
%%-/

/-%%
\begin{corollary}[Corollary of limiting identity]\label{limiting-cor}\lean{limiting_cor}\leanok  With the hypotheses as above, we have
  $$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) = A \int_{-\infty}^\infty \hat \psi(\frac{u}{2\pi})\ du + o(1)$$
  as $x \to \infty$.
\end{corollary}
%%-/

open Filter

lemma limiting_cor {Ïˆ:â„ â†’ â„‚} (hÏˆ: ContDiff â„ 2 Ïˆ) (hsupp: HasCompactSupport Ïˆ) : Tendsto (fun x : â„ â†¦ âˆ‘' n, f n / n * fourierIntegral Ïˆ (1/(2*Ï€) * log (n/x)) - A * âˆ« u in Set.Ici (-log x), fourierIntegral Ïˆ (u / (2*Ï€)) âˆ‚ volume) atTop (nhds 0) := by sorry

/-%%
\begin{proof}
\uses{limiting}
 Immediate from the Riemann-Lebesgue lemma, and also noting that $\int_{-\infty}^{-\log x} \hat \psi(\frac{u}{2\pi})\ du = o(1)$.
\end{proof}
%%-/

/-%%
\begin{lemma}[Smooth Urysohn lemma]\label{smooth-ury}\lean{smooth_urysohn}\leanok  If $I$ is a closed interval contained in an open interval $J$, then there exists a smooth function $\Psi: \R \to \R$ with $1_I \leq \Psi \leq 1_J$.
\end{lemma}
%%-/

lemma smooth_urysohn {a b c d:â„} (h1: a < b) (h2: b<c) (h3: c < d) : âˆƒ Î¨:â„ â†’ â„, (âˆ€ n, ContDiff â„ n Î¨) âˆ§ (HasCompactSupport Î¨) âˆ§ Set.indicator (Set.Icc b c) 1 â‰¤ Î¨ âˆ§ Î¨ â‰¤ Set.indicator (Set.Ioo a d) 1 := by
  have := exists_smooth_zero_one_of_isClosed (modelWithCornersSelf â„ â„) (s := Set.Iic a âˆª Set.Ici d) (t := Set.Icc b c)
    (IsClosed.union isClosed_Iic isClosed_Ici)
    (isClosed_Icc)
    (by
      simp_rw [Set.disjoint_union_left, Set.disjoint_iff, Set.subset_def, Set.mem_inter_iff, Set.mem_Iic, Set.mem_Icc,
        Set.mem_empty_iff_false, and_imp, imp_false, not_le, Set.mem_Ici]
      constructor <;> intros <;> linarith)
  rcases this with âŸ¨âŸ¨Î¨, hÎ¨contMDiffâŸ©, hÎ¨0, hÎ¨1, hÎ¨01âŸ©
  simp only [Set.EqOn, Set.mem_setOf_eq, Set.mem_union, Set.mem_Iic, Set.mem_Ici,
    ContMDiffMap.coeFn_mk, Pi.zero_apply, Set.mem_Icc, Pi.one_apply, and_imp] at *
  use Î¨
  constructor
  Â· rw [contDiff_all_iff_nat, â†contDiff_top]
    exact ContMDiff.contDiff hÎ¨contMDiff
  Â· constructor
    Â· rw [hasCompactSupport_def]
      apply IsCompact.closure_of_subset (K := Set.Icc a d) isCompact_Icc
      rw [Function.support_subset_iff]
      intro x hx
      contrapose! hx
      simp only [Set.mem_Icc, not_and_or] at hx
      apply hÎ¨0
      by_contra! h'
      cases' hx <;> linarith
    Â· constructor
      Â· intro x
        rw [Set.indicator_apply]
        split_ifs with h
        simp only [Set.mem_Icc, Pi.one_apply] at *
        rw [hÎ¨1 h.left h.right]
        exact (hÎ¨01 x).left
      Â· intro x
        rw [Set.indicator_apply]
        split_ifs with h
        simp at *
        exact (hÎ¨01 x).right
        rw [hÎ¨0]
        simp only [Set.mem_Ioo, not_and_or] at h
        by_contra! h'
        cases' h <;> linarith
  done


/-%%
\begin{proof}  \leanok
A standard analysis lemma, which can be proven by convolving $1_K$ with a smooth approximation to the identity for some interval $K$ between $I$ and $J$. Note that we have ``SmoothBumpFunction''s on smooth manifolds in Mathlib, so this shouldn't be too hard...
\end{proof}
%%-/

/-%%
\begin{lemma}[Limiting identity for Schwartz functions]\label{schwarz-id}\lean{limiting_cor_schwartz}\leanok  The previous corollary also holds for functions $\psi$ that are assumed to be in the Schwartz class, as opposed to being $C^2$ and compactly supported.
\end{lemma}
%%-/

lemma limiting_cor_schwartz {Ïˆ: SchwartzMap â„ â„‚} : Tendsto (fun x : â„ â†¦ âˆ‘' n, f n / n * fourierIntegral Ïˆ (1/(2*Ï€) * log (n/x)) - A * âˆ« u in Set.Ici (-log x), fourierIntegral Ïˆ (u / (2*Ï€)) âˆ‚ volume) atTop (nhds 0) := by sorry

/-%%
\begin{proof}
\uses{limiting-cor, smooth-ury}
For any $R>1$, one can use a smooth cutoff function (provided by Lemma \ref{smooth-ury} to write $\psi = \psi_{\leq R} + \psi_{>R}$, where $\psi_{\leq R}$ is $C^2$ (in fact smooth) and compactly supported (on $[-R,R]$), and $\psi_{>R}$ obeys bounds of the form
$$ |\psi_{>R}(t)|, |\psi''_{>R}(t)| \ll R^{-1} / (1 + |t|^2) $$
where the implied constants depend on $\psi$.  By Lemma \ref{decay} we then have
$$ \hat \psi_{>R}(u) \ll R^{-1} / (1+|u|^2).$$
Using this and \eqref{cheby} one can show that
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi_{>R}( \frac{1}{2\pi} \log \frac{n}{x} ), A \int_{-\infty}^\infty \hat \psi_{>R} (\frac{u}{2\pi})\ du \ll R^{-1} $$
(with implied constants also depending on $A$), while from Lemma \ref{limiting-cor} one has
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi_{\leq R}( \frac{1}{2\pi} \log \frac{n}{x} ) = A \int_{-\infty}^\infty \hat \psi_{\leq R} (\frac{u}{2\pi})\ du + o(1).$$
Combining the two estimates and letting $R$ be large, we obtain the claim.
\end{proof}
%%-/

/-%%
\begin{lemma}[Bijectivity of Fourier transform]\label{bij}\lean{fourier_surjection_on_schwartz}\leanok  The Fourier transform is a bijection on the Schwartz class.
\end{lemma}
%%-/

-- just the surjectivity is stated here, as this is all that is needed for the current application, but perhaps one should state and prove bijectivity instead

lemma fourier_surjection_on_schwartz (f : SchwartzMap â„ â„‚) : âˆƒ g : SchwartzMap â„ â„‚, fourierIntegral g = f := by sorry

/-%%
\begin{proof}
\uses{MellinInversion}
 This is a standard result in Fourier analysis.
It can be proved here by appealing to Mellin inversion, Theorem \ref{MellinInversion}.
In particular, given $f$ in the Schwartz class, let $F : \R_+ \to \C : x \mapsto f(\log x)$ be a function in the ``Mellin space''; then the Mellin transform of $F$ on the imaginary axis $s=it$ is the Fourier transform of $f$.  The Mellin inversion theorem gives Fourier inversion.
\end{proof}
%%-/

/-%%
\begin{corollary}[Smoothed Wiener-Ikehara]\label{WienerIkeharaSmooth}\lean{wiener_ikehara_smooth}\leanok
  If $\Psi: (0,\infty) \to \C$ is smooth and compactly supported away from the origin, then, then
$$ \sum_{n=1}^\infty f(n) \Psi( \frac{n}{x} ) = A x \int_0^\infty \Psi(y)\ dy + o(x)$$
as $u \to \infty$.
\end{corollary}
%%-/

lemma wiener_ikehara_smooth {Î¨: â„ â†’ â„‚} (hsmooth: âˆ€ n, ContDiff â„ n Î¨) (hsupp: HasCompactSupport Î¨) (hplus: closure (Function.support Î¨) âŠ† Set.Ioi (0:â„)): Tendsto (fun x : â„ â†¦ (âˆ‘' n, f n / n * Î¨ (n/x))/x - A * âˆ« y in Set.Ioi 0, Î¨ y âˆ‚ volume) atTop (nhds 0) := by sorry

/-%%
\begin{proof}
\uses{bij,schwarz-id}
 By Lemma \ref{bij}, we can write
$$ y \Psi(y) = \hat \psi( \frac{1}{2\pi} \log y )$$
for all $y>0$ and some Schwartz function $\psi$.  Making this substitution, the claim is then equivalent after standard manipulations to
$$ \sum_{n=1}^\infty \frac{f(n)}{n} \hat \psi( \frac{1}{2\pi} \log \frac{n}{x} ) = A \int_{-\infty}^\infty \hat \psi(\frac{u}{2\pi})\ du + o(1)$$
and the claim follows from Lemma \ref{schwarz-id}.
\end{proof}
%%-/


/-%%
Now we add the hypothesis that $f(n) \geq 0$ for all $n$.

\begin{proposition}[Wiener-Ikehara in an interval]
\label{WienerIkeharaInterval}\lean{WienerIkeharaInterval}\leanok
  For any closed interval $I \subset (0,+\infty)$, we have
  $$ \sum_{n=1}^\infty f(n) 1_I( \frac{n}{x} ) = A x |I|  + o(x).$$
\end{proposition}
%%-/

variable (hpos: âˆ€ n, 0 â‰¤ f n)

lemma WienerIkeharaInterval (a b:â„) (ha: 0 < a) (hb: a < b) : Tendsto (fun x : â„ â†¦ âˆ‘' n, f n / n * (Set.indicator (Set.Icc a b) 1 (n/x))/x - A * (b-a)) atTop (nhds 0) := by
  sorry

/-%%
\begin{proof}
\uses{smooth-ury, WienerIkeharaSmooth}
  Use Lemma \ref{smooth-ury} to bound $1_I$ above and below by smooth compactly supported functions whose integral is close to the measure of $|I|$, and use the non-negativity of $f$.
\end{proof}
%%-/

/-%%
\begin{corollary}[Wiener-Ikehara theorem]\label{WienerIkehara}\lean{WienerIkeharaTheorem'}\leanok
  We have
$$ \sum_{n\leq x} f(n) = A x |I|  + o(x).$$
\end{corollary}
%%-/
open Filter Nat ArithmeticFunction in
/-- A version of the *Wiener-Ikehara Tauberian Theorem*: If `f` is a nonnegative arithmetic
function whose L-series has a simple pole at `s = 1` with residue `A` and otherwise extends
continuously to the closed half-plane `re s â‰¥ 1`, then `âˆ‘ n < N, f n` is asymptotic to `A*N`. -/
theorem WienerIkeharaTheorem' {f : ArithmeticFunction â„} {A : â„} {F : â„‚ â†’ â„‚} (hf : âˆ€ n, 0 â‰¤ f n)
    (hF : Set.EqOn F (fun s â†¦ LSeries f s - A / (s - 1)) {s | 1 < s.re})
    (hF' : ContinuousOn F {s | 1 â‰¤ s.re}) :
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum f) / N) atTop (nhds A) := by
  sorry
/-%%
\begin{proof}
\uses{WienerIkeharaInterval}
  Apply the preceding proposition with $I = [\varepsilon,1]$ and then send $\varepsilon$ to zero (using \eqref{cheby} to control the error).
\end{proof}
%%-/

/-%%
\section{Weak PNT}

\begin{theorem}[Weak PNT]\label{WeakPNT}\lean{WeakPNT}\leanok  We have
$$ \sum_{n \leq x} \Lambda(n) = x + o(x).$$
\end{theorem}
%%-/
theorem WeakPNT :
    Tendsto (fun N : â„• â†¦ ((Finset.range N).sum Î›) / N) atTop (nhds 1) := by
  sorry
/-%%
\begin{proof}
\uses{WienerIkehara, ChebyshevPsi}
  Already done by Stoll, assuming Wiener-Ikehara.
\end{proof}
%%-/
