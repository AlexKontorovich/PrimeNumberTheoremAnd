import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.MeasureTheory.Integral.IntervalIntegral
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.NumberTheory.ZetaFunction
import Mathlib.Algebra.Group.Basic
import EulerProducts.PNT
import PrimeNumberTheoremAnd.ResidueCalcOnRectangles

open BigOperators Complex Topology Filter Interval

theorem ContDiffOn.hasDeriv_deriv {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {s : Set ‚Ñù} (œÜDiff : ContDiffOn ‚Ñù 1 œÜ s) {x : ‚Ñù}
    (x_in_s : s ‚àà nhds x) : HasDerivAt œÜ (deriv œÜ x) x :=
  (ContDiffAt.hasStrictDerivAt (œÜDiff.contDiffAt x_in_s) (by simp)).hasDerivAt

theorem ContDiffOn.continuousOn_deriv {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {a b : ‚Ñù}
    (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.uIoo a b)) :
    ContinuousOn (deriv œÜ) (Set.uIoo a b) := by
  apply ContDiffOn.continuousOn (ùïú := ‚Ñù) (n := 0)
  exact (fun h => ((contDiffOn_succ_iff_deriv_of_isOpen isOpen_Ioo).1 h).2) œÜDiff

theorem LinearDerivative_ofReal (x : ‚Ñù) (a b : ‚ÑÇ) : HasDerivAt (fun (t : ‚Ñù) ‚Ü¶ a * t + b) a x := by
  refine HasDerivAt.add_const ?_ b
  have := @ContinuousLinearMap.hasDerivAt (e := Complex.ofRealCLM) x
  have := this.const_mul (c := a)
  convert this using 1; simp

lemma sum_eq_int_deriv_aux2 {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {a b : ‚Ñù} (a_lt_b : a < b) (c : ‚ÑÇ)
    (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b)) :
    ‚à´ (x : ‚Ñù) in a..b, (c - x) * deriv œÜ x =
      (c - b) * œÜ b - (c - a) * œÜ a + ‚à´ (x : ‚Ñù) in a..b, œÜ x := by
  set v' := deriv œÜ
  set v := œÜ
  set u := fun (x : ‚Ñù) ‚Ü¶ c - x
  set u' := fun (x : ‚Ñù) ‚Ü¶ (-1 : ‚ÑÇ)
  have hu : ‚àÄ x ‚àà Set.uIcc a b, HasDerivAt u (u' x) x := by
    intros x hx
    convert LinearDerivative_ofReal x (-1 : ‚ÑÇ) c; ring
  have hv : ‚àÄ x ‚àà Set.uIcc a b, HasDerivAt v (v' x) x := by
    refine fun x hx ‚Ü¶ œÜDiff.hasDeriv_deriv ?_
    --- argh, what if x=a or b :( Need to somehow replace `uIcc` with `uIoo`
    sorry
  have hu' : IntervalIntegrable u' MeasureTheory.volume a b := by
    apply Continuous.intervalIntegrable
    continuity
  have hv' : IntervalIntegrable v' MeasureTheory.volume a b := by
    apply ContinuousOn.intervalIntegrable
    -- same problem, need to replace `uIcc` with `uIoo`
    --have := œÜDiff.continuousOn_deriv
    --convert ContDiffOn.continuousOn_deriv
    sorry
  convert intervalIntegral.integral_mul_deriv_eq_deriv_mul hu hv hu' hv' using 1
  simp [v, u]

lemma sum_eq_int_deriv_aux_eq {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {a b : ‚Ñù} {k : ‚Ñ§} (a_lt_b : a < b)
    (b_eq_kpOne : b = k + 1) (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b)) :
    ‚àë n in Finset.Ioc k ‚åäb‚åã, œÜ n =
    (‚à´ x in a..b, œÜ x) + (‚åäb‚åã + 1 / 2 - b) * œÜ b - (k + 1 / 2 - a) * œÜ a
      - ‚à´ x in a..b, (k + 1 / 2 - x) * deriv œÜ x := by
  have flb_eq_k : ‚åäb‚åã = k + 1 := Int.floor_eq_iff.mpr ‚ü®by exact_mod_cast b_eq_kpOne.symm.le,
    by rw [b_eq_kpOne]; simp‚ü©
  simp only [flb_eq_k, Finset.Icc_self, Finset.sum_singleton, Int.cast_add, Int.cast_one]
  rw [sum_eq_int_deriv_aux2 a_lt_b (k + 1 / 2) œÜDiff, b_eq_kpOne]
  ring_nf
  have : Finset.Ioc k (1 + k) = {k + 1} := by
    ext m
    simp only [Finset.mem_Ioc, Finset.mem_singleton]
    constructor
    ¬∑ intro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
      rw [add_comm] at h‚ÇÇ
      exact Int.le_antisymm h‚ÇÇ h‚ÇÅ
    ¬∑ exact fun h ‚Ü¶ ‚ü®by simp [h], by simp [h, add_comm]‚ü©
  simp_rw [this]
  simp only [Finset.sum_singleton, Int.cast_add, Int.cast_one, add_comm]

lemma sum_eq_int_deriv_aux_lt {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {a b : ‚Ñù} {k : ‚Ñ§} (k_le_a : k ‚â§ a) (a_lt_b : a < b)
    (b_lt_kpOne : b < k + 1) (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b)) :
    ‚àë n in Finset.Ioc k ‚åäb‚åã, œÜ n =
    (‚à´ x in a..b, œÜ x) + (‚åäb‚åã + 1 / 2 - b) * œÜ b - (k + 1 / 2 - a) * œÜ a
      - ‚à´ x in a..b, (k + 1 / 2 - x) * deriv œÜ x := by
  have flb_eq_k : ‚åäb‚åã = k := Int.floor_eq_iff.mpr ‚ü®by linarith, by linarith‚ü©
  simp only [flb_eq_k, gt_iff_lt, lt_add_iff_pos_right, zero_lt_one, Finset.Icc_eq_empty_of_lt,
    Finset.sum_empty]
  rw [sum_eq_int_deriv_aux2 a_lt_b (k + 1 / 2) œÜDiff]
  have : Finset.Ioc k k = {} := by
    simp only [ge_iff_le, le_refl, Finset.Ioc_eq_empty_of_le]
  simp only [this, Finset.sum_empty, one_div]
  ring_nf

lemma sum_eq_int_deriv_aux1 {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {a b : ‚Ñù} {k : ‚Ñ§} (k_le_a : k ‚â§ a) (a_lt_b : a < b)
    --(a_lt_kpOne : a < k + 1)
    (b_le_kpOne : b ‚â§ k + 1) (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b)) :
    ‚àë n in Finset.Ioc k ‚åäb‚åã, œÜ n =
    (‚à´ x in a..b, œÜ x) + (‚åäb‚åã + 1 / 2 - b) * œÜ b - (k + 1 / 2 - a) * œÜ a
      - ‚à´ x in a..b, (k + 1 / 2 - x) * deriv œÜ x := by
  by_cases h : b = k + 1
  ¬∑ exact sum_eq_int_deriv_aux_eq a_lt_b h œÜDiff
  ¬∑ exact sum_eq_int_deriv_aux_lt k_le_a a_lt_b (Ne.lt_of_le h b_le_kpOne) œÜDiff

/-%%
\begin{lemma}[sum_eq_int_deriv_aux]\label{sum_eq_int_deriv_aux}\lean{sum_eq_int_deriv_aux}\leanok
  Let $k \le a < b\le k+1$, with $k$ an integer, and let $\phi$ be continuously differentiable on
  $[a, b]$.
  Then
  \[
  \sum_{a < n \le b} \phi(n) = \int_a^b \phi(x) \, dx + \left(\lfloor b \rfloor + \frac{1}{2} - b\right) \phi(b) - \left(\lfloor a \rfloor + \frac{1}{2} - a\right) \phi(a) - \int_a^b \left(\lfloor x \rfloor + \frac{1}{2} - x\right) \phi'(x) \, dx.
  \]
\end{lemma}
%%-/
lemma sum_eq_int_deriv_aux {œÜ : ‚Ñù ‚Üí ‚ÑÇ} {a b : ‚Ñù} {k : ‚Ñ§} (k_le_a : k ‚â§ a) (a_lt_b : a < b)
    (b_le_kpOne : b ‚â§ k + 1) (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b)) :
    ‚àë n in Finset.Ioc ‚åäa‚åã ‚åäb‚åã, œÜ n =
    (‚à´ x in a..b, œÜ x) + (‚åäb‚åã + 1 / 2 - b) * œÜ b - (‚åäa‚åã + 1 / 2 - a) * œÜ a
      - ‚à´ x in a..b, (‚åäx‚åã + 1 / 2 - x) * deriv œÜ x := by
  have fl_a_eq_k : ‚åäa‚åã = k := Int.floor_eq_iff.mpr ‚ü®k_le_a, by linarith‚ü©
  convert sum_eq_int_deriv_aux1 k_le_a a_lt_b b_le_kpOne œÜDiff using 2 <;> try {congr}
  apply intervalIntegral.integral_congr_ae
  have :  ‚àÄ·µê (x : ‚Ñù) ‚àÇMeasureTheory.volume, x ‚â† b := by
    convert Set.Countable.ae_not_mem (s := {b}) (by simp) (Œº := MeasureTheory.volume) using 1
  filter_upwards [this]
  intro x x_ne_b hx
  rw [Set.uIoc_of_le a_lt_b.le, Set.mem_Ioc] at hx
  congr
  exact Int.floor_eq_iff.mpr ‚ü®by linarith, by have := Ne.lt_of_le x_ne_b hx.2; linarith‚ü©
/-%%
\begin{proof}\leanok
Partial integration.
\end{proof}
%%-/

-- Thanks to Arend Mellendijk

lemma interval_induction_aux_int (n : ‚Ñï) : ‚àÄ (P : ‚Ñù ‚Üí ‚Ñù ‚Üí Prop)
    (_ : ‚àÄ a b : ‚Ñù, ‚àÄ k : ‚Ñ§, k ‚â§ a ‚Üí a < b ‚Üí b ‚â§ k + 1 ‚Üí P a b)
    (_ : ‚àÄ (a : ‚Ñù) (k : ‚Ñ§) (c : ‚Ñù), a < k ‚Üí k < c ‚Üí P a k ‚Üí P k c ‚Üí P a c)
    (a b : ‚Ñù) (_ : a < b) (_ : n = ‚åäb‚åã - ‚åäa‚åã),
    P a b := by
  induction n using Nat.case_strong_induction_on with
  | hz =>
    intro P base _ a b hab hn
    apply base a b ‚åäa‚åã (Int.floor_le a) hab
    simp only [CharP.cast_eq_zero] at hn
    rw [(by linarith : ‚åäa‚åã = ‚åäb‚åã)]
    exact (Int.lt_floor_add_one b).le
  | hi n ih =>
    intro P base step a b _ hn
    have Pa : P a (‚åäa‚åã + 1) :=
      base a (‚åäa‚åã + 1) ‚åäa‚åã (Int.floor_le a) (Int.lt_floor_add_one a) (le_of_eq rfl)
    by_cases b_le_flaP1 : b = ‚åäa‚åã + 1
    ¬∑ rwa [b_le_flaP1]
    have flaP1_lt_b : ‚åäa‚åã + 1 < b := by
      simp only [Nat.cast_succ] at hn
      have := Int.floor_le b
      have : 0 ‚â§ n := Nat.zero_le n
      have : ‚åäa‚åã + 1 ‚â§ ‚åäb‚åã := by linarith
      have : (‚åäa‚åã : ‚Ñù) + 1 ‚â§ ‚åäb‚åã := by exact_mod_cast this
      push_neg at b_le_flaP1
      exact Ne.lt_of_le (id (Ne.symm b_le_flaP1)) (by linarith : ‚åäa‚åã + 1 ‚â§ b)
    have Pfla_b : P (‚åäa‚åã + 1) b := by
      apply ih n (le_of_eq rfl) P base step (‚åäa‚åã + 1) b flaP1_lt_b
      simp only [Int.floor_add_one, Int.floor_intCast, Nat.cast_succ] at hn ‚ä¢
      rw [sub_eq_add_neg, neg_add, ‚Üê add_assoc, ‚Üê sub_eq_add_neg (a := ‚åäb‚åã), ‚Üê hn]
      ring
    refine step a (‚åäa‚åã + 1) b ?_ (by exact_mod_cast flaP1_lt_b) (by exact_mod_cast Pa)
      (by exact_mod_cast Pfla_b)
    have := Int.lt_floor_add_one a
    exact_mod_cast this

lemma interval_induction (P : ‚Ñù ‚Üí ‚Ñù ‚Üí Prop)
    (base : ‚àÄ a b : ‚Ñù, ‚àÄ k : ‚Ñ§, k ‚â§ a ‚Üí a < b ‚Üí b ‚â§ k + 1 ‚Üí P a b)
    (step : ‚àÄ (a : ‚Ñù) (k : ‚Ñ§) (b : ‚Ñù), a < k ‚Üí k < b ‚Üí P a k ‚Üí P k b ‚Üí P a b)
    (a b : ‚Ñù) (hab : a < b) : P a b := by
  set n := ‚åäb‚åã - ‚åäa‚åã with hn
  clear_value n
  have : 0 ‚â§ n := by
    have : ‚åäa‚åã ‚â§ ‚åäb‚åã := Int.floor_le_floor _ _ (hab.le)
    simp only [hn, sub_nonneg, ge_iff_le]
    exact this
  lift n to ‚Ñï using this
  exact interval_induction_aux_int n P base step a b hab hn

/-%%
\begin{lemma}[sum_eq_int_deriv]\label{sum_eq_int_deriv}\lean{sum_eq_int_deriv}\leanok
  Let $a < b$, and let $\phi$ be continuously differentiable on $[a, b]$.
  Then
  \[
  \sum_{a < n \le b} \phi(n) = \int_a^b \phi(x) \, dx + \left(\lfloor b \rfloor + \frac{1}{2} - b\right) \phi(b) - \left(\lfloor a \rfloor + \frac{1}{2} - a\right) \phi(a) - \int_a^b \left(\lfloor x \rfloor + \frac{1}{2} - x\right) \phi'(x) \, dx.
  \]
\end{lemma}
%%-/
/-- ** Partial summation ** (TODO : Add to Mathlib).
  Note: Need to finish proof of `sum_eq_int_deriv_aux2` -/

-- stupid lemma -- what's the better way to do this?
lemma add_two {a b c d : ‚ÑÇ} (h : a = b) (h' : c = d) : a + c = b + d := by
  exact Mathlib.Tactic.LinearCombination.add_pf h h'

-- In Ya√´l Dillies's API (https://leanprover.zulipchat.com/#narrow/stream/217875-Is-there-code-for-X.3F/topic/Finset.2Esum_add_adjacent_intervals/near/430127101)
lemma Finset.sum_Ioc_add_sum_Ioc {a b c : ‚Ñ§} (f : ‚Ñ§ ‚Üí ‚ÑÇ) (h : a ‚â§ b) (h' : b ‚â§ c) :
  (‚àë n in Finset.Ioc a b, f n) + (‚àë n in Finset.Ioc b c, f n) = ‚àë n in Finset.Ioc a c, f n := by
  sorry

lemma integrability_aux {a b : ‚Ñù} (a_lt_b : a < b) :
    IntervalIntegrable (fun (x : ‚Ñù) ‚Ü¶ (‚åäx‚åã : ‚ÑÇ) + 1 / 2 - x) MeasureTheory.volume a b := sorry

lemma ContinuousOn_derivWithin_of_deriv (œÜ : ‚Ñù ‚Üí ‚ÑÇ) (a b : ‚Ñù)
    (h : ContinuousOn (deriv œÜ) (Set.uIcc a b)) :
    ContinuousOn (derivWithin œÜ (Set.uIcc a b)) (Set.uIcc a b) := by sorry

theorem sum_eq_int_deriv {œÜ : ‚Ñù ‚Üí ‚ÑÇ} (a b : ‚Ñù) (a_lt_b : a < b)
    (œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b)) :
    ‚àë n in Finset.Ioc ‚åäa‚åã ‚åäb‚åã, œÜ n =
      (‚à´ x in a..b, œÜ x) + (‚åäb‚åã + 1 / 2 - b) * œÜ b - (‚åäa‚åã + 1 / 2 - a) * œÜ a
        - ‚à´ x in a..b, (‚åäx‚åã + 1 / 2 - x) * deriv œÜ x := by
  let P : ‚Ñù ‚Üí ‚Ñù ‚Üí Prop := fun a‚ÇÅ b‚ÇÅ ‚Ü¶ (ContDiffOn ‚Ñù 1 œÜ (Set.Icc a‚ÇÅ b‚ÇÅ)) ‚Üí
    ‚àë n in Finset.Ioc ‚åäa‚ÇÅ‚åã ‚åäb‚ÇÅ‚åã, œÜ n =
    (‚à´ x in a‚ÇÅ..b‚ÇÅ, œÜ x) + (‚åäb‚ÇÅ‚åã + 1 / 2 - b‚ÇÅ) * œÜ b‚ÇÅ - (‚åäa‚ÇÅ‚åã + 1 / 2 - a‚ÇÅ) * œÜ a‚ÇÅ
      - ‚à´ x in a‚ÇÅ..b‚ÇÅ, (‚åäx‚åã + 1 / 2 - x) * deriv œÜ x
  apply interval_induction P ?_ ?_ a b a_lt_b œÜDiff
  ¬∑ exact fun _ _ _ k_le_a‚ÇÅ a‚ÇÅ_le_b‚ÇÅ b‚ÇÅ_le_k1 œÜDiff‚ÇÅ ‚Ü¶
      sum_eq_int_deriv_aux k_le_a‚ÇÅ a‚ÇÅ_le_b‚ÇÅ b‚ÇÅ_le_k1 œÜDiff‚ÇÅ
  ¬∑ intro a‚ÇÅ k‚ÇÅ b‚ÇÅ a_lt_k‚ÇÅ k_lt_b‚ÇÅ ih‚ÇÅ ih‚ÇÇ œÜDiff‚ÇÅ
    have : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a‚ÇÅ k‚ÇÅ) := by
      apply œÜDiff‚ÇÅ.mono
      rw [Set.Icc_subset_Icc_iff] <;> simp [k_lt_b‚ÇÅ.le, a_lt_k‚ÇÅ.le]
    have s‚ÇÅ := ih‚ÇÅ this
    have : ContDiffOn ‚Ñù 1 œÜ (Set.Icc k‚ÇÅ b‚ÇÅ) := by
      apply œÜDiff‚ÇÅ.mono
      rw [Set.Icc_subset_Icc_iff] <;> simp [k_lt_b‚ÇÅ.le, a_lt_k‚ÇÅ.le]
    have s‚ÇÇ := ih‚ÇÇ this
    convert add_two s‚ÇÅ s‚ÇÇ using 1
    ¬∑ rw [‚Üê Finset.sum_Ioc_add_sum_Ioc]
      ¬∑ exact Int.floor_mono a_lt_k‚ÇÅ.le
      ¬∑ exact Int.floor_mono k_lt_b‚ÇÅ.le
    ¬∑ set I‚ÇÅ := ‚à´ (x : ‚Ñù) in a‚ÇÅ..b‚ÇÅ, œÜ x
      set I‚ÇÇ := ‚à´ (x : ‚Ñù) in a‚ÇÅ..k‚ÇÅ, œÜ x
      set I‚ÇÉ := ‚à´ (x : ‚Ñù) in k‚ÇÅ..b‚ÇÅ, œÜ x
      set J‚ÇÅ := ‚à´ (x : ‚Ñù) in a‚ÇÅ..b‚ÇÅ, (‚Üë‚åäx‚åã + 1 / 2 - ‚Üëx) * deriv œÜ x
      set J‚ÇÇ := ‚à´ (x : ‚Ñù) in a‚ÇÅ..k‚ÇÅ, (‚Üë‚åäx‚åã + 1 / 2 - ‚Üëx) * deriv œÜ x
      set J‚ÇÉ := ‚à´ (x : ‚Ñù) in k‚ÇÅ..b‚ÇÅ, (‚Üë‚åäx‚åã + 1 / 2 - ‚Üëx) * deriv œÜ x
      have : I‚ÇÇ + I‚ÇÉ = I‚ÇÅ := by
        apply intervalIntegral.integral_add_adjacent_intervals <;>
        apply ContinuousOn.intervalIntegrable <;>
        apply ContDiffOn.continuousOn (n := 1) (ùïú := ‚Ñù) <;>
        apply œÜDiff‚ÇÅ.mono <;>
        simp [Set.uIcc_of_le, a_lt_k‚ÇÅ.le, k_lt_b‚ÇÅ.le, Set.Icc_subset_Icc_iff]
      rw [‚Üê this]
      have : J‚ÇÇ + J‚ÇÉ = J‚ÇÅ := by
        apply intervalIntegral.integral_add_adjacent_intervals <;>
        apply IntervalIntegrable.mul_continuousOn
        ¬∑ apply integrability_aux a_lt_k‚ÇÅ
        ¬∑ -- have := ((contDiff_succ_iff_deriv (f‚ÇÇ := œÜ) (n := 0)).mp ?_).2.
          -- have : UniqueDiffOn ‚Ñù (Set.uIcc a‚ÇÅ k‚ÇÅ) := by
          --   sorry
          -- have := ((contDiffOn_succ_iff_derivWithin this (f‚ÇÇ := œÜ) (n := 0)).mp ?_).2.continuousOn
          sorry
        ¬∑ sorry
        ¬∑ sorry
      rw [‚Üê this]
      ring
/-%%
\begin{proof}\uses{sum_eq_int_deriv_aux}
  Apply Lemma \ref{sum_eq_int_deriv_aux} in blocks of length $\le 1$.
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaSum_aux1]\label{ZetaSum_aux1}\lean{ZetaSum_aux1}\leanok
  Let $a < b$ be natural numbers and $s\in \C$ with $s \ne 1$.
  Then
  \[
  \sum_{a < n \le b} \frac{1}{n^s} =  \frac{b^{1-s} - a^{1-s}}{1-s} + \frac{b^{-s}-a^{-s}}{2} + s \int_a^b \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx.
  \]
\end{lemma}
%%-/
lemma ZetaSum_aux1 {a b : ‚Ñï} {s : ‚ÑÇ} (s_ne_one : s ‚â† 1) (a_lt_b : a < b) :
    ‚àë n in Finset.Icc (a + 1) b, 1 / (n : ‚ÑÇ)^s =
    (b ^ (1 - s) - a ^ (1 - s)) / (1 - s) + (b ^ (-s) - a ^ (-s)) / 2
      + s * ‚à´ x in a..b, (‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^(s + 1) := by
  let œÜ := fun (x : ‚Ñù) ‚Ü¶ (x : ‚ÑÇ) ^ (-s)
  let œÜ' := fun (x : ‚Ñù) ‚Ü¶ -s / (x : ‚ÑÇ)^(s + 1)
  have œÜDiff : ContDiffOn ‚Ñù 1 œÜ (Set.Icc a b) := sorry
  -- convert sum_eq_int_deriv (by exact_mod_cast a_lt_b) œÜDiff using 1
  -- ¬∑ sorry
  -- ¬∑ sorry
  sorry
/-%%
\begin{proof}\uses{sum_eq_int_deriv}
  Apply Lemma \ref{sum_eq_int_deriv} to the function $x \mapsto x^{-s}$.
\end{proof}
%%-/

lemma ZetaSum_aux1a_aux1 {a b x : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) (hx : x ‚àà [[a,b]])
    : x > 0 := by
  rcases hx with ‚ü®h, _‚ü©
  have : a ‚äì b > 0 := by
    rw [inf_eq_min]
    have : b > 0 := by
      exact lt_of_lt_of_le apos (le_of_lt a_lt_b)
    exact lt_min apos this
  exact lt_of_lt_of_le this h

lemma ZetaSum_aux1a_aux1' {a b x : ‚Ñù} (apos : 0 < a) (hx : x ‚àà Set.Icc a b)
    : x > 0 := by
  rcases hx with ‚ü®h, _‚ü©
  exact lt_of_lt_of_le apos h

lemma ZetaSum_aux1a_aux2a  {x r : ‚Ñù} (hx : x > 0) : 1 / x^r = x^(-r) := by
  have h : x^(-r) * x^(r) = 1 := by
    rw [‚Üê Real.rpow_add hx (-r) (r)]
    simp only [add_left_neg, Real.rpow_zero]
  have h' : x^r ‚â† 0 := by
    intro h'
    rw [h', mul_zero] at h
    exact zero_ne_one h
  exact div_eq_of_eq_mul h' h.symm

lemma ZetaSum_aux1a_aux2 {a b : ‚Ñù} {c : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b)
    (h : c ‚â† 0 ‚àß 0 ‚àâ [[a, b]]) :
    ‚à´ (x : ‚Ñù) in a..b, 1/x^(c+1) = (a ^ (-c) - b ^ (-c)) / c := by
  have : (a ^ (-c) - b ^ (-c)) / c = (b ^ (-c) - a ^ (-c)) / (-c) := by
    ring
  rw [this]
  have : -c-1 ‚â† -1 := by
    simp only [ne_eq, sub_eq_neg_self, neg_eq_zero]
    exact h.1
  have : -c-1 ‚â† -1 ‚àß 0 ‚àâ [[a, b]] := ‚ü® this, h.2 ‚ü©
  have := integral_rpow (a := a) (b := b) (r := -c-1) (Or.inr this)
  simp only [sub_add_cancel] at this
  rw [‚Üê this]
  apply intervalIntegral.integral_congr
  intro x h
  simp only
  have : x > 0 := by
    exact ZetaSum_aux1a_aux1 apos a_lt_b h
  rw [ZetaSum_aux1a_aux2a this]
  congr
  ring

lemma ZetaSum_aux1a_aux3a (x : ‚Ñù) : -(1/2) < ‚åä x ‚åã + 1/2 - x := by
  have : 0 < (‚åä x ‚åã + 1) - x := by
    exact sub_pos_of_lt (Int.lt_floor_add_one x)
  calc
    _ = -1/2 := by norm_num
    _ < -1/2 + ((‚åä x ‚åã + 1) - x) := lt_add_of_pos_right (-1/2) this
    _ = _ := by ring

lemma ZetaSum_aux1a_aux3b (x : ‚Ñù) : ‚åäx‚åã + 1/2 - x ‚â§ 1/2 := by
  have : ‚åäx‚åã - x ‚â§ 0 := by
    exact sub_nonpos.mpr (Int.floor_le x)
  ring_nf
  exact add_le_of_nonpos_right this

lemma ZetaSum_aux1a_aux3 (x : ‚Ñù) : |(‚åäx‚åã + 1/2 - x)| ‚â§ 1/2 := by
  apply abs_le.mpr
  constructor
  ¬∑ exact le_of_lt (ZetaSum_aux1a_aux3a x)
  exact ZetaSum_aux1a_aux3b x

lemma ZetaSum_aux1a_aux4a (x : ‚Ñù) (c : ‚ÑÇ) (s : ‚ÑÇ) (hx : 0 < x) : (Complex.abs (c / ((x : ‚ÑÇ) ^ (s+1)))) = (Complex.abs c) / x^((s + 1).re) := by
  simp only [map_div‚ÇÄ, abs_ofReal]
  congr
  exact Complex.abs_cpow_eq_rpow_re_of_pos hx (s+1)

lemma ZetaSum_aux1a_aux4b (c : ‚Ñù) : (Complex.abs c) = |c| := by
  exact abs_ofReal c

lemma ZetaSum_aux1a_aux4b' (x : ‚Ñù) : (Complex.abs (‚åäx‚åã + 1 / 2 - x)) = |‚åäx‚åã + 1 / 2 - x| := by
  have := ZetaSum_aux1a_aux4b (‚åäx‚åã + 1 / 2 - x)
  rw [‚Üê this]
  simp only [one_div, ofReal_sub, ofReal_add, ofReal_int_cast, ofReal_inv, ofReal_ofNat]

lemma ZetaSum_aux1a_aux4c (x : ‚Ñù) (hx : 0 < x) (s : ‚ÑÇ) : Complex.abs ((‚åäx‚åã + 1 / 2 - (x : ‚Ñù)) / (x : ‚ÑÇ)^(s + 1)) = |‚åäx‚åã + 1 / 2 - x| / x^((s + 1).re) := by
  calc
    _ = (Complex.abs (‚åäx‚åã + 1 / 2 - x)) / x^((s + 1).re) := by
      exact ZetaSum_aux1a_aux4a x (‚åäx‚åã + 1 / 2 - x) s hx
    _ = |‚åäx‚åã + 1 / 2 - x| / x^((s + 1).re) := by
      congr
      exact ZetaSum_aux1a_aux4b' x

theorem ZetaSum_aux1a_aux4 {a b : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) {s : ‚ÑÇ} :
  ‚à´ (x : ‚Ñù) in a..b, Complex.abs ((‚Üë‚åäx‚åã + 1 / 2 - ‚Üëx) / ‚Üëx ^ (s + 1)) =
    ‚à´ (x : ‚Ñù) in a..b, |‚åäx‚åã + 1 / 2 - x| / x ^ (s + 1).re := by
  apply intervalIntegral.integral_congr
  intro x hx
  simp only
  exact ZetaSum_aux1a_aux4c x (ZetaSum_aux1a_aux1 apos a_lt_b hx) s

theorem ZetaSum_aux1a_aux5a {a b : ‚Ñù} (apos : 0 < a) {s : ‚ÑÇ} (x : ‚Ñù)
  (h : x ‚àà Set.Icc a b) : |‚Üë‚åäx‚åã + 1 / 2 - x| / x ^ (s.re + 1) ‚â§ 1 / x ^ (s.re + 1) := by
  apply div_le_div_of_nonneg_right _ _
  ¬∑ calc
    _ ‚â§ 1/2 := ZetaSum_aux1a_aux3 x
    _ ‚â§ 1 := by norm_num
  ¬∑ apply Real.rpow_nonneg
    exact le_of_lt (ZetaSum_aux1a_aux1' apos h)

theorem ZetaSum_aux1a_aux5b {a b : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) {s : ‚ÑÇ} (œÉpos : 0 < s.re) :
  IntervalIntegrable (fun u ‚Ü¶ 1 / u ^ (s.re + 1)) MeasureTheory.volume a b := by
  apply ContinuousOn.intervalIntegrable_of_Icc (le_of_lt a_lt_b) _
  apply ContinuousOn.div
  ¬∑ apply continuousOn_const
  ¬∑ apply ContinuousOn.rpow_const
    ¬∑ apply continuousOn_id
    ¬∑ intro x hx
      have : x > 0 := by
        exact ZetaSum_aux1a_aux1' apos hx
      exact Or.inl (ne_of_gt this)
  ¬∑ intro x hx
    by_contra h
    have h1 : x > 0 := by
      exact (ZetaSum_aux1a_aux1' apos hx)
    have : s.re + 1 ‚â† 0 := by
      exact ne_of_gt (add_pos œÉpos zero_lt_one)
    have := (Real.rpow_eq_zero (le_of_lt h1) this).mp h
    exact (ne_of_gt h1) this

theorem ZetaSum_aux1a_aux5c {a b : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) {s : ‚ÑÇ} (œÉpos : 0 < s.re) :
  let g : ‚Ñù ‚Üí ‚Ñù := fun u ‚Ü¶ |‚Üë‚åäu‚åã + 1 / 2 - u| / u ^ (s.re + 1);
  MeasureTheory.AEStronglyMeasurable g (MeasureTheory.Measure.restrict MeasureTheory.volume (Œô a b)) := by
  intro g
  let g1 : ‚Ñù ‚Üí ‚Ñù := fun u ‚Ü¶ |‚Üë‚åäu‚åã + 1 / 2 - u|;
  let g2 : ‚Ñù ‚Üí ‚Ñù := fun u ‚Ü¶ u ^ (s.re + 1);
  have : g = g1 / g2 := by
    ext x
    simp only [Pi.div_apply]
  rw [this]
  sorry

theorem ZetaSum_aux1a_aux5d {a b : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) {s : ‚ÑÇ} (œÉpos : 0 < s.re) :
  IntervalIntegrable (fun u ‚Ü¶ |‚Üë‚åäu‚åã + 1 / 2 - u| / u ^ (s.re + 1)) MeasureTheory.volume a b := by
  set g : ‚Ñù ‚Üí ‚Ñù := (fun u ‚Ü¶ |‚Üë‚åäu‚åã + 1 / 2 - u| / u ^ (s.re + 1))
  suffices IntervalIntegrable g MeasureTheory.volume a b
    by exact this
  apply IntervalIntegrable.mono_fun (ZetaSum_aux1a_aux5b apos a_lt_b œÉpos)
  ¬∑ exact ZetaSum_aux1a_aux5c apos a_lt_b œÉpos
  simp
  show (fun x ‚Ü¶ |g x|) ‚â§·∂†[MeasureTheory.Measure.ae (MeasureTheory.Measure.restrict MeasureTheory.volume (Œô a b))] fun x ‚Ü¶
  |x ^ (s.re + 1)|‚Åª¬π
  filter_upwards
  unfold_let
  intro x
  simp only
  rw [abs_div, div_eq_mul_inv]
  nth_rw 2 [‚Üê one_mul |x ^ (s.re + 1)|‚Åª¬π]
  apply mul_le_mul
  ¬∑ rw [_root_.abs_abs]
    calc
      _ ‚â§ 1/2 := ZetaSum_aux1a_aux3 x
      _ ‚â§ 1 := by norm_num
  ¬∑ simp only [le_refl]
  ¬∑ simp only [inv_nonneg, abs_nonneg]
  ¬∑ norm_num

theorem ZetaSum_aux1a_aux5 {a b : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) {s : ‚ÑÇ} (œÉpos : 0 < s.re) :
  ‚à´ (x : ‚Ñù) in a..b, |‚åäx‚åã + 1 / 2 - x| / x ^ (s.re + 1) ‚â§ ‚à´ (x : ‚Ñù) in a..b, 1 / x ^ (s.re + 1) := by
  apply intervalIntegral.integral_mono_on _ _ _
  ¬∑ exact ZetaSum_aux1a_aux5a apos
  ¬∑ exact le_of_lt a_lt_b
  ¬∑ exact ZetaSum_aux1a_aux5d apos a_lt_b œÉpos
  ¬∑ exact ZetaSum_aux1a_aux5b apos a_lt_b œÉpos

/-%%
\begin{lemma}[ZetaSum_aux1a]\label{ZetaSum_aux1a}\lean{ZetaSum_aux1a}\leanok
For any $0 < a < b$ and  $s \in \C$ with $\sigma=\Re(s)>0$,
$$
\left|\int_a^b \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx\right|
\le \frac{a^{-\sigma}-b^{-\sigma}}{\sigma}.
$$
\end{lemma}
%%-/
lemma ZetaSum_aux1a {a b : ‚Ñù} (apos : 0 < a) (a_lt_b : a < b) {s : ‚ÑÇ} (œÉpos : 0 < s.re) :
    Complex.abs (‚à´ x in a..b, (‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^(s + 1)) ‚â§
      (a ^ (-s.re) - b ^ (-s.re)) / s.re := by
  calc
    _ ‚â§ ‚à´ x in a..b, Complex.abs ((‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^(s + 1)) :=
        intervalIntegral.norm_integral_le_integral_norm (Œº := MeasureTheory.volume)
          (a := a) (b := b) (f := Œª x => (‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^(s + 1)) (le_of_lt a_lt_b)
    _ = ‚à´ x in a..b, |(‚åäx‚åã + 1 / 2 - x)| / x^((s+1).re) := by
      exact ZetaSum_aux1a_aux4 apos a_lt_b
    _ = ‚à´ x in a..b, |(‚åäx‚åã + 1 / 2 - x)| / x^(s.re + 1) := by rfl
    _ ‚â§ ‚à´ x in a..b, 1 / x^(s.re + 1) := by
      exact ZetaSum_aux1a_aux5 apos a_lt_b œÉpos
    _ = (a ^ (-s.re) - b ^ (-s.re)) / s.re := by
      have h1 : s.re ‚â† 0 := by
        exact ne_of_gt œÉpos
      have h2 : 0 ‚àâ [[a,b]] := by
        by_contra h
        rw [Set.mem_uIcc] at h
        rcases h with ‚ü®h, _‚ü© | ‚ü®h, _‚ü©
        ¬∑ exact not_le_of_lt apos h
        have : a < a := by
          calc
            a < b := a_lt_b
            _ ‚â§ 0 := h
            _ < a := apos
        exact lt_irrefl a this
      apply ZetaSum_aux1a_aux2 (c := s.re) apos a_lt_b ‚ü® h1, h2 ‚ü©

/-%%
\begin{proof}
Apply the triangle inequality
$$
\left|\int_a^b \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx\right|
\le \int_a^b \frac{1}{x^{\sigma+1}} \, dx,
$$
and evaluate the integral.
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaSum_aux2]\label{ZetaSum_aux2}\lean{ZetaSum_aux2}\leanok
  Let $N$ be a natural number and $s\in \C$, $\Re(s)>1$.
  Then
  \[
  \sum_{N < n} \frac{1}{n^s} =  \frac{- N^{1-s}}{1-s} + \frac{-N^{-s}}{2} + s \int_N^\infty \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx.
  \]
\end{lemma}
%%-/
lemma ZetaSum_aux2 {N : ‚Ñï} {s : ‚ÑÇ} (s_re_pos : 1 < s.re) :
    ‚àë' (n : ‚Ñï), 1 / (n + N : ‚ÑÇ) ^ s =
    (- N ^ (1 - s)) / (1 - s) + (- N ^ (-s)) / 2
      + s * ‚à´ x in Set.Ici (N : ‚Ñù), (‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^(s + 1) := by
  sorry
/-%%
\begin{proof}\uses{ZetaSum_aux1, ZetaSum_aux1a}
  Apply Lemma \ref{ZetaSum_aux1} with $a=N$ and $b\to \infty$.
\end{proof}
%%-/

/-%%
\begin{definition}[RiemannZeta0]\label{RiemannZeta0}\lean{RiemannZeta0}\leanok
\uses{ZetaSum_aux2}
For any natural $N\ge1$, we define
$$
\zeta_0(N,s) :=
\sum_{1\le n < N} \frac1{n^s}
+
\frac{- N^{1-s}}{1-s} + \frac{-N^{-s}}{2} + s \int_N^\infty \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx
$$
\end{definition}
%%-/
noncomputable def RiemannZeta0 (N : ‚Ñï) (s : ‚ÑÇ) : ‚ÑÇ :=
  (‚àë n in Finset.Icc 1 (N - 1), 1 / (n : ‚ÑÇ) ^ s) +
  (- N ^ (1 - s)) / (1 - s) + (- N ^ (-s)) / 2
      + s * ‚à´ x in Set.Ici (N : ‚Ñù), (‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^(s + 1)

/-%%
\begin{lemma}[ZetaBnd_aux1]\label{ZetaBnd_aux1}\lean{ZetaBnd_aux1}\leanok
For any $N\ge1$ and $s\in \C$, $\sigma=\Re(s)\in[1/2,2]$,
$$
\left| s\int_N^\infty \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx \right|
\ll |t| \frac{N^{-\sigma}}{\sigma},
$$
as $|t|\to\infty$.
\end{lemma}
%%-/
lemma ZetaBnd_aux1 {N : ‚Ñï} (Npos : 1 ‚â§ N) {œÉ : ‚Ñù} (œÉ_ge : 1 / 2 ‚â§ œÉ) (œÉ_le : œÉ ‚â§ 2) :
    (fun (t : ‚Ñù) ‚Ü¶ Complex.abs ((œÉ + t * I) *
      ‚à´ x in Set.Ici (N : ‚Ñù), (‚åäx‚åã + 1 / 2 - x) / (x : ‚ÑÇ)^((œÉ + t * I) + 1)))
      =O[cocompact ‚Ñù] fun (t : ‚Ñù) ‚Ü¶ |t| * N ^ (-œÉ) / œÉ := by
  have := @ZetaSum_aux1a (a := N)
  sorry
/-%%
\begin{proof}\uses{ZetaSum_aux1a}
Apply Lemma \ref{ZetaSum_aux1a} with $a=N$ and $b\to \infty$, and estimate $|s|\ll |t|$.
\end{proof}
%%-/

/-%%
\begin{lemma}[Zeta0EqZeta]\label{Zeta0EqZeta}\lean{Zeta0EqZeta}\leanok
For $\Re(s)>0$, $s\ne1$, and for any $N$,
$$
\zeta_0(N,s) = \zeta(s).
$$
\end{lemma}
%%-/
lemma Zeta0EqZeta (N : ‚Ñï) (s : ‚ÑÇ) (reS_pos : 0 < s.re) (s_ne_one : s ‚â† 1) :
    RiemannZeta0 N s = riemannZeta s := by
  sorry
/-%%
\begin{proof}
\uses{ZetaSum_aux2, RiemannZeta0, ZetaBnd_aux1, ZetaBndAux}
Use Lemma \ref{ZetaSum_aux2} and the Definition \ref{RiemannZeta0}.
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaBnd_aux2]\label{ZetaBnd_aux2}\lean{ZetaBnd_aux2}\leanok
Given $n ‚â§ t$ and $\sigma$ with $1-A/\log t \le \sigma$, we have
that
$$
|n^{-s}| \le n^{-1} e^A.
$$
\end{lemma}
%%-/
lemma ZetaBnd_aux2 {n : ‚Ñï} {t A œÉ : ‚Ñù} (Apos : 0 < A) (œÉpos : 0 < œÉ) (n_le_t : n ‚â§ t)
    (œÉ_ge : (1 : ‚Ñù) - A / Real.log |t| ‚â§ œÉ) :
    Complex.abs (n ^ (-(œÉ + t * I))) ‚â§ (n : ‚Ñù)‚Åª¬π * Real.exp A := by
  by_cases n0 : n = 0
  ¬∑ simp [n0]
    sorry
  sorry
/-%%
\begin{proof}
Use $|n^{-s}| = n^{-\sigma}
= e^{-\sigma \log n}
\le
\exp(-\left(1-\frac{A}{\log t}\right)\log n)
\le
n^{-1} e^A$,
since $n\le t$.
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaUpperBnd]\label{ZetaUpperBnd}\lean{ZetaUpperBnd}\leanok
For any $s\in \C$, $1/2 \le \Re(s)=\sigma\le 2$,
and any $A>0$ sufficiently small, and $1-A/\log t \le \sigma$, we have
$$
|\zeta(s)| \ll \log t,
$$
as $|t|\to\infty$.
\end{lemma}
%%-/
lemma ZetaUpperBnd :
    ‚àÉ (A : ‚Ñù) (Apos : 0 < A) (C : ‚Ñù) (Cpos : 0 < C), ‚àÄ (œÉ : ‚Ñù) (t : ‚Ñù) (t_ge : 3 < |t|)
    (œÉ_ge : 1 - A / Real.log |t| ‚â§ œÉ) (œÉ_le : œÉ ‚â§ 2),
    Complex.abs (riemannZeta (œÉ + t * I)) ‚â§ C * Real.log |t| := by
  refine ‚ü®1/2, by norm_num, 10, by norm_num, ?_‚ü© -- placeholder values for `A` and `C`
  intro œÉ t t_ge œÉ_ge œÉ_le
  set N := ‚åä Real.log |t| ‚åã‚Çä
  have œÉPos :  0 < (‚ÜëœÉ + ‚Üët * I).re := by
    simp only [add_re, ofReal_re, mul_re, I_re, mul_zero, ofReal_im, I_im, mul_one, sub_self,
      add_zero]
    have : 1 < Real.log |t| := by
      sorry
    -- nlinarith
    sorry
  have neOne : ‚ÜëœÉ + ‚Üët * I ‚â† 1 := by
    sorry
  rw [‚Üê Zeta0EqZeta N (œÉ + t * I) œÉPos neOne]
  sorry
/-%%
\begin{proof}\uses{ZetaBnd_aux1, ZetaBnd_aux2}
First replace $\zeta(s)$ by $\zeta_0(N,s)$ for $N = \lfloor |t| \rfloor$.
We estimate:
$$
|\zeta_0(N,s)| \ll
\sum_{1\le n < |t|} |n^{-s}|
+
\frac{- |t|^{1-\sigma}}{|1-s|} + \frac{-|t|^{-\sigma}}{2} +
|t| * |t| ^ (-œÉ) / œÉ
$$
$$
\ll
e^A \sum_{1\le n < |t|} n^{-1}
+|t|^{1-\sigma}
$$
,
where we used Lemma \ref{ZetaBnd_aux2} and Lemma \ref{ZetaBnd_aux1}.
The first term is $\ll \log |t|$.
For the second term, estimate
$$
|t|^{1-\sigma}
\le |t|^{1-(1-A/\log |t|)}
= |t|^{A/\log |t|} \ll 1.
$$
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaDerivUpperBnd]\label{ZetaDerivUpperBnd}\lean{ZetaDerivUpperBnd}\leanok
For any $s\in \C$, $1/2 \le \Re(s)=\sigma\le 2$,
there is an $A>0$ so that for $1-A/\log t \le \sigma$, we have
$$
|\zeta'(s)| \ll \log^2 t,
$$
as $|t|\to\infty$.
\end{lemma}
%%-/
lemma ZetaDerivUpperBnd :
    ‚àÉ (A : ‚Ñù) (Apos : 0 < A) (C : ‚Ñù) (Cpos : 0 < C), ‚àÄ (œÉ : ‚Ñù) (t : ‚Ñù) (t_gt : 3 < |t|)
    (œÉ_ge : 1 - A / Real.log |t| ‚â§ œÉ) (œÉ_le : œÉ ‚â§ 2),
    Complex.abs (deriv riemannZeta (œÉ + t * I)) ‚â§ C * (Real.log |t|) ^ 2 := by
  sorry
/-%%
\begin{proof}\uses{ZetaBnd_aux1, ZetaBnd_aux2}
First replace $\zeta(s)$ by $\zeta_0(N,s)$ for $N = \lfloor |t| \rfloor$.
Differentiating term by term, we get:
$$
\zeta'(s) = -\sum_{1\le n < N} n^{-s} \log n
-
\frac{N^{1 - s}}{1 - s)^2} + \frac{N^{1 - s} \log N} {1 - s}
+ \frac{-N^{-s}\log N}{2} +
\int_N^\infty \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+1}} \, dx
-
s(s+1) \int_N^\infty \frac{\lfloor x\rfloor + 1/2 - x}{x^{s+2}} \, dx
.
$$
Estimate as before, with an extra factor of $\log |t|$.
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaNear1Bnd]\label{ZetaNear1Bnd}\lean{ZetaNear1Bnd}\leanok
As $\sigma\to1^+$,
$$
|\zeta(\sigma)| \ll 1/(\sigma-1).
$$
\end{lemma}
%%-/
lemma ZetaNear1Bnd':
    (fun œÉ : ‚Ñù ‚Ü¶ riemannZeta œÉ) =O[ùìù[>](1 : ‚Ñù)] (fun œÉ ‚Ü¶ (1 : ‚ÑÇ) / (œÉ - 1)) := by
  have : Tendsto (fun (x : ‚Ñù) ‚Ü¶ x - 1) (ùìù[>](1 : ‚Ñù)) (ùìù[>](0 : ‚Ñù)) := by
    refine tendsto_iff_forall_eventually_mem.mpr ?_
    intro s hs
    sorry
  have := riemannZeta_isBigO_near_one_horizontal.comp_tendsto this
  convert this using 1 <;> {ext1 _; simp}
/-%%
\begin{proof}\uses{ZetaBnd_aux1, Zeta0EqZeta}
Zeta has a simple pole at $s=1$. Equivalently, $\zeta(s)(s-1)$ remains bounded near $1$.
Lots of ways to prove this.
Probably the easiest one: use the expression for $\zeta_0 (N,s)$ with $N=1$ (the term $N^{1-s}/(1-s)$ being the only unbounded one).
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaNear1Bnd]\label{ZetaNear1Bnd}\lean{ZetaNear1Bnd}\leanok
There exists a $c>0$ such that for all $1 \sigma ‚â§ 2$,
$$
|\zeta(\sigma)| ‚â§ c/(\sigma-1).
$$
\end{lemma}
%%-/
lemma ZetaNear1Bnd:
    ‚àÉ (c : ‚Ñù) (cpos : 0 < c), ‚àÄ (œÉ : ‚Ñù) (œÉ_ge : 1 < œÉ) (œÉ_le : œÉ ‚â§ 2),
    ‚ÄñriemannZeta œÉ‚Äñ ‚â§ c / (œÉ - 1) := by
  use 10, (by norm_num)
  intro œÉ œÉ_ge œÉ_le
  sorry
/-%%
\begin{proof}\uses{ZetaBnd_aux1, Zeta0EqZeta}
Zeta has a simple pole at $s=1$. Equivalently, $\zeta(s)(s-1)$ remains bounded near $1$.
Lots of ways to prove this.
Probably the easiest one: use the expression for $\zeta_0 (N,s)$ with $N=1$ (the term $N^{1-s}/(1-s)$ being the only unbounded one).
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaInvBound1]\label{ZetaInvBound1}\lean{ZetaInvBound1}\leanok
For all $\sigma>1$,
$$
1/|\zeta(\sigma+it)| \le |\zeta(\sigma)|^{3/4}|\zeta(\sigma+2it)|^{1/4}
$$
\end{lemma}
%%-/
lemma ZetaInvBound1 {œÉ t : ‚Ñù} (œÉ_gt : 1 < œÉ) :
    1 / Complex.abs (riemannZeta (œÉ + t * I)) ‚â§
      Complex.abs (riemannZeta œÉ) ^ ((3 : ‚Ñù) / 4) *
        Complex.abs (riemannZeta (œÉ + 2 * t * I)) ^ ((1 : ‚Ñù) / 4) := by
  sorry -- use `norm_zeta_product_ge_one`
/-%%
\begin{proof}
The identity
$$
1 \le |\zeta(\sigma)|^3 |\zeta(\sigma+it)|^4 |\zeta(\sigma+2it)|
$$
for $\sigma>1$
is already proved by Michael Stoll in the EulerProducts PNT file.
\end{proof}
%%-/

lemma Ioi_union_Iio_mem_cocompact {a : ‚Ñù} (ha : 0 ‚â§ a) : Set.Ioi (a : ‚Ñù) ‚à™ Set.Iio (-a : ‚Ñù) ‚àà cocompact ‚Ñù := by
  simp only [Filter.mem_cocompact]
  use Set.Icc (-a) a
  constructor
  ¬∑ exact isCompact_Icc
  ¬∑ rw [@Set.compl_subset_iff_union, ‚Üê Set.union_assoc, Set.Icc_union_Ioi_eq_Ici, Set.union_comm, Set.Iio_union_Ici]
    linarith

lemma lt_abs_mem_cocompact {a : ‚Ñù} (ha : 0 ‚â§ a) : {t | a < |t|} ‚àà cocompact ‚Ñù := by
  convert Ioi_union_Iio_mem_cocompact ha using 1; ext t
  simp only [Set.mem_setOf_eq, Set.mem_union, Set.mem_Ioi, Set.mem_Iio, lt_abs, lt_neg]

/-%%
\begin{lemma}[ZetaInvBound2]\label{ZetaInvBound2}\lean{ZetaInvBound2}\leanok
For $\sigma>1$ (and $\sigma \le 2$),
$$
1/|\zeta(\sigma+it)| \ll (\sigma-1)^{-3/4}(\log |t|)^{1/4},
$$
as $|t|\to\infty$.
\end{lemma}
%%-/
lemma ZetaInvBound2 {œÉ : ‚Ñù} (œÉ_gt : 1 < œÉ) (œÉ_le : œÉ ‚â§ 2) :
    (fun (t : ‚Ñù) ‚Ü¶ 1 / Complex.abs (riemannZeta (œÉ + t * I))) =O[cocompact ‚Ñù]
      fun (t : ‚Ñù) ‚Ü¶ (œÉ - 1) ^ (-(3 : ‚Ñù) / 4) * (Real.log |t|) ^ ((1 : ‚Ñù) / 4) := by
  obtain ‚ü®A, ha, C, hC, h‚ü© := ZetaUpperBnd
  rw [Asymptotics.isBigO_iff]
  have bnd := ZetaNear1Bnd
  obtain ‚ü®c, hc, h_inv‚ü© := bnd
  use (2 * C) ^ ((1 : ‚Ñù)/ 4) * c ^ ((3 : ‚Ñù)/ 4)
  filter_upwards [lt_abs_mem_cocompact (by norm_num : 0 ‚â§ (2 : ‚Ñù))] with t ht
  replace ht : 4 < |2 * t| := by rw [abs_mul]; norm_num; linarith
  have ht' : 3 < |2 * t| := by linarith
  rw [abs_mul, Nat.abs_ofNat, mul_comm, ‚Üê div_lt_iff (by norm_num)] at ht; norm_num at ht

  calc
    _ ‚â§ ‚ÄñComplex.abs (riemannZeta ‚ÜëœÉ) ^ (3 / 4 : ‚Ñù) * Complex.abs (riemannZeta (‚ÜëœÉ + 2 * ‚Üët * I)) ^ (1 / 4 : ‚Ñù)‚Äñ := ?_
    _ ‚â§ ‚Äñ((œÉ - 1) / c) ^ (-3 / 4 : ‚Ñù) * Complex.abs (riemannZeta (‚ÜëœÉ + 2 * ‚Üët * I)) ^ (1 / 4 : ‚Ñù)‚Äñ := ?_
    _ ‚â§ ‚Äñ((œÉ - 1) / c) ^ (-3 / 4 : ‚Ñù) * C ^ (1 / 4 : ‚Ñù) * (Real.log |2 * t|) ^ (1 / 4 : ‚Ñù)‚Äñ := ?_
    _ ‚â§ ‚Äñ((œÉ - 1) / c) ^ (-3 / 4 : ‚Ñù) * C ^ (1 / 4 : ‚Ñù) * (Real.log (|t| ^ 2)) ^ (1 / 4 : ‚Ñù)‚Äñ := ?_
    _ = ‚Äñ((œÉ - 1)) ^ (-3 / 4 : ‚Ñù) * c ^ (3 / 4 : ‚Ñù) * (C ^ (1 / 4 : ‚Ñù) * (Real.log (|t| ^ 2)) ^ (1 / 4 : ‚Ñù))‚Äñ := ?_
    _ = ‚Äñ((œÉ - 1)) ^ (-3 / 4 : ‚Ñù) * c ^ (3 / 4 : ‚Ñù) * ((2 * C) ^ (1 / 4 : ‚Ñù) * Real.log |t| ^ (1 / 4 : ‚Ñù))‚Äñ := ?_
    _ = _ := ?_
  ¬∑ simp only [norm_div, norm_one, norm_eq_abs, Real.norm_eq_abs, Complex.abs_abs, norm_mul]
    convert ZetaInvBound1 œÉ_gt using 2
    <;> exact abs_eq_self.mpr <| Real.rpow_nonneg (apply_nonneg _ _) _
  ¬∑ have bnd1: Complex.abs (riemannZeta œÉ) ^ (3 / 4 : ‚Ñù) ‚â§ ((œÉ - 1) / c) ^ (-(3 : ‚Ñù) / 4) := by
      have : ((œÉ - 1) / c) ^ (-(3 : ‚Ñù) / 4) = (((œÉ - 1) / c) ^ (-1 : ‚Ñù)) ^ (3 / 4 : ‚Ñù) := by
        rw [‚Üê Real.rpow_mul]
        ring_nf
        apply div_nonneg (by linarith) hc.le
      rw [this]
      apply Real.rpow_le_rpow (by simp [apply_nonneg]) ?_ (by norm_num)
      simp only [Real.rpow_neg_one, inv_div]
      exact h_inv œÉ œÉ_gt œÉ_le
    simp only [norm_div, norm_one, norm_eq_abs, Real.norm_eq_abs, Complex.abs_abs, norm_mul]
    apply (mul_le_mul_right ?_).mpr
    convert bnd1 using 1
    ¬∑ exact abs_eq_self.mpr <| Real.rpow_nonneg (apply_nonneg _ _) _
    ¬∑ exact abs_eq_self.mpr <| Real.rpow_nonneg (div_nonneg (by linarith) hc.le) _
    ¬∑ apply lt_iff_le_and_ne.mpr ‚ü®(by simp), ?_‚ü©
      have : riemannZeta (‚ÜëœÉ + 2 * ‚Üët * I) ‚â† 0 := by
        apply riemannZeta_ne_zero_of_one_le_re ?_ (by simp [œÉ_gt.le])
        contrapose! œÉ_gt
        simp only [ext_iff, add_re, ofReal_re, mul_re, re_ofNat, im_ofNat, ofReal_im, mul_zero,
          sub_zero, I_re, mul_im, zero_mul, add_zero, I_im, mul_one, sub_self, one_re, add_im,
          zero_add, one_im, mul_eq_zero, OfNat.ofNat_ne_zero, false_or] at œÉ_gt
        linarith
      symm; intro h
      rw [Real.abs_rpow_of_nonneg (by norm_num), Real.rpow_eq_zero (by norm_num) (by norm_num)] at h
      simp only [Complex.abs_abs, map_eq_zero] at h
      exact this h
  ¬∑ have le_one : 1 - A / Real.log |t| ‚â§ 1 := by
      simp only [Real.log_abs, tsub_le_iff_right, le_add_iff_nonneg_right]
      rw [‚Üê Real.log_abs]
      apply div_nonneg ha.le (Real.log_nonneg ?_)
      linarith
    replace h := h œÉ (2 * t) (by linarith) ?_ œÉ_le
    ¬∑ have : 0 ‚â§ Real.log |2 * t| := Real.log_nonneg (by linarith)
      conv => rhs; rw [mul_assoc, ‚Üê Real.mul_rpow hC.le this]
      rw [norm_mul, norm_mul]
      conv => rhs; rhs; rw [Real.norm_rpow_of_nonneg <| mul_nonneg hC.le this]
      conv => lhs; rhs; rw [‚Üê norm_eq_abs, Real.norm_rpow_of_nonneg <| norm_nonneg _]
      apply (mul_le_mul_left ?_).mpr
      apply Real.rpow_le_rpow
      ¬∑ apply norm_nonneg
      ¬∑ convert h using 1
        ¬∑ simp
        ¬∑ simp [abs_eq_self.mpr hC.le, this, Real.log_abs, Or.inl]
          left
          convert this using 1
          apply (Real.log_abs _).symm
      ¬∑ norm_num
      ¬∑ simp only [Real.norm_eq_abs, abs_pos, ne_eq]
        replace : (œÉ - 1) / c ‚â† 0 := ne_of_gt <| div_pos (by linarith) hc
        contrapose! this
        rwa [Real.rpow_eq_zero (div_nonneg (by linarith) hc.le) (by norm_num)] at this
    ¬∑ have : 1 - A / Real.log |2 * t| ‚â§ 1 := by
        simp only [Real.log_abs, tsub_le_iff_right, le_add_iff_nonneg_right]
        rw [‚Üê Real.log_abs]
        apply div_nonneg ha.le (Real.log_nonneg ?_)
        linarith
      linarith
  ¬∑ simp only [Real.log_abs, norm_mul]
    apply (mul_le_mul_left ?_).mpr
    ¬∑ rw [‚Üê Real.log_abs, Real.norm_rpow_of_nonneg <| Real.log_nonneg (by linarith)]
      have : Real.log |2 * t| < Real.log |t ^ 2| := by sorry
      have : 1 ‚â§ |(|t| ^ 2)| := by
        sorry
      conv => rhs; rw [‚Üê Real.log_abs, Real.norm_rpow_of_nonneg <| Real.log_nonneg this]
      apply Real.rpow_le_rpow
      ¬∑ apply abs_nonneg
      ¬∑ nth_rewrite 2 [Real.log_abs]
        sorry
      ¬∑ norm_num
    . apply mul_pos
      ¬∑ apply abs_pos.mpr (by sorry)
      ¬∑ apply abs_pos.mpr (by sorry)
  ¬∑ have : (-3 : ‚Ñù) / 4 = -((3 : ‚Ñù)/ 4) := by norm_num
    simp only [norm_mul, mul_eq_mul_right_iff, abs_eq_zero, this, ‚Üê mul_assoc]
    left; left
    conv => lhs; rw [Real.div_rpow (by linarith) hc.le, Real.rpow_neg hc.le, div_inv_eq_mul, norm_mul]
  ¬∑ simp only [Real.log_pow, Nat.cast_ofNat, norm_mul, Real.norm_eq_abs]
    congr! 1
    rw [Real.mul_rpow, Real.mul_rpow]
    ¬∑ rw [abs_mul, abs_mul, ‚Üê mul_assoc, mul_comm _ |2 ^ (1 / 4)|]
    ¬∑ norm_num
    ¬∑ exact hC.le
    ¬∑ norm_num
    ¬∑ exact Real.log_nonneg (by linarith)
  ¬∑ simp only [norm_mul, Real.norm_eq_abs]
    have : (2 * C) ^ ((1 : ‚Ñù)/ 4) * c ^ ((3 : ‚Ñù)/ 4) =|(2 * C) ^ ((1 : ‚Ñù)/ 4) * c ^ ((3 : ‚Ñù)/ 4)| := by
      rw [abs_eq_self.mpr ?_]
      apply mul_nonneg
      ¬∑ apply Real.rpow_nonneg
        linarith
      ¬∑ apply Real.rpow_nonneg
        linarith
    rw [this, abs_mul]
    conv => rhs; rw [‚Üê mul_assoc]; lhs; rw [mul_comm]; rhs; rw [mul_comm]
    rw [mul_assoc, mul_assoc, mul_assoc]
/-%%
\begin{proof}\uses{ZetaInvBound1, ZetaNear1Bnd, ZetaUpperBnd}
Combine Lemma \ref{ZetaInvBound1} with the bounds in Lemmata \ref{ZetaNear1Bnd} and
\ref{ZetaUpperBnd}.
\end{proof}
%%-/

/-%%
\begin{lemma}[Zeta_eq_int_derivZeta]\label{Zeta_eq_int_derivZeta}\lean{Zeta_eq_int_derivZeta}
\leanok
For any $t\ne0$ (so we don't pass through the pole), and $\sigma_1 < \sigma_2$,
$$
\int_{\sigma_1}^{\sigma_2}\zeta'(\sigma + it) dt =
\zeta(\sigma_2+it) - \zeta(\sigma_1+it).
$$
\end{lemma}
%%-/
lemma Zeta_eq_int_derivZeta {œÉ‚ÇÅ œÉ‚ÇÇ t : ‚Ñù} (œÉ‚ÇÅ_lt_œÉ‚ÇÇ : œÉ‚ÇÅ < œÉ‚ÇÇ) (t_ne_zero : t ‚â† 0) :
    (‚à´ œÉ in Set.Icc œÉ‚ÇÅ œÉ‚ÇÇ, deriv riemannZeta (œÉ + t * I)) =
      riemannZeta (œÉ‚ÇÇ + t * I) - riemannZeta (œÉ‚ÇÅ + t * I) := by
  sorry
/-%%
\begin{proof}
This is the fundamental theorem of calculus.
\end{proof}
%%-/

/-%%
\begin{lemma}[Zeta_diff_Bnd]\label{Zeta_diff_Bnd}\lean{Zeta_diff_Bnd}\leanok
For any $A>0$ sufficiently small, there is a constant $C>0$ so that
whenever $1- A / \log t \le \sigma_1 < \sigma_2\le 2$, we have that:
$$
|\zeta (\sigma_2 + it) - \zeta (\sigma_1 + it)|
\le C (\log |t|)^2 (\sigma_2 - \sigma_1).
$$
\end{lemma}
%%-/
lemma Zeta_diff_Bnd :
    ‚àÉ (A : ‚Ñù) (Apos : 0 < A) (C : ‚Ñù) (Cpos : 0 < C), ‚àÄ (œÉ‚ÇÅ œÉ‚ÇÇ : ‚Ñù) (t : ‚Ñù) (t_gt : 3 < |t|)
    (œÉ‚ÇÅ_ge : 1 - A / Real.log |t| ‚â§ œÉ‚ÇÅ) (œÉ‚ÇÇ_le : œÉ‚ÇÇ ‚â§ 2) (œÉ‚ÇÅ_lt_œÉ‚ÇÇ : œÉ‚ÇÅ < œÉ‚ÇÇ),
    Complex.abs (riemannZeta (œÉ‚ÇÇ + t * I) - riemannZeta (œÉ‚ÇÅ + t * I)) ‚â§
      C * (Real.log |t|) ^ 2 * (œÉ‚ÇÇ - œÉ‚ÇÅ) := by
  obtain ‚ü®A, Apos, C, Cpos, hC‚ü© := ZetaDerivUpperBnd
  refine ‚ü®A, Apos, C, Cpos, ?_‚ü©
  intro œÉ‚ÇÅ œÉ‚ÇÇ t t_gt œÉ‚ÇÅ_ge œÉ‚ÇÇ_le œÉ‚ÇÅ_lt_œÉ‚ÇÇ
  have t_ne_zero : t ‚â† 0 := by contrapose! t_gt; simp only [t_gt, abs_zero, Nat.ofNat_nonneg]
  rw [‚Üê Zeta_eq_int_derivZeta œÉ‚ÇÅ_lt_œÉ‚ÇÇ (t_ne_zero)]
  simp_rw [‚Üê Complex.norm_eq_abs] at hC ‚ä¢
  rw [MeasureTheory.integral_Icc_eq_integral_Ioc, ‚Üê intervalIntegral.integral_of_le œÉ‚ÇÅ_lt_œÉ‚ÇÇ.le]
  convert intervalIntegral.norm_integral_le_of_norm_le_const ?_ using 1
  ¬∑ congr; rw [_root_.abs_of_nonneg (by linarith)]
  ¬∑ intro œÉ hœÉ; rw [Set.uIoc_of_le œÉ‚ÇÅ_lt_œÉ‚ÇÇ.le, Set.mem_Ioc] at hœÉ
    exact hC œÉ t t_gt (le_trans œÉ‚ÇÅ_ge hœÉ.1.le) (le_trans hœÉ.2 œÉ‚ÇÇ_le)
/-%%
\begin{proof}
\uses{Zeta_eq_int_derivZeta, ZetaDerivUpperBnd}\leanok
Use Lemma \ref{Zeta_eq_int_derivZeta} and
estimate trivially using Lemma \ref{ZetaDerivUpperBnd}.
\end{proof}
%%-/

/-%%
\begin{lemma}[ZetaInvBnd]\label{ZetaInvBnd}\lean{ZetaInvBnd}\leanok
For any $A>0$ sufficiently small, there is a constant $C>0$ so that
whenever $1- A / \log^9 |t| \le \sigma < 1$, we have that:
$$
1/|\zeta(\sigma+it)| \le C \log^7 |t|.
$$
\end{lemma}
%%-/
lemma ZetaInvBnd :
    ‚àÉ (A : ‚Ñù) (Apos : 0 < A) (C : ‚Ñù) (Cpos : 0 < C), ‚àÄ (œÉ : ‚Ñù) (t : ‚Ñù) (t_gt : 3 < |t|)
    (œÉ_ge : 1 - A / (Real.log |t|) ^ 9 ‚â§ œÉ) (œÉ_lt : œÉ < 1),
    1 / Complex.abs (riemannZeta (œÉ + t * I)) ‚â§ C * (Real.log |t|) ^ 7 := by
  sorry
/-%%
\begin{proof}
\uses{Zeta_diff_Bnd, ZetaInvBound2}
Let $\sigma$ be given in the prescribed range, and set $\sigma' := 1+ A / \log^9 |t|$.
Then
$$
|\zeta(\sigma+it)| \ge
|\zeta(\sigma'+it)| - |\zeta(\sigma+it) - \zeta(\sigma'+it)|
\ge
C (\sigma'-1)^{-3/4}\log |t|^{-1/4} - C \log^2 |t| (\sigma'-\sigma)
$$
$$
\ge
C A^{-3/4} \log |t|^{-7} - C \log^2 |t| (2 A / \log^9 |t|),
$$
where we used Lemma \ref{ZetaInvBound2}  and Lemma \ref{Zeta_diff_Bnd}.
Now by making $A$ sufficiently small (in particular, something like $A = 1/16$ should work), we can guarantee that
$$
|\zeta(\sigma+it)| \ge \frac C 2 (\log |t|)^{-7},
$$
as desired.
\end{proof}
%%-/

/-%%
\begin{lemma}[LogDerivZetaBnd]\label{LogDerivZetaBnd}\lean{LogDerivZetaBnd}\leanok
There is an $A>0$ so that for $1-A/\log^9 |t| \le \sigma < 1$,
$$
|\frac {\zeta'}{\zeta} (\sigma+it)| \ll \log^9 |t|.
$$
\end{lemma}
%%-/
lemma LogDerivZetaBnd :
    ‚àÉ (A : ‚Ñù) (Apos : 0 < A) (C : ‚Ñù) (Cpos : 0 < C), ‚àÄ (œÉ : ‚Ñù) (t : ‚Ñù) (t_gt : 3 < |t|)
    (œÉ_ge : 1 - A / (Real.log |t|) ^ 9 ‚â§ œÉ) (œÉ_lt : œÉ < 1),
    Complex.abs (deriv riemannZeta (œÉ + t * I) / riemannZeta (œÉ + t * I)) ‚â§
      C * (Real.log |t|) ^ 9 := by
  obtain ‚ü®A, hA, C, hC, h‚ü© := ZetaInvBnd
  obtain ‚ü®A', hA', C', hC', h'‚ü© := ZetaDerivUpperBnd
  use min A A', lt_min hA hA', C * C', mul_pos hC hC'
  intro œÉ t t_gt œÉ_ge œÉ_lt
  have logt_gt : (1 : ‚Ñù) < Real.log |t| := by
    refine (Real.lt_log_iff_exp_lt (by linarith)).mpr (lt_trans ?_ t_gt)
    exact lt_trans Real.exp_one_lt_d9 (by norm_num)
  have œÉ_ge' : 1 - A / Real.log |t| ^ 9 ‚â§ œÉ := by
    apply le_trans (tsub_le_tsub_left ?_ 1) œÉ_ge
    apply div_le_div hA.le (min_le_left A A') ?_ (by rfl)
    exact pow_pos (lt_trans (by norm_num) logt_gt) 9
  have œÉ_ge'' : 1 - A' / Real.log |t| ‚â§ œÉ := by
    apply le_trans (tsub_le_tsub_left ?_ 1) œÉ_ge
    apply div_le_div hA'.le (min_le_right A A') (lt_trans (by norm_num) logt_gt) ?_
    exact le_self_pow logt_gt.le (by norm_num)
  replace h := h œÉ t t_gt œÉ_ge' œÉ_lt
  replace h' := h' œÉ t t_gt œÉ_ge'' (by linarith)
  simp only [map_div‚ÇÄ]
  convert mul_le_mul h h' (by simp [apply_nonneg]) ?_ using 1 <;> ring_nf
  exact le_trans (by simp only [one_div, inv_nonneg, apply_nonneg]) h
/-%%
\begin{proof}\leanok
\uses{ZetaInvBnd, ZetaDerivUpperBnd}
Combine the bound on $|\zeta'|$ from Lemma \ref{ZetaDerivUpperBnd} with the bound on $1/|\zeta|$ from Lemma \ref{ZetaInvBnd}.
\end{proof}
%%-/
