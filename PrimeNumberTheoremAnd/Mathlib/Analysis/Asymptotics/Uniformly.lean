/-
Copyright (c) 2024 Lawrence Wu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lawrence Wu
-/

import Mathlib.MeasureTheory.Integral.SetIntegral
import PrimeNumberTheoremAnd.Mathlib.Analysis.Asymptotics.Theta

/-!
# Uniform Asymptotics

For a family of functions `f : Î¹ Ã— Î± â†’ E` and `g : Î± â†’ E`, we can think of
`f =O[ğ“Ÿ s Ã—Ë¢ l] fun (i, x) â†¦ g x` as expressing that `f i` is O(g) uniformly on `s`.

This file provides methods for constructing `=O[ğ“Ÿ s Ã—Ë¢ l]` relations (similarly `Î˜`)
and deriving their consequences.
-/

open Filter

open Topology

namespace Asymptotics

variable {Î± Î¹ E F : Type*} {s : Set Î¹}

section Basic

variable [Norm E] [Norm F] {f : Î¹ Ã— Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}

/-- If f = O(g) uniformly on `s`, then f_i = O(g) for any i.` -/
theorem isBigO_of_isBigOUniformly (h : f =O[ğ“Ÿ s Ã—Ë¢ l] (g âˆ˜ Prod.snd)) {i : Î¹} (hi : i âˆˆ s) :
    (fun x â†¦ f (i, x)) =O[l] g := by
  obtain âŸ¨C, hCâŸ© := h.bound
  obtain âŸ¨t, htl, htâŸ© := hC.exists_mem
  obtain âŸ¨u, hu, v, hv, huvâŸ© := Filter.mem_prod_iff.mp htl
  refine isBigO_iff.mpr âŸ¨C, Filter.eventually_iff_exists_mem.mpr âŸ¨v, hv, ?_âŸ©âŸ©
  exact fun y hy â†¦ ht _ <| huv âŸ¨hu hi, hyâŸ©

/-- If f = Î©(g) uniformly on `s`, then f_i = Î©(g) for any i.` -/
theorem isBigO_rev_of_isBigOUniformly_rev (h : (g âˆ˜ Prod.snd) =O[ğ“Ÿ s Ã—Ë¢ l] f) {i : Î¹} (hi : i âˆˆ s) :
    g =O[l] fun x â†¦ f (i, x) := by
  obtain âŸ¨C, hCâŸ© := h.bound
  obtain âŸ¨t, htl, htâŸ© := hC.exists_mem
  obtain âŸ¨u, hu, v, hv, huvâŸ© := Filter.mem_prod_iff.mp htl
  refine isBigO_iff.mpr âŸ¨C, Filter.eventually_iff_exists_mem.mpr âŸ¨v, hv, ?_âŸ©âŸ©
  exact fun y hy â†¦ ht (i, y) <| huv âŸ¨hu hi, hyâŸ©

/-- If f = Î˜(g) uniformly on `s`, then f_i = Î˜(g) for any i.` -/
theorem isTheta_of_isThetaUniformly (h : f =Î˜[ğ“Ÿ s Ã—Ë¢ l] (g âˆ˜ Prod.snd)) {i : Î¹} (hi : i âˆˆ s) :
    (fun x â†¦ f (i, x)) =Î˜[l] g :=
  âŸ¨isBigO_of_isBigOUniformly h.1 hi, isBigO_rev_of_isBigOUniformly_rev h.2 hiâŸ©

end Basic

section Order

variable [NormedAddCommGroup Î±] [LinearOrder Î±] [ProperSpace Î±] [NormedAddCommGroup F]

theorem isLittleO_const_fst_atBot [NoMinOrder Î±] [ClosedIicTopology Î±] (c : F) (ly : Filter E) :
    (fun (_ : Î± Ã— E) â†¦ c) =o[atBot Ã—Ë¢ ly] Prod.fst := by
  refine ly.eq_or_neBot.casesOn (fun h â†¦ by simp [h]) (fun _ â†¦ ?_)
  show ((fun _ â†¦ c) âˆ˜ Prod.fst) =o[atBot Ã—Ë¢ ly] (id âˆ˜ Prod.fst)
  rewrite [â† isLittleO_map, map_fst_prod]
  exact isLittleO_const_id_atBot2 c

theorem isLittleO_const_snd_atBot [NoMinOrder Î±] [ClosedIicTopology Î±] (c : F) (lx : Filter E) :
    (fun (_ : E Ã— Î±) â†¦ c) =o[lx Ã—Ë¢ atBot] Prod.snd := by
  refine lx.eq_or_neBot.casesOn (fun h â†¦ by simp [h]) (fun _ â†¦ ?_)
  show ((fun _ â†¦ c) âˆ˜ Prod.snd) =o[lx Ã—Ë¢ atBot] (id âˆ˜ Prod.snd)
  rewrite [â† isLittleO_map, map_snd_prod]
  exact isLittleO_const_id_atBot2 c

theorem isLittleO_const_fst_atTop [NoMaxOrder Î±] [ClosedIciTopology Î±] (c : F) (ly : Filter E) :
    (fun (_ : Î± Ã— E) â†¦ c) =o[atTop Ã—Ë¢ ly] Prod.fst := by
  refine ly.eq_or_neBot.casesOn (fun h â†¦ by simp [h]) (fun _ â†¦ ?_)
  show ((fun _ â†¦ c) âˆ˜ Prod.fst) =o[atTop Ã—Ë¢ ly] (id âˆ˜ Prod.fst)
  rewrite [â† isLittleO_map, map_fst_prod]
  exact isLittleO_const_id_atTop2 c

theorem isLittleO_const_snd_atTop [NoMaxOrder Î±] [ClosedIciTopology Î±] (c : F) (lx : Filter E) :
    (fun (_ : E Ã— Î±) â†¦ c) =o[lx Ã—Ë¢ atTop] Prod.snd := by
  refine lx.eq_or_neBot.casesOn (fun h â†¦ by simp [h]) (fun _ â†¦ ?_)
  show ((fun _ â†¦ c) âˆ˜ Prod.snd) =o[lx Ã—Ë¢ atTop] (id âˆ˜ Prod.snd)
  rewrite [â† isLittleO_map, map_snd_prod]
  exact isLittleO_const_id_atTop2 c

end Order

section ContinuousOn

variable [TopologicalSpace Î¹] {C : Î¹ â†’ E} {c : F}

section IsBigO

variable [SeminormedAddGroup E] [Norm F]

/-- A family of constant functions `f (i, x) = C i` is uniformly bounded w.r.t. `s` by
`â¨† i âˆˆ s, â€–C iâ€–`, if `s` is compact and `C` is continuous. -/
theorem _root_.ContinuousOn.const_isBigOWithUniformlyOn_isCompact
    (hf : ContinuousOn C s) (hs : IsCompact s) (hc : â€–câ€– â‰  0) (l : Filter Î±) :
    IsBigOWith (sSup (norm '' (C '' s)) / â€–câ€–) (ğ“Ÿ s Ã—Ë¢ l)
    (fun (i, _x) â†¦ C i) fun _ => c := by
  refine isBigOWith_iff.mpr <| eventually_of_mem ?_ (fun x hx â†¦ ?_) (U := s Ã—Ë¢ Set.univ)
  Â· exact prod_mem_prod (mem_principal_self s) univ_mem
  Â· rw [div_mul_cancelâ‚€ _ hc]
    replace hs := hs.image_of_continuousOn hf |>.image continuous_norm
    have h_sSup := hs.isLUB_sSup <| Set.image_nonempty.mpr <| Set.image_nonempty.mpr âŸ¨x.1, hx.1âŸ©
    exact h_sSup.1 <| Set.mem_image_of_mem _ <| Set.mem_image_of_mem _ hx.1

/-- A family of constant functions `f (i, x) = C i` is uniformly O(1) w.r.t. `s`,
if `s` is compact and `C` is continuous. -/
theorem _root_.ContinuousOn.const_isBigOUniformlyOn_isCompact
    (hf : ContinuousOn C s) (hs : IsCompact s) (hc : â€–câ€– â‰  0) (l : Filter Î±) :
    (fun (i, _x) â†¦ C i) =O[ğ“Ÿ s Ã—Ë¢ l] fun _ => c :=
  (hf.const_isBigOWithUniformlyOn_isCompact hs hc l).isBigO

end IsBigO

section IsTheta

variable [NormedAddGroup E] [SeminormedAddGroup F]

/-- A family of constant functions `f (i, x) = C i` is uniformly bounded below w.r.t. `s` by
`âŠ“ i âˆˆ s, â€–C iâ€–`, if `s` is compact and `C` is continuous. -/
theorem _root_.ContinuousOn.const_isBigOWithUniformlyOn_isCompact_rev
    (hf : ContinuousOn C s) (hs : IsCompact s) (hC : âˆ€ i âˆˆ s, C i â‰  0) (l : Filter Î±) :
    IsBigOWith (â€–câ€– / sInf (norm '' (C '' s))) (ğ“Ÿ s Ã—Ë¢ l)
    (fun _ => c) fun (i, _x) â†¦ C i := by
  refine isBigOWith_iff.mpr <| eventually_of_mem ?_ (fun x hx â†¦ ?_) (U := s Ã—Ë¢ Set.univ)
  Â· exact prod_mem_prod (mem_principal_self s) univ_mem
  Â· rewrite [mul_comm_div]
    replace hs := hs.image_of_continuousOn hf |>.image continuous_norm
    have h_sInf := hs.isGLB_sInf <| Set.image_nonempty.mpr <| Set.image_nonempty.mpr âŸ¨x.1, hx.1âŸ©
    refine le_mul_of_one_le_right (norm_nonneg c) <| (one_le_div ?_).mpr <|
      h_sInf.1 <| Set.mem_image_of_mem _ <| Set.mem_image_of_mem _ hx.1
    obtain âŸ¨_, âŸ¨x, hx, hCxâŸ©, hnormCxâŸ© := hs.sInf_mem h_sInf.nonempty
    rewrite [â† hnormCx, â† hCx]
    exact (norm_ne_zero_iff.mpr (hC x hx)).symm.lt_of_le (norm_nonneg _)

/-- A family of constant functions `f (i, x) = C i` is uniformly Î©(1) w.r.t. `s`,
if `s` is compact and `C` is continuous with no zeros on `s`. -/
theorem _root_.ContinuousOn.const_isBigOUniformlyOn_isCompact_rev
    (hf : ContinuousOn C s) (hs : IsCompact s) (hC : âˆ€ i âˆˆ s, C i â‰  0) (l : Filter Î±) :
    (fun _ => c) =O[ğ“Ÿ s Ã—Ë¢ l] (fun (i, _x) â†¦ C i) :=
  (hf.const_isBigOWithUniformlyOn_isCompact_rev hs hC l).isBigO

/-- A family of constant functions `f (i, x) = C i` is uniformly Î˜(1) w.r.t. `s`,
if `s` is compact and `C` is continuous with no zeros on `s`. -/
theorem _root_.ContinuousOn.const_isThetaUniformlyOn_isCompact (hf : ContinuousOn C s)
    (hs : IsCompact s) (hc : â€–câ€– â‰  0) (hC : âˆ€ i âˆˆ s, C i â‰  0) (l : Filter Î±) :
    (fun (i, _x) â†¦ C i) =Î˜[ğ“Ÿ s Ã—Ë¢ l] fun _ => c :=
  âŸ¨hf.const_isBigOUniformlyOn_isCompact hs hc l, hf.const_isBigOUniformlyOn_isCompact_rev hs hC lâŸ©

end IsTheta

end ContinuousOn

section Integral -- TODO: move to Integral/Asymptotics.lean

open MeasureTheory

variable [MeasurableSpace Î¹] [NormedAddCommGroup E] {f : Î¹ Ã— Î± â†’ E} {g : Î± â†’ F}
  {Î¼ : Measure Î¹} {l : Filter Î±}

/-- Let `f : X x Y â†’ Z`. If as y â†’ l, f(x, y) = O(g(y)) uniformly on `s : Set X` of finite measure,
then f is eventually (as y â†’ l) integrable along `s`. -/
theorem IsBigO.eventually_integrableOn [Norm F]
    (hf : f =O[ğ“Ÿ s Ã—Ë¢ l] fun (_i, x) â†¦ g x)
    (hfm : âˆ€á¶  x in l, AEStronglyMeasurable (fun i â†¦ f (i, x)) (Î¼.restrict s))
    (hs : MeasurableSet s) (hÎ¼ : Î¼ s < âŠ¤) :
    âˆ€á¶  x in l, IntegrableOn (fun i â†¦ f (i, x)) s Î¼ := by
  obtain âŸ¨C, hCâŸ© := hf.bound
  obtain âŸ¨t, htl, htâŸ© := hC.exists_mem
  obtain âŸ¨u, hu, v, hv, huvâŸ© := Filter.mem_prod_iff.mp htl
  obtain âŸ¨w, hwl, hwâŸ© := hfm.exists_mem
  refine eventually_iff_exists_mem.mpr âŸ¨w âˆ© v, inter_mem hwl hv, fun x hx â†¦ ?_âŸ©
  haveI : IsFiniteMeasure (Î¼.restrict s) :=
    âŸ¨by convert hÎ¼ using 1; exact Measure.restrict_apply_univ sâŸ©
  refine Integrable.mono' (integrable_const (C * â€–g xâ€–)) (hw x hx.1) ?_
  filter_upwards [MeasureTheory.self_mem_ae_restrict hs]
  intro y hy
  exact ht (y, x) <| huv âŸ¨hu hy, hx.2âŸ©

variable [NormedSpace â„ E] [NormedAddCommGroup F] [NormedSpace â„ F]

/-- Let `f : X x Y â†’ Z`. If as y â†’ l, f(x, y) = O(g(y)) uniformly on `s : Set X` of finite measure,
then the integral of f along s is O(g(y)). -/
theorem IsBigO.set_integral_isBigO
    (hf : f =O[ğ“Ÿ s Ã—Ë¢ l] fun (_i, x) â†¦ g x) (hs : MeasurableSet s) (hÎ¼ : Î¼ s < âŠ¤)  :
    (fun x â†¦ âˆ« i in s, f (i, x) âˆ‚Î¼) =O[l] g := by
  obtain âŸ¨C, hCâŸ© := hf.bound
  obtain âŸ¨t, htl, htâŸ© := hC.exists_mem
  obtain âŸ¨u, hu, v, hv, huvâŸ© := Filter.mem_prod_iff.mp htl
  refine isBigO_iff.mpr âŸ¨C * (Î¼ s).toReal, eventually_iff_exists_mem.mpr âŸ¨v, hv, fun x hx â†¦ ?_âŸ©âŸ©
  rewrite [mul_assoc, â† smul_eq_mul (a' := â€–g xâ€–), â† MeasureTheory.Measure.restrict_apply_univ,
    â† integral_const, mul_comm, â† smul_eq_mul, â† integral_smul_const]
  refine (norm_integral_le_integral_norm _).trans <|
    integral_mono_of_nonneg (univ_mem' fun _ â†¦ norm_nonneg _) ?_ ?_
  Â· haveI : IsFiniteMeasure (Î¼.restrict s) :=
      âŸ¨by convert hÎ¼ using 1; exact Measure.restrict_apply_univ sâŸ©
    exact integrable_const _
  Â· filter_upwards [MeasureTheory.self_mem_ae_restrict hs]
    intro y hy
    rewrite [smul_eq_mul, mul_comm]
    exact ht (y, x) <| huv âŸ¨hu hy, hxâŸ©

end Integral
