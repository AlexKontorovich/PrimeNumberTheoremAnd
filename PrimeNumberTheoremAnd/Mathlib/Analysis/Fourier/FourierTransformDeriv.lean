/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, David Loeffler, Heather Macbeth
-/
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import PrimeNumberTheoremAnd.Mathlib.Analysis.Calculus.LineDeriv.IntegrationByParts
import PrimeNumberTheoremAnd.Mathlib.MeasureTheory.Function.L1Space

import Mathlib.Analysis.Fourier.FourierTransformDeriv

/-!
# Derivatives of the Fourier transform

In this file we compute the FrÃ©chet derivative of the Fourier transform of `f`, where `f` is a
function such that both `f` and `v â†¦ â€–vâ€– * â€–f vâ€–` are integrable. Here the Fourier transform is
understood as an operator `(V â†’ E) â†’ (W â†’ E)`, where `V` and `W` are normed `â„`-vector spaces
and the Fourier transform is taken with respect to a continuous `â„`-bilinear
pairing `L : V Ã— W â†’ â„` and a given reference measure `Î¼`.

We also investigate higher derivatives: Assuming that `â€–vâ€–^n * â€–f vâ€–` is integrable, we show
that the Fourier transform of `f` is `C^n`.

We also study in a parallel way the Fourier transform of the derivative, which is obtained by
tensoring the Fourier transform of the original function with the bilinear form.

We give specialized versions of these results on inner product spaces (where `L` is the scalar
product) and on the real line, where we express the one-dimensional derivative in more concrete
terms, as the Fourier transform of `-2Ï€I x * f x` (or `(-2Ï€I x)^n * f x` for higher derivatives).

## Main definitions and results

We introduce two convenience definitions:

* `VectorFourier.fourierSMulRight L f`: given `f : V â†’ E` and `L` a bilinear pairing
  between `V` and `W`, then this is the function `fun v â†¦ -(2 * Ï€ * I) (L v â¬) â€¢ f v`,
  from `V` to `Hom (W, E)`.
  This is essentially `ContinousLinearMap.smulRight`, up to the factor `- 2Ï€I` designed to make sure
  that the Fourier integral of `fourierSMulRight L f` is the derivative of the Fourier
  integral of `f`.
* `VectorFourier.fourierPowSMulRight` is the higher order analogue for higher derivatives:
  `fourierPowSMulRight L f v n` is informally `(-(2 * Ï€ * I))^n (L v â¬)^n â€¢ f v`, in
  the space of continuous multilinear maps `W [Ã—n]â†’L[â„] E`.

With these definitions, the statements read as follows, first in a general context
(arbitrary `L` and `Î¼`):

* `VectorFourier.hasFDerivAt_fourierIntegral`: the Fourier integral of `f` is differentiable, with
    derivative the Fourier integral of `fourierSMulRight L f`.
* `VectorFourier.differentiable_fourierIntegral`: the Fourier integral of `f` is differentiable.
* `VectorFourier.fderiv_fourierIntegral`: formula for the derivative of the Fourier integral of `f`.
* `VectorFourier.fourierIntegral_fderiv`: formula for the Fourier integral of the derivative of `f`.
* `VectorFourier.hasFTaylorSeriesUpTo_fourierIntegral`: under suitable integrability conditions,
  the Fourier integral of `f` has an explicit Taylor series up to order `N`, given by the Fourier
  integrals of `fun v â†¦ fourierPowSMulRight L f v n`.
* `VectorFourier.contDiff_fourierIntegral`: under suitable integrability conditions,
  the Fourier integral of `f` is `C^n`.
* `VectorFourier.iteratedFDeriv_fourierIntegral`: under suitable integrability conditions,
  explicit formula for the `n`-th derivative of the Fourier integral of `f`, as the Fourier
  integral of `fun v â†¦ fourierPowSMulRight L f v n`.

These statements are then specialized to the case of the usual Fourier transform on
finite-dimensional inner product spaces with their canonical Lebesgue measure (covering in
particular the case of the real line), replacing the namespace `VectorFourier` by
the namespace `Real` in the above statements.

We also give specialized versions of the one-dimensional real derivative (and iterated derivative)
in `Real.deriv_fourierIntegral` and `Real.iteratedDeriv_fourierIntegral`.
-/

noncomputable section

open Real Complex MeasureTheory Filter TopologicalSpace

open scoped FourierTransform Topology BigOperators

-- without this local instance, Lean tries first the instance
-- `secondCountableTopologyEither_of_right` (whose priority is 100) and takes a very long time to
-- fail. Since we only use the left instance in this file, we make sure it is tried first.
attribute [local instance 101] secondCountableTopologyEither_of_left

namespace Real

lemma differentiable_fourierChar : Differentiable â„ (ğ Â· : â„ â†’ â„‚) :=
  fun x â†¦ (Real.hasDerivAt_fourierChar x).differentiableAt

lemma deriv_fourierChar (x : â„) : deriv (ğ Â· : â„ â†’ â„‚) x = 2 * Ï€ * I * ğ x :=
  (Real.hasDerivAt_fourierChar x).deriv

variable {V W : Type*} [NormedAddCommGroup V] [NormedSpace â„ V]
  [NormedAddCommGroup W] [NormedSpace â„ W] (L : V â†’L[â„] W â†’L[â„] â„)

lemma hasFDerivAt_fourierChar_neg_bilinear_right (v : V) (w : W) :
    HasFDerivAt (fun w â†¦ (ğ (-L v w) : â„‚))
      ((-2 * Ï€ * I * ğ (-L v w)) â€¢ (ofRealCLM âˆ˜L (L v))) w := by
  have ha : HasFDerivAt (fun w' : W â†¦ L v w') (L v) w := ContinuousLinearMap.hasFDerivAt (L v)
  convert (hasDerivAt_fourierChar (-L v w)).hasFDerivAt.comp w ha.neg
  ext y
  simp only [neg_mul, ContinuousLinearMap.coe_smul', ContinuousLinearMap.coe_comp', Pi.smul_apply,
    Function.comp_apply, ofRealCLM_apply, smul_eq_mul, ContinuousLinearMap.comp_neg,
    ContinuousLinearMap.neg_apply, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.one_apply, real_smul, neg_inj]
  ring

lemma fderiv_fourierChar_neg_bilinear_right_apply (v : V) (w y : W) :
    fderiv â„ (fun w â†¦ (ğ (-L v w) : â„‚)) w y = -2 * Ï€ * I * L v y * ğ (-L v w) := by
  simp [(hasFDerivAt_fourierChar_neg_bilinear_right L v w).fderiv]
  ring

lemma differentiable_fourierChar_neg_bilinear_right (v : V) :
    Differentiable â„ (fun w â†¦ (ğ (-L v w) : â„‚)) :=
  fun w â†¦ (hasFDerivAt_fourierChar_neg_bilinear_right L v w).differentiableAt

lemma hasFDerivAt_fourierChar_neg_bilinear_left (v : V) (w : W) :
    HasFDerivAt (fun v â†¦ (ğ (-L v w) : â„‚))
      ((-2 * Ï€ * I * ğ (-L v w)) â€¢ (ofRealCLM âˆ˜L (L.flip w))) v :=
  hasFDerivAt_fourierChar_neg_bilinear_right L.flip w v

lemma fderiv_fourierChar_neg_bilinear_left_apply (v y : V) (w : W) :
    fderiv â„ (fun v â†¦ (ğ (-L v w) : â„‚)) v y = -2 * Ï€ * I * L y w * ğ (-L v w) := by
  simp [(hasFDerivAt_fourierChar_neg_bilinear_left L v w).fderiv]
  ring

lemma differentiable_fourierChar_neg_bilinear_left (w : W) :
    Differentiable â„ (fun v â†¦ (ğ (-L v w) : â„‚)) :=
  fun v â†¦ (hasFDerivAt_fourierChar_neg_bilinear_left L v w).differentiableAt

end Real

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

namespace VectorFourier

variable {V W : Type*} [NormedAddCommGroup V] [NormedSpace â„ V]
  [NormedAddCommGroup W] [NormedSpace â„ W] (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E)

variable {f}

/-- The Fourier integral of the derivative of a function is obtained by multiplying the Fourier
integral of the original function by `-L w v`. -/
theorem fourierIntegral_fderiv [MeasurableSpace V] [BorelSpace V] [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼]
    (hf : Integrable f Î¼) (h'f : Differentiable â„ f) (hf' : Integrable (fderiv â„ f) Î¼) :
    fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fderiv â„ f)
      = fourierSMulRight (-L.flip) (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) := by
  ext w y
  let g : V â†’ â„‚ := fun v â†¦ ğ (-L v w)
  have J : Integrable (fun v â†¦ ğ (-(L v) w) â€¢ fderiv â„ f v) Î¼ :=
    (fourierIntegral_convergent_iff' _ _).2 hf'
  /- First rewrite things in a simplified form, without any real change. -/
  suffices âˆ« x, g x â€¢ fderiv â„ f x y âˆ‚Î¼ = âˆ« x, (2 * â†‘Ï€ * I * L y w * g x) â€¢ f x âˆ‚Î¼ by
    simpa only [fourierIntegral, ContinuousLinearMap.toLinearMapâ‚‚_apply,
      ContinuousLinearMap.integral_apply J, ContinuousLinearMap.coe_smul', Pi.smul_apply,
      fourierSMulRight_apply, ContinuousLinearMap.neg_apply, ContinuousLinearMap.flip_apply, â†
      integral_smul, neg_smul, smul_neg, â† smul_smul, Complex.coe_smul, neg_neg]
  have A x : fderiv â„ g x y = - 2 * â†‘Ï€ * I * L y w * g x :=
    fderiv_fourierChar_neg_bilinear_left_apply _ _ _ _
  /- Key step: integrate by parts with respect to `y` to switch the derivative from `f` to `g`. -/
  rw [integral_smul_fderiv_eq_neg_fderiv_smul_of_integrable, â† integral_neg]
  Â· congr with x
    simp only [A, neg_mul, neg_smul, neg_neg]
  Â· have : Integrable (fun x â†¦ (-(2 * â†‘Ï€ * I * â†‘((L y) w)) â€¢ ((g x : â„‚) â€¢ f x))) Î¼ :=
      ((fourierIntegral_convergent_iff' _ _).2 hf).smul _
    convert this using 2 with x
    simp [A, smul_smul]
  Â· exact (fourierIntegral_convergent_iff' _ _).2 (hf'.apply_continuousLinearMap _)
  Â· exact (fourierIntegral_convergent_iff' _ _).2 hf
  Â· exact differentiable_fourierChar_neg_bilinear_left _ _
  Â· exact h'f

open ContinuousMultilinearMap

variable [SecondCountableTopology V] [MeasurableSpace V] [BorelSpace V] {Î¼ : Measure V}

/-- The Fourier integral of the `n`-th derivative of a function is obtained by multiplying the
Fourier integral of the original function by `(2Ï€I L w â¬ )^n`. -/
theorem fourierIntegral_iteratedFDeriv [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼] {N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedFDeriv â„ n f) Î¼) {n : â„•} (hn : n â‰¤ N) :
    fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (iteratedFDeriv â„ n f)
      = (fun w â†¦ fourierPowSMulRight (-L.flip) (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) w n) := by
  induction n with
  | zero =>
    ext w m
    have I : Integrable (fun v â†¦ ğ (- L v w) â€¢ iteratedFDeriv â„ 0 f v) Î¼ :=
      (fourierIntegral_convergent_iff' _ _).2 (h'f 0 bot_le)
    simp only [Nat.zero_eq, fourierIntegral, ContinuousLinearMap.toLinearMapâ‚‚_apply,
      integral_apply I, smul_apply, iteratedFDeriv_zero_apply, fourierPowSMulRight_apply, pow_zero,
      Finset.univ_eq_empty, ContinuousLinearMap.neg_apply, ContinuousLinearMap.flip_apply,
      Finset.prod_empty, one_smul]
  | succ n ih =>
    ext w m
    -- instance on next line should not be necessary, but proof breaks down without it.
    let NS : NormedSpace â„ (V [Ã—n]â†’L[â„] E) := by infer_instance
    have J : Integrable (fderiv â„ (iteratedFDeriv â„ n f)) Î¼ := by
      specialize h'f (n + 1) hn
      simp_rw [iteratedFDeriv_succ_eq_comp_left] at h'f
      exact (LinearIsometryEquiv.integrable_comp_iff _).1 h'f
    suffices H : (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fderiv â„ (iteratedFDeriv â„ n f)) w)
          (m 0) (Fin.tail m) =
        (-(2 * Ï€ * I)) ^ (n + 1) â€¢ (âˆ x : Fin (n + 1), -L (m x) w) â€¢ âˆ« v, ğ (-L v w) â€¢ f v âˆ‚Î¼ by
      have A : âˆ« v, ğ (-L v w) â€¢ (fderiv â„ (iteratedFDeriv â„ n f) v (m 0)) (Fin.tail m) âˆ‚Î¼
          = (âˆ« v, ğ (-L v w) â€¢ (fderiv â„ (iteratedFDeriv â„ n f) v (m 0)) âˆ‚Î¼) (Fin.tail m) := by
        rw [integral_apply]
        Â· simp only [smul_apply]
        Â· exact (fourierIntegral_convergent_iff' L w).2 (J.apply_continuousLinearMap _)
      have B : âˆ« v, ğ (-L v w) â€¢ (fderiv â„ (iteratedFDeriv â„ n f) v (m 0)) âˆ‚Î¼ =
          (âˆ« v, ğ (-L v w) â€¢ (fderiv â„ (iteratedFDeriv â„ n f) v) âˆ‚Î¼) (m 0) := by
        rw [ContinuousLinearMap.integral_apply]
        Â· simp only [ContinuousLinearMap.coe_smul', Pi.smul_apply]
        Â· exact (fourierIntegral_convergent_iff' L w).2 J
      simp only [fourierIntegral, ContinuousLinearMap.toLinearMapâ‚‚_apply,
        integral_apply ((fourierIntegral_convergent_iff' L w).2 (h'f _ hn)), smul_apply,
        iteratedFDeriv_succ_apply_left, fourierPowSMulRight_apply, ContinuousLinearMap.neg_apply,
        ContinuousLinearMap.flip_apply, A, B]
      exact H
    have h'n : n < N := lt_of_lt_of_le (by simp [-Nat.cast_succ]) hn
    rw [fourierIntegral_fderiv]
    Â· have A : âˆ€ (x : â„) (v : E), x â€¢ v = (x : â„‚) â€¢ v := fun x v â†¦ rfl
      simp only [ih h'n.le, fourierSMulRight_apply, ContinuousLinearMap.neg_apply,
        ContinuousLinearMap.flip_apply, neg_smul, smul_neg, neg_neg, smul_apply,
        fourierPowSMulRight_apply, A, smul_smul]
      congr 1
      have B : âˆ€ (i : Fin n), Fin.tail m i = m (Fin.succ i) := fun i â†¦ rfl
      simp only [ofReal_prod, ofReal_neg, pow_succ, mul_neg, Fin.prod_univ_succ, neg_mul,
        ofReal_mul, neg_neg, B]
      ring
    Â· exact h'f n h'n.le
    Â· exact hf.differentiable_iteratedFDeriv h'n
    Â· exact J

end VectorFourier

namespace Real
open VectorFourier

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V] {f : V â†’ E}

/-- The Fourier integral of the FrÃ©chet derivative of a function is obtained by multiplying the
Fourier integral of the original function by `2Ï€I âŸªv, wâŸ«`. -/
theorem fourierIntegral_fderiv
    (hf : Integrable f) (h'f : Differentiable â„ f) (hf' : Integrable (fderiv â„ f)) :
    ğ“• (fderiv â„ f) = fourierSMulRight (-innerSL â„) (ğ“• f) := by
  rw [â† innerSL_real_flip V]
  exact VectorFourier.fourierIntegral_fderiv (innerSL â„) hf h'f hf'

/-- The Fourier integral of the `n`-th derivative of a function is obtained by multiplying the
Fourier integral of the original function by `(2Ï€I L w â¬ )^n`. -/
theorem fourierIntegral_iteratedFDeriv {N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedFDeriv â„ n f)) {n : â„•} (hn : n â‰¤ N) :
    ğ“• (iteratedFDeriv â„ n f)
      = (fun w â†¦ fourierPowSMulRight (-innerSL â„) (ğ“• f) w n) := by
  rw [â† innerSL_real_flip V]
  exact VectorFourier.fourierIntegral_iteratedFDeriv (innerSL â„) hf h'f hn

/-- The Fourier integral of the FrÃ©chet derivative of a function is obtained by multiplying the
Fourier integral of the original function by `2Ï€I x`. -/
theorem fourierIntegral_deriv
    {f : â„ â†’ E} (hf : Integrable f) (h'f : Differentiable â„ f) (hf' : Integrable (deriv f)) :
    ğ“• (deriv f) = fun (x : â„) â†¦ (2 * Ï€ * I * x) â€¢ (ğ“• f x) := by
  ext x
  have I : Integrable (fun x â†¦ fderiv â„ f x) := by
    simp_rw [â† deriv_fderiv]
    change Integrable (fun x â†¦ ContinuousLinearMap.smulRightL _ _ _ 1 (deriv f x)) volume
    apply ContinuousLinearMap.integrable_comp _ hf'
  have : ğ“• (deriv f) x = ğ“• (fderiv â„ f) x 1 := by
    simp_rw [fourierIntegral_eq, deriv,
      ContinuousLinearMap.integral_apply ((fourierIntegral_convergent_iff _).2 I)]
    rfl
  rw [this, fourierIntegral_fderiv hf h'f I]
  have : x â€¢ ğ“• f x = (x : â„‚) â€¢ ğ“• f x := rfl
  simp only [fourierSMulRight_apply, ContinuousLinearMap.neg_apply, innerSL_apply, smul_smul,
    RCLike.inner_apply, conj_trivial, mul_one, neg_smul, smul_neg, neg_neg, neg_mul, this]

theorem fourierIntegral_iteratedDeriv {f : â„ â†’ E} {N : â„•âˆ} {n : â„•} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedDeriv n f)) (hn : n â‰¤ N) :
    ğ“• (iteratedDeriv n f) = fun (x : â„) â†¦ (2 * Ï€ * I * x) ^ n â€¢ (ğ“• f x) := by
  ext x : 1
  have A : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedFDeriv â„ n f) := by
    intro n hn
    rw [iteratedFDeriv_eq_equiv_comp]
    exact (LinearIsometryEquiv.integrable_comp_iff _).2 (h'f n hn)
  have B : ğ“• (fun x â†¦ (iteratedFDeriv â„ n f x) (fun i â†¦ 1)) x =
      ğ“• (iteratedFDeriv â„ n f) x (fun i â†¦ 1) := by
    rw [fourierIntegral_eq, fourierIntegral_eq, ContinuousMultilinearMap.integral_apply]
    Â· rfl
    Â· exact (fourierIntegral_convergent_iff _).2 (A n hn)
  have C : âˆ€ (c : â„) (v : E), c â€¢ v = (c : â„‚) â€¢ v := fun c v â†¦ rfl
  change ğ“• (fun x â†¦ iteratedDeriv n f x) x = _
  simp_rw [iteratedDeriv, B, fourierIntegral_iteratedFDeriv hf A hn]
  simp [C, smul_smul, â† mul_pow]

end Real
