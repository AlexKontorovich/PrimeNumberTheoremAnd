/-
Copyright (c) 2025 Maksym Radziwill. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Maksym Radziwill
-/

import Mathlib.NumberTheory.VonMangoldt
import Mathlib.NumberTheory.ArithmeticFunction
import Mathlib.Topology.EMetricSpace.Defs
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Data.Complex.Basic
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.SpecialFunctions.Pow.Deriv
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Analytic.Within
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Analysis.Complex.AbsMax


noncomputable abbrev divRemovable_zero (f : ‚ÑÇ ‚Üí ‚ÑÇ) : ‚ÑÇ ‚Üí ‚ÑÇ :=
  Function.update (fun z ‚Ü¶ (f z) / z) 0 ((deriv f) 0)

-- Away from zero divRemovable_zero f z is equal to f z / z

lemma divRemovable_zero_of_ne_zero {z : ‚ÑÇ} (f : ‚ÑÇ ‚Üí ‚ÑÇ) (z_ne_0 : z ‚â† 0) : divRemovable_zero f z = f z / z := by 
  unfold divRemovable_zero; apply Function.update_of_ne z_ne_0

-- If f is analytic on an open set and f 0 = 0 then f z / z is also
-- analytic on the same open set.

lemma AnalyticOn.divRemovable_zero {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ}
  (sInNhds0 : s ‚àà nhds 0) (zero : f 0 = 0) (o : IsOpen s)
  (analytic : AnalyticOn ‚ÑÇ f s) : AnalyticOn ‚ÑÇ (divRemovable_zero f) s :=
  by
     rw [Complex.analyticOn_iff_differentiableOn o]
     rw [‚Üê(Complex.differentiableOn_compl_singleton_and_continuousAt_iff sInNhds0)]
     constructor
     ¬∑ rw [differentiableOn_congr (by intros; apply Function.update_of_ne; grind)]
       exact DifferentiableOn.fun_div
         (AnalyticOn.differentiableOn (AnalyticOn.mono analytic Set.diff_subset))
         (DifferentiableOn.mono (differentiableOn_id (s := Set.univ))
         (Set.subset_univ (s \ {0}))) (by grind)

     ¬∑ have U := HasDerivAt.continuousAt_div (c := 0) (a := (deriv f) 0) (f := f)
         (DifferentiableOn.hasDerivAt
            ((Complex.analyticOn_iff_differentiableOn o).mp analytic) sInNhds0)
       have T : (fun (x : ‚ÑÇ) ‚Ü¶ (f x - 0) / (x - 0)) = (fun (x : ‚ÑÇ) ‚Ü¶ (f x) / x) := by funext; grind
       rw [zero, T] at U; exact U

-- The proof of the Lemma below is cumbersome, a proper way would be to
-- show that if f is analytic on a closed set C, then it is analytic on an
-- open set O containing the closed set C and apply the previous lemma.

lemma AnalyticOn_divRemovable_zero_closedBall {f : ‚ÑÇ ‚Üí ‚ÑÇ} {R : ‚Ñù} 
  (Rpos : 0 < R) (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R)) 
  (zero : f 0 = 0): AnalyticOn ‚ÑÇ (divRemovable_zero f) (Metric.closedBall 0 R) := by
    apply analyticOn_of_locally_analyticOn
    intro x x_hyp
    by_cases h : ‚Äñx‚Äñ = R
    ¬∑ use Metric.ball x (R / 2)
      constructor
      ¬∑ exact Metric.isOpen_ball
      ¬∑ constructor
        ¬∑ simp; positivity
        ¬∑ have Z : ‚àÄw ‚àà Metric.closedBall 0 R ‚à© Metric.ball x (R / 2), divRemovable_zero f w = f w / w := by
             intro x‚ÇÇ hyp_x‚ÇÇ
             apply divRemovable_zero_of_ne_zero
             simp [ball_eq] at hyp_x‚ÇÇ
             rw [‚Üê norm_pos_iff]
             calc ‚Äñx‚ÇÇ‚Äñ
               _ = ‚Äñx - (x - x‚ÇÇ)‚Äñ := by simp
               _ ‚â• ‚Äñx‚Äñ - ‚Äñx - x‚ÇÇ‚Äñ := by apply norm_sub_norm_le
               _ = R - ‚Äñx‚ÇÇ - x‚Äñ := by simp [h, norm_sub_rev]
               _ > 0 := by linarith

          apply AnalyticOn.congr 
          ¬∑ apply AnalyticOn.div (AnalyticOn.mono analytic Set.inter_subset_left) analyticOn_id 
            ¬∑ intro x‚ÇÅ hyp_x‚ÇÅ
              simp [ball_eq] at hyp_x‚ÇÅ
              rw [‚Üê norm_pos_iff]
              calc ‚Äñx‚ÇÅ‚Äñ
                   _ = ‚Äñx - (-(x‚ÇÅ - x))‚Äñ := by simp
                   _ ‚â• ‚Äñx‚Äñ - ‚Äñ-(x‚ÇÅ - x)‚Äñ := by apply norm_sub_norm_le
                   _ = R - ‚Äñx‚ÇÅ - x‚Äñ := by simp [h, norm_sub_rev]
                   _ > 0 := by linarith
          ¬∑ simp [Set.EqOn.eq_1]
            intro x‚ÇÉ hyp_x‚ÇÉ dist_hyp
            have : x‚ÇÉ ‚àà Metric.closedBall 0 R ‚à© Metric.ball x (R / 2) := by
              apply Set.mem_inter
              ¬∑ simp [hyp_x‚ÇÉ]
              ¬∑ rw [Metric.mem_ball]; exact dist_hyp
            exact Z x‚ÇÉ this

    ¬∑ use Metric.ball 0 R
      constructor
      ¬∑ exact Metric.isOpen_ball
      ¬∑ constructor
        ¬∑ simp [ball_eq]; simp at x_hyp; grind
        ¬∑ have si : Metric.closedBall (0 : ‚ÑÇ) R ‚à© Metric.ball (0 : ‚ÑÇ) R = Metric.ball (0 : ‚ÑÇ) R := by
            apply Set.inter_eq_self_of_subset_right
            simp at x_hyp
            exact Metric.ball_subset_closedBall
          ¬∑ rw [si]
            apply AnalyticOn.divRemovable_zero 
            ¬∑ apply Metric.ball_mem_nhds; positivity
            ¬∑ exact zero
            ¬∑ apply Metric.isOpen_ball
            ¬∑ apply AnalyticOn.mono (t := Metric.closedBall 0 R) (s := Metric.ball 0 R) analytic
              ¬∑ apply Metric.ball_subset_closedBall 

noncomputable abbrev schwartzQuotient (f : ‚ÑÇ ‚Üí ‚ÑÇ) (M : ‚Ñù) : ‚ÑÇ ‚Üí ‚ÑÇ :=
  fun z ‚Ü¶ (divRemovable_zero f z) / (2 * M - f z)

-- AnalyticOn.schwartzQuotient establishes that f_{M}(z) is analytic.

lemma AnalyticOn.schwartzQuotient {f : ‚ÑÇ ‚Üí ‚ÑÇ} {R : ‚Ñù} (M : ‚Ñù)
  (Rpos : 0 < R) (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
  (nonzero: ‚àÄz ‚àà Metric.closedBall 0 R, 2 * M - f z ‚â† 0)
  (zero : f 0 = 0): AnalyticOn ‚ÑÇ (schwartzQuotient f M) (Metric.closedBall 0 R) := by

  have sInNhds0 : Metric.closedBall 0 R ‚àà nhds 0 := by
    apply Metric.closedBall_mem_nhds; exact Rpos

  exact AnalyticOn.div
    (AnalyticOn_divRemovable_zero_closedBall Rpos
      analytic zero) (AnalyticOn.sub (analyticOn_const) analytic) nonzero

-- If Re x ‚â§ M then |x| ‚â§ |2 * M - x|, this simple inequality is used
-- in the proof of borelCaratheodory_closedBall.

lemma Complex.norm_le_norm_two_mul_sub_of_re_le {M : ‚Ñù} {x : ‚ÑÇ} 
  (Mpos : 0 < M) (hyp_re_x : x.re ‚â§ M) : ‚Äñx‚Äñ ‚â§ ‚Äñ2 * M - x‚Äñ := by
  have Z : M ^ 2 = M * M := by grind
  rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (by positivity)]
  repeat rw [Complex.sq_norm, Complex.normSq_apply]
  simp
  ring_nf
  simp [add_comm (-(x.re * M * 4)) (x.re ^ 2), add_assoc,
        le_add_iff_nonneg_right (x.re ^ 2), Z, mul_le_mul_iff_left‚ÇÄ Mpos]
  exact hyp_re_x

-- This is a version of the maximum modulus principle specialized to closed balls.

lemma AnalyticOn.norm_le_of_norm_le_on_sphere {f : ‚ÑÇ ‚Üí ‚ÑÇ} {C R r : ‚Ñù}  
  (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
  (hyp_r : r ‚â§ R) (cond : ‚àÄz ‚àà Metric.sphere 0 r, ‚Äñf z‚Äñ ‚â§ C) 
  (w : ‚ÑÇ) (wInS : w ‚àà Metric.closedBall 0 r) : ‚Äñf w‚Äñ ‚â§ C :=
    by
      apply Complex.norm_le_of_forall_mem_frontier_norm_le
              (U := Metric.closedBall 0 r) (Metric.isBounded_closedBall)
      ¬∑ apply DifferentiableOn.diffContOnCl; rw [Metric.closure_closedBall]
        apply AnalyticOn.differentiableOn
        apply AnalyticOn.mono (f := f) (s := Metric.closedBall 0 r) (t := Metric.closedBall 0 R) (ùïú := ‚ÑÇ) analytic
        ¬∑ apply Metric.closedBall_subset_closedBall; linarith
      ¬∑ rw [frontier_closedBall']; exact cond
      ¬∑ rw [Metric.closure_closedBall]; exact wInS

-- We can now prove Borel-Caratheodory for closed balls

/-%%
\begin{theorem}[BorelCaratheodory]\label{BorelCaratheodory}\lean{BorelCaratheodory}
    Let $R,\,M>0$. Let $f$ be analytic on $\abs{z}\leq R$ such that $f(0)=0$ and suppose
    $\mathfrak{R}f(z)\leq M$ for all $\abs{z}\leq R$. Then for any $0 < r < R$,
    $$\sup_{\abs{z}\leq r}\abs{f(z)}\leq\frac{2Mr}{R-r}.$$
\end{theorem}
%%-/

theorem borelCaratheodory_closedBall {M R r : ‚Ñù} {z : ‚ÑÇ} {f : ‚ÑÇ ‚Üí ‚ÑÇ} 
  (Rpos : 0 < R) (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
  (zeroAtZero: f 0 = 0) (Mpos : 0 < M)
  (realPartBounded: ‚àÄz ‚àà Metric.closedBall 0 R, (f z).re ‚â§ M) 
  (hyp_r : r < R) (hyp_z : z ‚àà Metric.closedBall 0 r)
  : ‚Äñf z‚Äñ ‚â§ (2 * M * r) / (R - r) := by

  have zInSFunc : ‚àÄr ‚â§ R, ‚àÄz ‚àà Metric.sphere (0 : ‚ÑÇ) r, z ‚àà Metric.closedBall (0 : ‚ÑÇ) R := by
      intro r hyp_r z hyp_z
      apply Set.mem_of_mem_of_subset (s := Metric.sphere 0 r) hyp_z
      ¬∑ calc Metric.sphere (0 : ‚ÑÇ) r
          _ ‚äÜ Metric.closedBall (0 : ‚ÑÇ) r := Metric.sphere_subset_closedBall
          _ ‚äÜ Metric.closedBall (0 : ‚ÑÇ) R := Metric.closedBall_subset_closedBall hyp_r

  have fPosAll : ‚àÄz ‚àà Metric.closedBall 0 R, 2 * M - f z ‚â† 0 := by
    intro z zInS
    exact Complex.ne_zero_of_re_pos (by simp; linarith [realPartBounded z zInS])

  have schwartzQuotientBounded : ‚àÄz ‚àà Metric.sphere 0 R, ‚ÄñschwartzQuotient f M z‚Äñ ‚â§ 1 / R := by
    intro z hyp_z
    have zNe0 : z ‚â† 0 := by
      rw [mem_sphere_zero_iff_norm] at hyp_z
      exact ne_zero_of_norm_ne_zero (by grind)
    have zInS : z ‚àà Metric.closedBall 0 R := zInSFunc R (by rfl) z hyp_z
    simp [mem_sphere_iff_norm] at hyp_z

    calc ‚ÄñschwartzQuotient f M z‚Äñ
      _ = (‚Äñf z‚Äñ / ‚Äñz‚Äñ) / ‚Äñ2 * M - f z‚Äñ := by simp [divRemovable_zero_of_ne_zero f zNe0]
      _ ‚â§ (‚Äñf z‚Äñ / ‚Äñz‚Äñ) / ‚Äñf z‚Äñ := by
        by_cases h : ‚Äñf z‚Äñ = 0;
        ¬∑ simp [h]
        ¬∑ exact div_le_div_of_nonneg_left (by positivity) (by positivity)
              (Complex.norm_le_norm_two_mul_sub_of_re_le Mpos (realPartBounded z zInS))
      _ ‚â§ (1 / ‚Äñz‚Äñ) := by
        by_cases h : ‚Äñf z‚Äñ = 0
        ¬∑ simp [h]
        ¬∑ rw [div_div, mul_comm, ‚Üê div_div, div_self]; exact h
      _ = 1 / R := by rw [hyp_z]

  have maxMod: ‚àÄz ‚àà Metric.closedBall 0 R, ‚ÄñschwartzQuotient f M z‚Äñ ‚â§ 1 / R := by
    exact AnalyticOn.norm_le_of_norm_le_on_sphere 
          (AnalyticOn.schwartzQuotient M Rpos
             analytic fPosAll zeroAtZero)
          (by rfl) schwartzQuotientBounded

  have boundForF : ‚àÄr < R, 0 < r ‚Üí ‚àÄz ‚àà Metric.sphere 0 r, ‚Äñf z‚Äñ ‚â§ 2 * M * r / (R - r) := by
    intro r hyp_r r_pos z zOnR
    have zInS : z ‚àà Metric.closedBall 0 R := zInSFunc r (by grind) z (zOnR)
    rw [mem_sphere_zero_iff_norm] at zOnR
    have := maxMod z zInS
    unfold schwartzQuotient at this
    have U : z ‚â† 0 := by rw [‚Üê norm_pos_iff]; linarith
    rw [divRemovable_zero_of_ne_zero f U] at this
    simp at this
    have U : 0 < r * ‚Äñ2 * M - f z‚Äñ := by simp [r_pos, fPosAll z zInS]
    simp [zOnR, div_div, div_le_iff‚ÇÄ' U ] at this
    have U0 : ‚Äñf z‚Äñ ‚â§ 2 * M * r / R + ( r / R ) * ‚Äñf z‚Äñ := by
      calc ‚Äñf z‚Äñ
        _ ‚â§ r * ‚Äñ2 * M - f z‚Äñ * R‚Åª¬π := this
        _ ‚â§ r * (‚Äñ(2 : ‚ÑÇ) * M‚Äñ + ‚Äñf z‚Äñ) * R‚Åª¬π := by
          gcongr; apply norm_sub_le (E := ‚ÑÇ) ((2 : ‚ÑÇ) * ‚ÜëM) (f z)
        _ = r * (2 * M + ‚Äñf z‚Äñ) * R‚Åª¬π := by
          have U : ‚Äñ(2 : ‚ÑÇ) * M‚Äñ = 2 * M := by simp; linarith
          rw [U]
        _ = 2 * M * r * R‚Åª¬π + r * ‚Äñf z‚Äñ * R‚Åª¬π := by grind
        _ = 2 * M * r / R + (r / R) * ‚Äñf z‚Äñ := by grind
    have U1 : ‚Äñf z‚Äñ - ‚Äñf z‚Äñ * (r * R‚Åª¬π) = ‚Äñf z‚Äñ * (1 - r * R‚Åª¬π) := by ring
    have U2 : (0 : ‚Ñù) < 1 - r * R‚Åª¬π := by
      have U1 : 0 < R := by linarith
      have U : r * R‚Åª¬π < 1 := by simp [‚Üê div_lt_one‚ÇÄ U1] at hyp_r; exact hyp_r
      linarith
    have U3 : r * R‚Åª¬π * M * 2 / (1 - r * R‚Åª¬π) = 2 * M * r / (R - r) := by grind

    rw [‚Üê sub_le_sub_iff_right ((r / R) * ‚Äñf z‚Äñ)] at U0; ring_nf at U0
    rw [mul_assoc, U1, ‚Üê le_div_iff‚ÇÄ U2, U3] at U0
    exact U0

  have maxBoundForF: ‚àÄr < R, 0 < r ‚Üí ‚àÄz ‚àà Metric.closedBall 0 r, ‚Äñf z‚Äñ ‚â§ 2 * M * r / (R - r) := by
    intro r hyp_r pos_r
    exact AnalyticOn.norm_le_of_norm_le_on_sphere
            analytic (by linarith) (boundForF r hyp_r pos_r)

  by_cases pos_r : r = 0
  ¬∑ have U : z = 0 := by simp [pos_r] at hyp_z; exact hyp_z
    rw [U, pos_r]; simp; exact zeroAtZero
  ¬∑ have U : 0 ‚â§ r := by
      rw [mem_closedBall_iff_norm] at hyp_z; simp at hyp_z; linarith [norm_nonneg z]
    exact maxBoundForF r (by grind) (by grind) z hyp_z
